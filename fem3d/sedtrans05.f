!****************************************************************************
!
!              SEDTRANS05:    A SEDIMENT TRANSPORT MODEL FOR
!                           CONTINENTAL SHELVES AND ESTUARIES
!
!****************************************************************************
! Sedtrans05 - a sediment transport model
! Preliminary test version
! Copyright (C) 2005 Urs Neumeier, Christian Ferrarin, Carl Amos
! and Georg Umgiesser.
! Sedtrans05 was developed at the ISMAR-CNR in Venice and the
! University of Southampton.
! Previous versions of Sedtrans (1992 and 1996) were developed at
! the Geological Survey of Canada, Bedford Institute of Oceanography.
!
! This program is free software; you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation; either version 2 of the License, or
! (at your option) any later version. See file "licence.txt" in the
! Sedtrans05 directory for the full licence text, which can also be
! found at http://www.gnu.org/licenses/
!
! This program is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
! **********************************************************************
!
! revision log :
!
! 01.08.2005    ccf	changes in FRICFAC, compute ripple if USTC<USTCRB
! 01.08.2005    ccf	no ripple prediction in case of cohesive sediment
! 14.02.2006    ccf	compute C0 with Van Rijn instead of Smith and McLean
! 13.03.2006    ccf	correct computation of USTC for cohesive
! 11.06.2006    ccf	correct Z0 in GETC01 and add KCOES
! 12.09.2006	ccf	in FRICFAC limit RKB <= D
! 30.01.2007	ccf	introduce Z0S in PROFL
! 21.12.2007	ccf	NBCONC as input variables
! 16.04.2008	ccf	bugfix in SETCCONST (new label 930)
! 20.10.2008	ccf	new routine for RIPPLE prediction in case of current 
! 01.04.2016	ccf	converted to module
! 01.04.2016	ccf	converted to module
! 22.02.2018	ccf	bug fix for TAUCD (wrong formula, look for TAUCD=)
!
!****************************************************************************

      MODULE mod_sedtrans05

      IMPLICIT NONE

      DOUBLE PRECISION, PARAMETER       :: G = 9.81             !GRAVITY
      DOUBLE PRECISION, PARAMETER       :: PII = 2.*ASIN(1d0)

! Constants for solid transmitted stress by Ulva
      DOUBLE PRECISION, SAVE    :: CSULVA   = 159.4d0   ! coefficient for the solid transmitted stress by Ulva
      DOUBLE PRECISION, SAVE    :: TMULVA   = 1.054d-3  ! threshold of motion of   Ulva (Pa)
      DOUBLE PRECISION, SAVE    :: TRULVA   = 0.0013d0  ! threshold of full resuspension of Ulva (Pa)
! Constants for erosion formula
      DOUBLE PRECISION, SAVE    :: RKERO    = 5.88d0    ! Erosion proportionality coefficient
      DOUBLE PRECISION, SAVE    :: E0       = 1.95d-5   ! Minimum erosion rate
! Constant for turbulent disruption
      DOUBLE PRECISION, SAVE    :: CDISRUPT = 0.001d0   ! constant for turbulent floc disruption during erosion
! Constants for the flocculation
      DOUBLE PRECISION, SAVE    :: CLIM1    = 0.1d0     ! lower limit for flocculation (kg/m**3)
      DOUBLE PRECISION, SAVE    :: CLIM2    = 2d0       ! limit between simple and complex flocculation equation (kg/m**3)
      DOUBLE PRECISION, SAVE    :: KFLOC    = 0.001d0   ! constant K for flocculation equation
      DOUBLE PRECISION, SAVE    :: MFLOC    = 1d0       ! constant M for flocculation equation
      DOUBLE PRECISION, SAVE    :: RHOCLAY  = 2600d0    ! density of clay mineral
! Constant for deposition formula
      DOUBLE PRECISION, SAVE    :: CTAUDEP  = 1d0       ! scaling factor for TAUCD
      DOUBLE PRECISION, SAVE    :: PRS      = 0d0       ! Resuspension probability (range 0-1)
! Constants for freshly deposited sediment
      DOUBLE PRECISION, SAVE    :: RHOMUD   = 50d0      ! Density of the freshly deposited mud
! Constants for density profile
!     FINAL(I) = A -  B * EXP(-C*ABOVE(I)) - D * EXP(-E*ABOVE(I))
!     A : final deep density
!     A-B-D : final surface density
!     C and E : define the shape (in conjunction with B and C)
      DOUBLE PRECISION, SAVE    :: DPROFA   = 470d0     ! constants for final density profile
      DOUBLE PRECISION, SAVE    :: DPROFB   = 150d0
      DOUBLE PRECISION, SAVE    :: DPROFC   = 0.015d0
      DOUBLE PRECISION, SAVE    :: DPROFD   = 0d0
      DOUBLE PRECISION, SAVE    :: DPROFE   = 0d0
! Constant for consolidation
      DOUBLE PRECISION, SAVE    :: CONSOA   = 1d-5      ! time constant of consolidation
! Constants for erosion threshold from density and overlaying mass
      DOUBLE PRECISION, SAVE    :: TEROA    = 6d-10     !constants for erosion threshold from density and overlaying mass
      DOUBLE PRECISION, SAVE    :: TEROB    = 3d0
      DOUBLE PRECISION, SAVE    :: TEROC    = 3.47d0
      DOUBLE PRECISION, SAVE    :: TEROD    = -1.915d0
! Constant for the sediment put in suspension
      INTEGER, SAVE             :: WSCLAY   = 5        ! primary median Ws class (must be in the range 1:NBCONC)

      INTEGER, SAVE             :: MEDDISTR = 3        ! median position of DISTR
      INTEGER, SAVE             :: DOCOMPACT= 0        ! if not zero, call COMPACT from COHESIVE
      INTEGER, SAVE             :: NDISTR   = 5        ! number of elements used in DISTR
! Constant for the drag reduction formula : TAU0effectif=TAU0*exp(CDRAGRED*SSC)
      DOUBLE PRECISION, SAVE    :: CDRAGRED = -0.0893d0! constant for the drag reduction formula

! Fracion of mud for sediment to be cohesive
      DOUBLE PRECISION, SAVE    :: Z0COH  = 2.0D-4  !BED ROUGHNESS LENGHT FOR COHESIVE SEDIMENTS
      DOUBLE PRECISION, SAVE    :: FCWCOH = 2.2D-3  !FRICTION FACTOR FOR COHESIVE SEDIMENTS

! Additional result from COHESIVE
      DOUBLE PRECISION, SAVE    :: TAU0EFF  !effective bed shear stress used in COHESIVE
                                !includes drag reduction and solid transm. stress by Ulva

! Those MUST be initialised with an call to INICONST before the first
! call to COHESIVE (or to first call to SEDISDGM with IOPT1=7)
      DOUBLE PRECISION, ALLOCATABLE, SAVE    :: DISTR(:)! (normal) distribution of sediment put in suspension
 			                                ! the sum of DISTR(1:NDISTR) MUST be exactly 1.0
      DOUBLE PRECISION, ALLOCATABLE, SAVE    :: WSI(:)  ! settling velocity of each Ws class (m/s)

!****************************************************************************

      CONTAINS

!****************************************************************************
!                  *** SUBROUTINE SEDTRANS05 ***
!****************************************************************************

      SUBROUTINE SEDTRANS05(D,UZ,Z,CDIR,HT,PER,WDIR,GD,BETA,RHOS,
     $RHOW,IOPT1,VISC,NBCONC,CONC,MAXBED,BEDCHA,TIMEDR,AULVA,    !**INPUT**
     $USTCRB,USTCRS,USTUP,UB,FCW,UA,U100,USTCWS,USTCW,Z0,RHEIGHT,
     $RLENGTH,QS,QSDIR,SEDM,SED,SEDDIR,FALL,C0,EDRATE,ZS,TAOS,
     $TCONC)		!**OUTPUT VARIABLES**

      IMPLICIT NONE

!************* INPUT VARIABLES ******************

      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION UZ       !AMBIENT CURRENT AT HEIGHT Z ABOVE THE SEAFLOOR (M/S)
      DOUBLE PRECISION Z        !HEIGHT OF UZ ABOVE SEAFLOOR
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION HT       !WAVE HEIGHT (M)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION BETA     !BED SLOPE (DEGREE)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION VISC     !DYNAMIC VISCOSITY OF THE FLUID (KG/M*SEC (OR N.S/M**2))

c cohesive
      INTEGER NBCONC            ! number of Ws classes = elements in CONC and in WSI
      DOUBLE PRECISION CONC(NBCONC) !COHESIVE SUSPENDED SEDIMENT CONCENTRATION (kg/m3)
      INTEGER MAXBED            ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                                ! (1) depth below sediment surface (m)
                                ! (2) critical erosion stress (Pa)
                                ! (3) dry bulk density (kg/m**3)
      DOUBLE PRECISION TIMEDR   !Duration of call to Sedtrans (s)
      DOUBLE PRECISION AULVA    !fraction of bed area covered by the algae 'Ulva' (range 0-1)

!************* OUTPUT VARIABLES *****************

      DOUBLE PRECISION AB       !EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
      DOUBLE PRECISION WL       !WAVE LENGTH (M)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTCRS   !CRITICAL SHEAR VEL FOR INITI OF SUSPENDED LOAD TRANSPORT (M/SEC)
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION FCW      !BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCW	!COMBINED TOTAL SHEAR VELOCITY OF GM
      DOUBLE PRECISION Z0       !BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION RHEIGHT	!PREDICTED RIPPLE HEIGHT
      DOUBLE PRECISION RLENGTH	!PREDICTED RIPPLE LENGTH
      DOUBLE PRECISION QS	!SUSPENDED SEDIMENT TRANSPORT RATE (KG/M/S)
      DOUBLE PRECISION QSDIR	!DIRECTION OF SUSPENDED SEDIMENT TRANSPORT (DEGREE)
      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDM	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS (KG/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION C0       !REFERENCE CONCENTRATION AT Z0 (KG/M^3)
      DOUBLE PRECISION C0A      !DEPTH AVERAGED REFERENCE CONCENTRATION AT Z0 (KG/M^3)

c cohesive
      DOUBLE PRECISION EDRATE  !mean erosion/deposition rate (kg/m^2/s)
                               !(positive: erosion, negative: deposition
      DOUBLE PRECISION ZS      !Height change in bed surface (m)
                               !(positive: erosion, negative: deposition)
      DOUBLE PRECISION TAOS    !solid transmitted stress due to Ulva (Pa)
      DOUBLE PRECISION TCONC   !final total suspended sediment concentration (kg/m**3)
     
      DOUBLE PRECISION VISK     !KINEMATIC VISCOSITY OF FLUID (M**2/SEC)

c local
      LOGICAL COHES             !YES=COHESIVE SEDIMENT
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE
      DOUBLE PRECISION TB1      !TIME AT WHICH BEDLOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TB2      !TIME AT WHICH BEDLOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION TS1      !TIME AT WHICH SUSPENDED LOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TS2      !TIME AT WHICH SUSPENDED LOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION PERBED   !PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
      DOUBLE PRECISION PERSUSP  !PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE
      DOUBLE PRECISION Z0C      !APPARENT BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION DELTACW  !HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION USTC     !TOTAL CURRENT SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTW     !TOTAL WAVE SHEAR VELOCITY OF GM
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF

!**************************************************************************
!  INITIALIZE UNASSIGNED VARIABLES

      QS = 0.D0
      QSDIR = 0.D0
      C0 = 0.D0
      C0A = 0.D0
      SED = 0.D0
      SEDM = 0.D0
      USTCRB=0.D0
      USTCRS=0.D0
      USTUP=0.D0
      FALL=0.D0
      TB1=0.D0                !ccf INTEL
      TB2=0.D0                !ccf INTEL
      TS1=0.D0                !ccf INTEL
      TS2=0.D0                !ccf INTEL
      PERBED=0.D0             !ccf INTEL
      PERSUSP=0.D0            !ccf INTEL
      VISK=VISC/RHOW

      COHES = IOPT1 .EQ. 7	!COHESIVE SEDIMENT

!**************************************************************************
! CALCULATE WAVE-INDUCED BOTTOM PARTICLE VELOCITY AND ORBITAL DIAMETER
!
! OUTPUT VARIABLES:
!
!    UB = MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
!    AB = EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
!               (1/2 OF THE ORBITAL DIAMETER)
!    WL = WAVE LENGTH (M)

      CALL OSCIL(HT,PER,D,UB,AB,WL)

!***************************************************************************
! CALCULATE THRESHOLD CRITERIA FOR BEDLOAD, SUSPENDED LOAD AND
! SHEET FLOW NON-COHESIVE SEDIMENT TRANSPORT.
! THRESH SUBROUTINE NOT APPLICABLE FOR COHESIVE SEDIMENTS --> GOTO FRICFAC
!
! OUTPUT VARIABLES:
!
!        FALL = SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
!      USTCRB = CRITICAL SHEAR VELOCITY FOR INITIATION OF BEDLOAD
!               TRANSPORT (M/SEC)
!      USTCRS = CRITICAL SHEAR VELOCITY FOR INITIATION OF SUSPENDED
!               LOAD TRANSPORT (M/SEC)
!       USTUP = CRITICAL SHEAR VELOCITY FOR INITIATION OF SHEET FLOW
!               TRANSPORT (M/SEC)
!          DX = DIMENSIONLESS GRAIN SIZE

      IF( .NOT. COHES ) CALL THRESH(VISK,GD,RHOS,RHOW,FALL,USTCRB,
     $USTCRS,USTUP,DX)

!**************************************************************************
! CALCULATE FRICTION FACTOR, AMBIENT CURRENT AND BOTTOM STRESSES
!
! OUTPUT VARIABLES:
!
c          Z0 = BED ROUGHNESS LENGTH (M)
!         Z0C = APPARENT BED ROUGHNESS LENGTH (M)
!         FCW = BOTTOM (SKIN) FRICTION FACTOR
!          UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
!        PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
!               WAVE BOUNDARY LAYER (RADIANS)
!        U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
!      PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
!               ABOVE SEABED (RADIANS)
!               NOTE:  PHI100 = PHIZ AS LONG AS PHIZ IS MEASURED
!               OUTSIDE THE WAVE BOUNDARY LAYER.
!       USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
!       USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
!      USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
!     USTCWSE = EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
!     USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY
!        USTC = TOTAL CURRENT SHEAR VELOCITY OF GM 
!        USTW = TOTAL WAVE SHEAR VELOCITY OF GM
!       USTCW = COMBINED TOTAL SHEAR VELOCITY OF GM
!     DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
!     RHEIGHT = PREDICTED RIPPLE HEIGHT
!     RLENGTH = PREDICTED RIPPLE LENGTH
!     RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION 

      CALL FRICFAC(COHES,D,UZ,Z,CDIR,UB,PER,WDIR,AB,GD,VISK,RHOW,RHOS,
     $Z0C,PHIB,PHI100,DELTACW,USTCS,USTWS,USTCWSB,USTC,USTW,RPLCOEF,
     $USTBF,USTCRB,USTUP,Z0,FCW,UA,U100,USTCWS,USTCW,RHEIGHT,RLENGTH)

      IF(USTCW.EQ.0) USTCW=DMAX1(USTC,USTW)
      IF(USTCWS.EQ.0) USTCWS=DMAX1(USTCS,USTWS)

!**************************************************************************
! IF COHESIVE COMPUTE THE TRANSPORT AND THEN EXIT

      IF ( COHES ) THEN

       CALL COHESIVE(NBCONC,CONC,BEDCHA,MAXBED,TIMEDR,USTCWS,U100,D,
     & RHOW,VISK,AULVA,EDRATE,ZS,SEDM,TCONC,TAOS)

       SEDDIR = CDIR
       RETURN
      END IF

!**************************************************************************
! CALCULATE THE DURATION OF THE DIFFERENT SEDIMENT TRANSPORT PHASES
!
! OUTPUT VARIABLES:
!
!         TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
!               TRANSPORT CEASES (SEC)
!         TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
!               TRANSPORT RECOMMENCES (SEC)
!         TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
!               LOAD TRANSPORT CEASES (SEC)
!         TS2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
!               LOAD TRANSPORT RECOMMENCES (SEC)
!      PERBED = PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
!     PERSUSP = PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE

      CALL TIMING(RHOW,UA,UB,PER,U100,USTCRB,USTCRS,USTCWS,PHIB,
     $USTCS,USTWS,RPLCOEF,TB1,TB2,TS1,PERBED,PERSUSP)

! CALCULATE VELOCITY PROFILE, SUSPENDED SEDIMENT CONCENTRATION PROFILE
! AND THE SUSPENDED SEDIMENT TRANSPORT RATE AND DIRECTION

      CALL PROFL(D,RHOW,FALL,UB,CDIR,USTCWS,USTCW,USTCRB,USTCRS,
     $USTUP,Z0C,DELTACW,USTCS,USTWS,USTCWSB,USTC,USTW,RPLCOEF,
     $USTBF,Z0,GD,DX,RHOS,QS,QSDIR,C0,C0A)

!**************************************************************************
! CALCULATE SEDIMENT TRANSPORT RATE AND DIRECTION
!
! OUTPUT VARIABLES:
!
!    SED = TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME OF SEDIMENT SOLIDS 
!          TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (M**3/S/M)
!    SEDM = TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS OF SEDIMENT SOLIDS 
!           TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (KG/S/M)
!    SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      CALL TRANSPO(D,UA,UB,U100,PER,GD,BETA,RHOS,RHOW,USTCRB,
     $USTUP,PHIB,PHI100,USTCS,USTWS,USTCWSB,RPLCOEF,TB1,TB2,TS1,
     $PERBED,PERSUSP,USTBF,FCW,USTCWS,HT,CDIR,WDIR,IOPT1,DX,FALL,
     $SED,SEDM,SEDDIR)

      RETURN
      END subroutine sedtrans05

!****************************************************************************
!****************************************************************************

! **********************************************************
! SUBROUTINE OSCIL
! **********************************************************
!  THIS SUBROUTINE CALCULATES WAVE-INDUCED BOTTOM PARTICLE VELOCITY
!  AND DISPLACEMENT USING LINEAR WAVE THEORY.  A CHECK IS ALSO MADE
!  FOR WAVE BREAKING.
!
!  INPUT VARIABLES:
!         HT = WAVE HEIGHT (M)       (INDATA96 file)
!        PER = WAVE PERIOD (SEC)     (INDATA96 file)
!          D = WATER DEPTH (M)       (INDATA96 file)
!
!  OUTPUT VARIABLES:
!         UB = MAX. WAVE-INDUCED BOTTOM HORIZ. PARTICLE VELOCITY (M/SEC)
!         AB = MAX. WAVE-INDUCED BOTTOM HORIZ. PARTICLE DISPLACEMENT (M)
!              (1/2 OF THE ORBIT SIZE)
!         WL = WAVELENGTH FROM LWT DISPERSION EQUATION (M)
!
!  INTERMEDIATE VARIABLES:
!          G = ACCELERATION DUE TO GRAVITY (M/SEC**2)
!        WL0 = DEEP WATER WAVE LENGTH (M)
!          W = WAVE ANGULAR FREQUENCY (RAD/SEC)
!        RKD = K*D  ( K = WAVE NUMBER (RAD/M) )
!         HB = BREAKING WAVE HEIGHT FOR GIVEN WAVE PERIOD AND WATER DEPTH (M)
!---------------------------------------------------------------------------

      SUBROUTINE OSCIL(HT,PER,D,UB,AB,WL)

      IMPLICIT NONE

      DOUBLE PRECISION HT       !WAVE HEIGHT (M)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)

      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      DOUBLE PRECISION WL0	!DEEP WATER WAVE LENGTH (M)
      DOUBLE PRECISION RKD	!K*D  ( K = WAVE NUMBER (RAD/M))
      DOUBLE PRECISION RKD0
      DOUBLE PRECISION DKD
      DOUBLE PRECISION AB       !EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
      DOUBLE PRECISION WL       !WAVE LENGTH (M)

      UB=0.0
      AB=0.0
      WL=0.0

!---------------------------------------------------------------------
! CHECK FOR CURRENT ONLY CASE (INCLUDING 'DEEP WATER' WAVE CONDITIONS)
!
!   FOR DEEP WATER WAVE CONDITIONS THE NORMAL CRITERION IS D/WL GREATER
!   THAN 0.5. TO ENSURE NO APPRECIABLE WAVE INDUCED BOTTOM STRESS, THIS
!   CODE USES THE CRITERION OF D/WL GREATER THAN 2.0

      IF (PER .EQ. 0) GOTO 30
      WL0 = G*PER*PER/(2*PII)

      IF ((D/WL0) .GT. 2.) RETURN

!---------------------------------------------------------------------
!  CALCULATE WAVELENGTH BY NEWTON-RAPHSON SOLUTION OF LWT DISPERSION
!  EQUATION.

      W=2.*PII/PER
      RKD0=W**2*D/G
      RKD=RKD0
  20  CONTINUE
      DKD=(1./TANH(RKD)-RKD/RKD0)/(1./RKD0+1./SINH(RKD)**2)
      RKD=RKD+DKD
      IF (ABS(DKD) .GE. 1.0E-4) GO TO 20
      WL=WL0*TANH(RKD)

!---------------------------------------------------------------------
!  CALCULATE WAVE-INDUCED BOTTOM PARTICLE VELOCITY AND ORBIT SIZE

! CONVENTIONALLY: UB=HT*G*PER/(2*WL*COSH(2*PII*D/WL))
! THE MORE EFFICIENT ALGORITH IS:
      UB=PII*HT/(PER*SINH(RKD))

! CONVENTIONALLY: AB=HT/(2*SINH(2*PII*D/WL))
! THE MORE EFFICIENT ALGORITH IS:
      AB=UB/W
!---------------------------------------------------------------------

 30   RETURN
      END subroutine

! *****************************************************
! SUBROUTINE THRESHOLD
! *****************************************************
!  THIS SUBROUTINE CALCULATES THE THRESHOLD SHEAR VELOCITIES FOR BEDLOAD,
!  SUSPENDED LOAD, AND SHEET FLOW SEDIMENT TRANSPORT. THE CRITICAL STRESS
!  FOR BEDLOAD TRANSPORT IS BASED ON THE VAN RIJN METHOD.
!  THE CRITICAL STRESS FOR SUSPENDED LOAD IS BASED ON THE VAN RIJN, WHERE
!  THE PARTICLE FALL VELOCITY IS AS GIVEN BY SOULSBY. THE SHEET FLOW
!  CRITICAL SHEAR STRESS IS GIVEN BY LEE AND AMOS.
!
! INPUT VARIABLES:
!     VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
!       GD = SEDIMENT GRAIN SIZE (M)                (INDATA96 file)
!     RHOS = SEDIMENT DENSITY (KG/M**3)             (READIN subroutine)
!     RHOW = FLUID DENSITY (KG/M**3)                (READIN subroutine)
!
! OUTPUT VARIABLES:
!     FALL = FALL VELOCITY OF SEDIMENT GRAINS AS GIVEN BY SOULSBY (M/SEC)
!   USTCRB = CRITICAL SHEAR VEL. FOR INIT. OF BEDLOAD  (M/SEC)
!   USTCRS = CRITICAL SHEAR VEL. FOR INIT. OF SUSPENDED (M/SEC)
!    USTUP = CRITICAL SHEAR VEL. FOR INIT. OF UPPER PLANE BED SHEET FLOW (M/SEC)
!       DX = DIMENSIONLESS GRAIN SIZE
!
! INTERMEDIATE VARIABLES:
!        G = ACCELERATION DUE TO GRAVITY (M/SEC**2)
!     DRHO = SEDIMENT DENSITY - FLUID DENSITY (KG/M**3)
!    YALIN = YALIN PARAMETER
!   GYALIN = LOG10 OF THE YALIN PARAMETER
!      SCR = SHIELDS PARAMETER
!      AUX = AUX VARIABLE
!---------------------------------------------------------------------------

      SUBROUTINE THRESH(VISK,GD,RHOS,RHOW,FALL,USTCRB,USTCRS,USTUP,DX)

      IMPLICIT NONE

      DOUBLE PRECISION VISK     !KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTCRS   !CRITICAL SHEAR VEL FOR INITI OF SUSPENDED LOAD TRANSPORT (M/SEC)
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE

      DOUBLE PRECISION DRHO             !SEDIMENT DENSITY - FLUID DENSITY (KG/M**3)
      DOUBLE PRECISION YALIN            !YALIN PARAMETER
      DOUBLE PRECISION GYALIN           !LOG10 OF THE YALIN PARAMETER
      DOUBLE PRECISION SCR              !SHIELDS PARAMETER
      DOUBLE PRECISION AUX              !AUX VARIABLE

! INITIALIZE CONSTANTS
      DRHO = RHOS-RHOW
      AUX = (DRHO*G*GD)/RHOW

! COMPUTE THE DIMENSIONLESS PARTICLE DIAMETER
      DX = GD * (((DRHO/RHOW)*G)/VISK**2.)**(1./3.)

! COMPUTE FALL VELOCITY (Soulsby, 1997)
      FALL = (VISK/GD)*((10.36**2. + 1.049*DX**3.)**0.5 - 10.36)

!-----------------------------------------------------------------
! CALCULATE THRESHOLD SHEAR VELOCITY FOR BEDLOAD TRANSPORT, USTCRB
!-----------------------------------------------------------------
! GET SHIELDS PARAMETER USING YALIN METHOD MODIFIED FROM MILLER ET AL. (1977)

      YALIN=SQRT((DRHO*G*GD**3.)/(RHOW*VISK**2.))
      GYALIN=dLOG10(YALIN)
      IF (YALIN .GE. 3000) THEN
         SCR=0.045
      ELSE IF (YALIN .LE. 100) THEN
         SCR=10.**((0.041*GYALIN**2)-0.356*GYALIN-0.977)
      ELSE
         SCR=10.**(0.132*GYALIN-1.804)
      ENDIF

! CONVERT SHIELDS PARAMETER TO SHEAR VELOCITY
      USTCRB=SQRT(SCR*AUX)

!--------------------------------------------------------------------------
! CALCULATE THRESHOLD SHEAR VELOCITY FOR SUSPENDED LOAD TRANSPORT, USTCRS
!--------------------------------------------------------------------------
! GET THE CRITICAL USTCRS/FALL RATIO for SUSPENSION (Van Rijn, 1984)

      IF(DX.GT.1..AND.DX.LE.10.)THEN
        SCR = 4./DX
      ELSE
        SCR = 0.4
      ENDIF

! COMPUTE THRESHOLD SHEAR VELOCITY
      USTCRS=FALL*SCR
      USTCRS = DMAX1(USTCRS,USTCRB)

!--------------------------------------------------------------------------
! CALCULATE THRESHOLD SHEAR VELOCITY FOR SHEET FLOW TRANSPORT, USTUP
!--------------------------------------------------------------------------
! CALCULATE SHEETFLOW SHIELDS PARAMETER ACCORDING TO LI AND AMOS
c      SCR = 0.172*(100.*GD)**(-0.376)
      SCR = 0.413*(1000*GD)**(-0.4)

! CONVERT SHIELDS PARAMETER TO SHEAR VELOCITY
      USTUP=SQRT(SCR*AUX)

      RETURN
      END subroutine

! **********************************************************
! SUBROUTINE FRICFAC
! **********************************************************
!  THIS SUBROUTINE CALCULATES THE FRICTION FACTOR, SHEAR VELOCITIES AND
!  RIPPLE DIMENSIONS FOR VARIOUS WAVE AND CURRENT CONDITIONS.

!  CASE A: FOR SI92 DATA     YES
!  CASE B: FOR SI82 DATA     NO

!  FOR COMBINED FLOW, GRAIN-SIZE ROUGHNESS IS FIRST USED TO OBTAIN THE SKIN-
!  FRICTION SHEAR VELOCITY WHICH IS USED TO CALCULATE THE BEDLOAD ROUGHNESS.
!  THE COMBINED GRAIN AND BEDLOAD ROUGHNESS IS USED TO OBTAIN A TRANSPORT-
!  RELATED SHEAR VELOCITY WHICH IS USED TO COMPUTE RIPPLE GEOMETRY AND
!  DETERMINE IF TRANSPORT IS IN SUSPENSION OR SHEETFLOW MODES.
!  FOR COHESIVE SEDIMENTS USE CONSTANT ROUGHNESS LENGHT (SOULSBY 1997)
!  AND DO NOT PREDICT RIPPLE DIMENSION
!
! INPUT VARIABLES:
!         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)    (INDATA96 file)
!          Z = HEIGHT ABOVE SEABED AT WHICH CURRENT IS MEASURED (M)  (INDATA96 file)
!       CDIR = CURRENT DIRECTION AT 1 M. ABOVE SEABED (AZIMUTH)      (INDATA96 file)
!        PER = WAVE PERIOD (SEC)                                     (INDATA96 file)
!       WDIR = WAVE DIRECTION (AZIMUTH)                              (INDATA96 file)
!         GD = SEDIMENT GRAIN SIZE (M)                               (INDATA96 file)
!      RHINP = INPUT RIPPLE HEIGHT (M)                               (INDATA96 file)
!      RLINP = INPUT RIPPLE LENGTH (M)                               (INDATA96 file)
!       RHOW = DENSITY OF FLUID (WATER)  (KG/M**3)                   (READIN subr)
!       RHOS = DENSITY OF SEDIMENT GRAIN  (KG/M**3)                  (READIN subr)
!         UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC) (OSCIL subr)
!         AB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE DISPLACEMENT (M) (OSCIL subr)
!     USTCRB = CRITICAL SHEAR VELOCITY FOR BEDLOAD TRANSPORT (M/SEC) (THRESH subr)
!      USTUP = CRITICAL SHEAR VELOCITY FOR INITIATION OF UPPER 
!              PLANE BED SHEET FLOW (M/SEC)                          (THRESH subr)
!       VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)               (THRESH subr)
!      COHES = IF YES = COHESIVE SEDIMENTS
!
! OUTPUT VARIABLES:
!         Z0 = BED ROUGHNESS LENGTH (M)
!        Z0C = APPARENT BED ROUGHNESS LENGTH (M)
!        FCW = BOTTOM FRICTION FACTOR
!         UA = CURRENT SPEED AT THE TOP OF THE WAVE-CURRENT BOUNDARY LAYER 
!              TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
!       PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
!              WAVE BOUNDARY LAYER (RADIANS)
!       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
!     PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
!              ABOVE SEABED (RADIANS)
!                  NOTE:  PHI100 = PHIZ AS LONG AS PHIZ IS MEASURED
!                  OUTSIDE THE WAVE BOUNDARY LAYER.
!      USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
!      USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
!     USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
!    USTCWSE = EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY 
!    USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY
!       USTC = TOTAL CURRENT SHEAR VELOCITY OF GM 
!       USTW = TOTAL WAVE SHEAR VELOCITY OF GM
!      USTCW = TOTAL COMBINED SHEAR VELOCITY OF GM 
!    DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
!    RHEIGHT = PREDICTED RIPPLE HEIGHT (M)
!    RLENGTH = PREDICTED RIPPLE LENGTH (M)
!    RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION 
!      USTBF = CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
!
! INTERMEDIATE VARIABLES:
!        RKB = BOTTOM ROUGHNESS HEIGHT (M)
!      DELTA = USTCS CONVERGENCE CRITERION FACTOR
!        FWS = SKIN-FRICTION FACTOR
!       FBAD = TOTAL FRICTION FACTOR INCLUDING FORM DRAG
!       UBAD = CURRENT SPEED NEGLECTING FORM DRAG (M/SEC)
!     PHIBAD = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS, WITHIN
!              WAVE B.L. AND NEGLECTING FORM DRAG (RADIANS)
!      RATIO = UA/UB;  DETERMINES VALIDITY OF EQUATION OF MOTION
!              USED BY GRANT AND MADSEN (1979)
!        SKB = PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
!        BKB = PREDICTED BEDLOAD ROUGHNESS HEIGHT
!        HTM = BEDLOAD LAYER HEIGHT
!---------------------------------------------------------------------------

      SUBROUTINE FRICFAC(COHES,D,UZ,Z,CDIR,UB,PER,WDIR,AB,GD,VISK,RHOW,
     $RHOS,Z0C,PHIB,PHI100,DELTACW,USTCS,USTWS,USTCWSB,USTC,USTW,
     $RPLCOEF,USTBF,USTCRB,USTUP,Z0,FCW,UA,U100,USTCWS,USTCW,RHINP,
     $RLINP)

      IMPLICIT NONE

      LOGICAL COHES
      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION UZ       !AMBIENT CURRENT AT HEIGHT Z ABOVE THE SEAFLOOR (M/S)
      DOUBLE PRECISION Z        !HEIGHT OF UZ ABOVE SEAFLOOR
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)
      DOUBLE PRECISION AB       !EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION VISK     !KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION Z0       !BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION FCW      !BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCW	!COMBINED TOTAL SHEAR VELOCITY OF GM
      DOUBLE PRECISION RHEIGHT	!PREDICTED RIPPLE HEIGHT
      DOUBLE PRECISION RLENGTH	!PREDICTED RIPPLE LENGTH

      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      DOUBLE PRECISION DELTA 	!USTCS CONVERGENCE CRITERION FACTOR
      DOUBLE PRECISION FBAD	!TOTAL FRICTION FACTOR INCLUDING FORM DRAG
      DOUBLE PRECISION GKB	!GRAIN ROUGHNESS HEIGHT (=2.5*GD)
      DOUBLE PRECISION BKB	!PREDICTED BEDLOAD ROUGHNESS HEIGHT
      DOUBLE PRECISION SKB	!PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
      DOUBLE PRECISION TKB	!PREDICTED TOTAL BOTTOM ROUGHNESS HEIGHT
      DOUBLE PRECISION HTM	!BEDLOAD LAYER HEIGHT
      DOUBLE PRECISION RKBC	!APPARENT BOTTOM ROUGHNESS (M)
      DOUBLE PRECISION RHINP	!INITIAL RIPPLE HEIGHT (M)         
      DOUBLE PRECISION RLINP	!INITIAL RIPPLE LENGTH (M)
      DOUBLE PRECISION USTCSP	!TEMPORARY VARIABLE FOR INITIAL USTCS TO A
      DOUBLE PRECISION FCWB  	!TRANSPORT-RELATED FCWB
      DOUBLE PRECISION USTCSB	!TRANSPORT-RELATED FCWB AND UST'S
      DOUBLE PRECISION USTWSB	!TEMPORARY VARIABLE

      DOUBLE PRECISION Z0C      !APPARENT BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION DELTACW  !HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSE  !EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION USTC     !TOTAL CURRENT SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTW     !TOTAL WAVE SHEAR VELOCITY OF GM
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION RKB      !BOTTOM ROUGHNESS HEIGHT (M)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF

      INTEGER ICOUNT

! INITIALIZE PARAMETERS
      FCW=0.0
      U100=0.0
      PHI100=0.
      PHIB=0.
      USTCS=0.0
      USTWS=0.0
      USTCWS=0.0
      USTC=0.0
      USTW=0.0
      USTCW=0.0
      USTCWSE=0.0
      USTCWSB=0.0
      USTBF=0.0
      Z0=0.0
      Z0C=0.0
      DELTACW=0.0
      RPLCOEF=1.
      BKB=0.0
      SKB=0.0
      TKB=0.0
      ICOUNT = 0

!*********************
! PURE CURRENT CASE
!*********************
      IF (UB .LT. 0.01) THEN

! GET PRELIMINARY BED ROUGHNESS HEIGHT RKB ACCORDING TO GRANT AND MADSEN
! (1982). IF THERE IS NO MEASUREMENT OF RIPPLE HEIGHTS, RKB=2.5*GD, OTHERWISE
! USE THE MEASURED INPUT RIPPLE HEIGHT AND LENGTH TO OBTAIN RKB=27.7*RHINP
! *RHINP/RLINP. FOR COHESIVE SEDIMENTS USE COSTANT BED ROUGHNESS HEIGHT

        IF (RHINP .EQ. 0) THEN
          RKB=2.5*GD
        ELSE
          RKB=27.7*RHINP*RHINP/RLINP
        ENDIF
	RKB=DMIN1(RKB,D)			!ccf

! CALL FRIC1 TO OBTAIN THE PRELIMINARY ESTIMATES OF "UA","U100","USTC"
        CALL FRIC1(UZ,Z,GD,RKB,UA,U100,USTC)

! COMPUTE PRELIMINARY SKIN-FRICTION CURRENT SHEAR VELOCITY
        FCW = 6.0E-3
        USTCS=SQRT(0.5*FCW*U100**2)

! ASSIGN AN ARBITRARY VALUE TO THE USTCS CONVERGENCE CRITERION DELTA
        DELTA = 1.0D0
 100    IF (DELTA .LT. 0.0001d0) GOTO 200

        ICOUNT = ICOUNT + 1
! PREDICT CURRENT RIPPLE DIMENSIONS:
! FOR NO-TRANSPORT CASE OR COARSE AND VERY-COARSE SANDS OR COHESIVE SEDIMENTS, 
! BEDFORM DIMENSION WILL NOT BE PREDICTED AND INPUT VALUES WILL BE USED. 
! OTHERWISE, RIPPLE HEIGHT AND LENGTH WILL BE PREDICTED ACCORDING TO YALIN 
! (1964) AND ALLEN (1970), RESPECTIVELY.
        IF (USTCS .LT. USTCRB .OR. GD .GE. 0.0005 .OR. COHES) THEN
           RHEIGHT=RHINP
           RLENGTH=RLINP
        ELSE

! FOR SHEET-FLOW, RIPPLE HEIGHT AND LENGTH WILL BE 0
           IF (USTCS .GE. USTUP) THEN
              RHEIGHT=0.D0
              RLENGTH=0.D0
           ELSE
              RLENGTH=1000*GD
              RHEIGHT=0.00074*(100*RLENGTH)**1.19
              CALL CURRIPPLE(GD,RHOS,RHOW,VISK,USTCS,USTCRB,USTUP,
     $		             RHINP,RLINP,RHEIGHT,RLENGTH)
          ENDIF

        ENDIF

        IF (RHEIGHT .EQ. 0.D0) THEN
           USTC=USTCS
           RKB=30.*EXP(LOG(Z)-0.4*UZ/USTC) + 2.5*GD
        ELSE
           RKB=27.7*RHEIGHT*RHEIGHT/RLENGTH
        ENDIF
	RKB=DMIN1(RKB,D)			!ccf

! CALL FRIC1 WITH NEW RKB TO OBTAIN NEW U100 AND USTC
        CALL FRIC1(UZ,Z,GD,RKB,UA,U100,USTC)

! ASSIGN THE INITIAL USTCS TO A TEMPORARY VARIABLE
        USTCSP=USTCS

! COMPUTE THE NEW SKIN-FRICTION CURRENT SHEAR VELOCITY
        USTCS=SQRT(0.5*FCW*U100**2)

! OBTAIN THE USTCS CONVERGENCE CRITERION DELTA
        DELTA=DABS(USTCSP/USTCS-1)

        if (USTCS .GE. USTUP) DELTA=0.00001D0             !ccf
        if (USTCSP .LT. USTCS) DELTA=0.00001D0            !ccf
        if (ICOUNT .GT. 51) DELTA=0.00001D0            !ccf
        IF (USTCSP.EQ.0.D0.AND.USTCS.EQ.0.D0)THEN
          DELTA=0.00001D0
        ENDIF

! GOTO 100 TO COMPARE THE DELTA VALUE WITH THE SET CRITERION VALUE
        GOTO 100

 200    CONTINUE

! GET FINAL BED ROUGHNESS Z0
        Z0=RKB/30.

	RHINP = RHEIGHT
	RLINP = RLENGTH

        RETURN

      ENDIF

!*************************
! WAVES AND CURRENT CASE  
!*************************

      IF (UZ .NE. 0.0) THEN

      PHI100=DMIN1(ABS(CDIR-WDIR),ABS(180.-ABS(CDIR-WDIR)),
     $         360.-ABS(CDIR-WDIR))*ASIN(1.)/90.
      PHIB=PHI100

! COMPUTE SKIN-FRICTION "FCW" AND "UST'S" BASED ON GRAIN ROUGHNESS HEIGHT GKB ONLY
! FOR COHESIVE SEDIMENTS USE COSTANT BED ROUGHNESS HEIGHT.
      GKB = 2.5*GD

      CALL FRIC2(UZ,Z,PHI100,UB,PER,GKB,RKBC,FCW,UA,PHIB,
     $U100,USTCS,USTWS,USTCWS,DELTACW)

      IF ( COHES ) THEN
        RHEIGHT = RHINP
        RLENGTH = RLINP 
        USTCWSE = USTCWS
        USTCW = USTCWS
        TKB = GKB
        GOTO 543
      ENDIF

! COMPUTE INITIAL BED ROUGHNESS HEIGHTS BASED ON SKIN-FRICTION UST'S
      CALL ROUGH(VISK,RHINP,RLINP,USTCS,USTWS,USTCWS,USTCRB,
     $USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     $RPLCOEF,USTBF)

      BKB=180*HTM
      SKB=2.5*GD+BKB
      SKB=DMIN1(SKB,D)			!ccf

! COMPUTE TRANSPORT-RELATED FCWB AND UST'S USING THE SUM OF GRAIN AND BEDLOAD
! ROUGHNESS HEIGHTS, SKB.
      CALL FRIC2(UZ,Z,PHI100,UB,PER,SKB,RKBC,FCWB,UA,PHIB,
     $U100,USTCSB,USTWSB,USTCWSB,DELTACW)

! COMPUTE FINAL BED ROUGHNESS HEIGHTS BASED ON TRANSPORT-RELATED UST'S.
! USE PREDICTED RIPPLE GEOMETRY FROM ROUGH AS INPUTS IF THE RIPPLE-ENHANCED
! SHEAR VELOCITY U*CWSE IS LARGER THAN BEDLOAD THRESHOLD SHEAR VELOCITY U*CRB

      CALL ROUGH(VISK,RHINP,RLINP,USTCSB,USTWSB,USTCWSB,USTCRB,
     $USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     $RPLCOEF,USTBF)
      TKB=DMIN1(TKB,D)			!ccf

! COMPUTE TOTAL FCW (FBAD) AND UST'S BASED ON TOTAL ROUGHNESS HEIGHT TKB.
      CALL FRIC2(UZ,Z,PHI100,UB,PER,TKB,RKBC,FBAD,UA,PHIB,
     $U100,USTC,USTW,USTCW,DELTACW)

! OBTAIN BED ROUGHNESS Z0, APPARENT BED ROUGHNESS Z0C
543   CONTINUE
      Z0=TKB/30.
      RKB = TKB
      Z0C=RKBC

      RHINP = RHEIGHT
      RLINP = RLENGTH

      RETURN
      ENDIF

!******************
! PURE WAVES CASE
!******************
! CALL FRIC3 USING MAX WAVE-INDUCED BOTTOM PARTICLE VELOCITY "AB"
! AND GRAIN SIZE "GD" TO COMPUTE SKIN-FRICTION FWS AND U*WS
! FOR COHESIVE SEDIMENTS USE COSTANT BED ROUGHNESS HEIGHT
      RKB = GD

      CALL FRIC3(AB,RKB,FCW)
      UA=0.0
      W=2.*PII/PER
      USTWS=SQRT(0.5*FCW*UB**2)

! PREDICT WAVE RIPPLE DIMENSIONS. FOR NO-TRANSPORT CASE OR COARSE AND 
! VERY-COARSE SANDS OR COHESIVE SEDIMENTS, BEDFORM DIMENSION WILL NOT 
! BE PREDICTED AND INPUT VALUES WILL BE USED
      IF (USTWS .LT. USTCRB .OR. GD .GE. 0.0005 .OR. COHES) THEN
         RHEIGHT=RHINP
         RLENGTH=RLINP
      ELSE

! FOR FINE OR MEDIUM SAND IN ACTIVE TRANSPORT, RIPPLES ARE PREDICTED
! ACCORDING TO BOYD ET AL. (1988) AND ALLEN (1970)
         IF (USTWS .GE. USTUP) THEN
            RHEIGHT=0
            RLENGTH=0
         ELSE
            RLENGTH=AB*557*(UB*AB/VISK)**(-0.68)
            RHEIGHT=0.00074*(100*RLENGTH)**1.19
         ENDIF
      ENDIF

      IF (RLENGTH .EQ. 0.) THEN
         RKB=2.5*GD
      ELSE
         RKB=27.7*RHEIGHT*RHEIGHT/RLENGTH
      ENDIF
      RKB=DMIN1(RKB,(D))	!ccf

! CALL FRIC3 AGAIN USING NEW ROUGHNESS HEIGHT TO COMPUTE THE TOTAL FW AND U*W
      CALL FRIC3(AB,RKB,FCW)
      USTW=SQRT(0.5*FCW*UB**2)

      DELTACW=2.*0.4*USTW/W
      Z0=RKB/30

      RHINP = RHEIGHT
      RLINP = RLENGTH

      RETURN
      END subroutine

!**************************************************************************
! SUBROUTINE FRIC1
!**************************************************************************
!  THIS SUBROUTINE CALCULATES THE BOTTOM FRICTION FACTOR FOR THE PURE
!  CURRENT CASE. A CONSTANT FRICTION FACTOR IS ASSUMED, BASED ON THE
!  WORK OF STERNBERG (1971).
!
!  INPUT VARIABLES:
!         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)
!          Z = HEIGHT ABOVE SEABED AT WHICH CURRENT IS MEASURED (M)
!         GD = SEDIMENT GRAIN SIZE (M)
!        RKB = BOTTOM ROUGHNESS (M)
!
!  OUTPUT VARIABLES:
!         UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
!       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
!       USTC = TOTAL CURRENT SHEAR VELOCITY OF GM (M/SEC)
!---------------------------------------------------------------------------

      SUBROUTINE FRIC1(UZ,Z,GD,RKB,UA,U100,USTC)

      IMPLICIT NONE

      DOUBLE PRECISION UZ       !AMBIENT CURRENT AT HEIGHT Z ABOVE THESEAFLOOR (M/S)
      DOUBLE PRECISION Z        !HEIGHT OF UZ ABOVE SEAFLOOR
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION RKB	!BOTTOM ROUGHNESS HEIGHT (M)
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTC	!TOTAL CURRENT SHEAR VELOCITY OF GM

      IF(RKB .EQ. 0.0) RKB=2.5*GD
      USTC=0.4*UZ/LOG(30*Z/RKB)
      U100=(USTC/0.4)*LOG(1*30/RKB)
      UA=U100
      RETURN
      END subroutine

!**************************************************************************
! SUBROUTINE CURRIPPLE
!**************************************************************************
!  THIS SUBROUTINE CALCULATES THE RIPPLE DIMENSION IN CASE OF CURRRNT ONLY
!  USING METHOD OF SOULSBY AND WHITEHOUSE (2005)
!
!  INPUT VARIABLES:
!         GD = SEDIMENT GRAIN SIZE (M)
!       RHOW = DENSITY OF FLUID (WATER)  (KG/M**3)                   (READIN subr)
!       RHOS = DENSITY OF SEDIMENT GRAIN  (KG/M**3)                  (READIN subr)
!       VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)               (THRESH subr)
!      USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
!     USTCRB = CRITICAL SHEAR VELOCITY FOR BEDLOAD TRANSPORT (M/SEC) (THRESH subr)
!      USTUP = CRITICAL SHEAR VELOCITY FOR INITIATION OF UPPER 
!      RHINP = INPUT RIPPLE HEIGHT (M)                               (INDATA96 file)
!
!  OUTPUT VARIABLES:
!    RHEIGHT = PREDICTED RIPPLE HEIGHT (M)
!    RLENGTH = PREDICTED RIPPLE LENGTH (M)
!---------------------------------------------------------------------------

      SUBROUTINE CURRIPPLE(GD,RHOS,RHOW,VISK,USTCS,USTCRB,USTUP,RHINP,
     $RLINP,RHEIGHT,RLENGTH)

      IMPLICIT NONE

      DOUBLE PRECISION GD	!SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION VISK     !KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCRB	!CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTUP	!CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RHINP	!INITIAL RIPPLE HEIGHT (M)         
      DOUBLE PRECISION RLINP	!INITIAL RIPPLE LENGTH (M)
      DOUBLE PRECISION RHEIGHT	!PREDICTED RIPPLE HEIGHT
      DOUBLE PRECISION RLENGTH	!PREDICTED RIPPLE LENGTH

      DOUBLE PRECISION RHMAX	!MAX RIPPLE HEIGHT
      DOUBLE PRECISION RLMAX	!MAX RIPPLE LENGTH
      DOUBLE PRECISION USTBF	!CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION SST	!DIMENTIONLESS SEDIMENT GRAIN SIZE PARAMETER
      DOUBLE PRECISION PSICR	!CRITICAL SHIELDS PARAMETER FOR BEDLOAD TRANSPORT
      DOUBLE PRECISION PSIBF	!GM82 CRITICAL SHIELDS PARAMETER FOR RIPPLE BREAKOFF
      DOUBLE PRECISION S	!RHOS/RHOW

! INITIALIZING PARAMETERS
      S=RHOS/RHOW
      SST=GD*((S-1.d0)*G*GD)**0.5/(4.d0*VISK)
      PSICR=(USTCRB**2)/((S-1.d0)*G*GD)
      PSIBF=1.8d0*(SST**0.6)*PSICR
      USTBF=(PSIBF*(S-1d0)*G*GD)**0.5
      USTBF = DMIN1(USTBF,USTUP*0.99999d0)

! MAX RIPPLE DIMENSION
      RHMAX = GD * 202.d0 * SST**(-0.554d0)
      RLMAX = GD * (500.d0 + 1881.d0*SST**(-1.5))

! COMPUTE RIPPLES IN THE TRANSPORT RANGE
      RHEIGHT = RHMAX
      RLENGTH = RLMAX

! COMPUTE RIPPLES IN THE BREAK-OFF RANGE
      IF (USTCS .GT. USTBF .AND. USTCS .LE. USTUP) THEN
         RHEIGHT = RHMAX * ((USTUP-USTCS)/(USTUP-USTBF))
         RLENGTH = RLMAX * ((USTUP-USTCS)/(USTUP-USTBF))
      END IF

      RETURN
      END subroutine

!**************************************************************************
! SUBROUTINE FRIC2
!**************************************************************************
!  THIS SUBROUTINE CALCULATES THE FRICTION FACTOR FOR COMBINED WAVE AND
!  CURRENT CONDITIONS USING THE METHOD OF GRANT AND MADSEN (1986). THIS
!  METHOD IS NOT VALID FOR UA/UB > 1.0 (APPROXIMATELY) DUE TO THE REL-
!  ATIVE IMPORTANCE OF THE CONVECTIVE ACCELERATION TERMS IN THE EQUATION
!  OF MOTION.
!
!  INPUT VARIABLES:
!         UZ = CURRENT SPEED AT HEIGHT Z (M) ABOVE SEABED (M/SEC)
!     PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.
!              ABOVE SEABED (RADIANS)  (NB:  PHI100 = PHIZ)
!         UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)
!        PER = WAVE PERIOD (SEC)
!        RKB = BOTTOM ROUGHNESS HEIGHT (M)
!
!  OUTPUT VARIABLES:
!        FCW = BOTTOM FRICTION FACTOR FOR THE COMBINED CASE
!         UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
!       U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
!       PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
!              WAVE BOUNDARY LAYER (RADIANS)
!       RKBC = APPARENT BOTTOM ROUGHNESS (M)
!       USTC = CURRENT SHEAR VELOCITY OF GM (M/SEC)
!       USTW = WAVE SHEAR VELOCITY OF GM (M/SEC)
!      USTCW = COMBINED SHEAR VELOCITY OF GM (M/SEC)
!    DELTACW = THICKNESS OF THE WAVE-CURRENT BOUNDARY LAYER
!
!  INTERMEDIATE VARIABLES:
!          W = WAVE ANGULAR FREQUENCY (RAD/SEC)
!         Z0 = DYNAMIC BOTTOM ROUGHNESS LENGTH
!         CR = FACTOR DESCRIBING RELATIVE RATIO OF USTC/USTW
!      DELTA = ITERATION CRITERION FOR UST CALCULATION
!       TEMP = TEMPORARY VARIABLE
!---------------------------------------------------------------------------

      SUBROUTINE FRIC2(UZ,Z,PHI100,UB,PER,RKB,RKBC,FCW,UA,PHIB,
     $U100,USTC,USTW,USTCW,DELTACW)

      IMPLICIT NONE

      DOUBLE PRECISION UZ       !AMBIENT CURRENT AT HEIGHT Z ABOVE THESEAFLOOR (M/S)
      DOUBLE PRECISION Z        !HEIGHT OF UZ ABOVE SEAFLOOR
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITYAT THE BOTTOM (M/S)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION RKB	!BOTTOM ROUGHNESS HEIGHT (M)
      DOUBLE PRECISION RKBC	!APPARENT BOTTOM ROUGHNESS (M)
      DOUBLE PRECISION FCW      !BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTC	!CURRENT SHEAR VELOCITY OF GM (M/SEC)
      DOUBLE PRECISION USTW	!TOTAL WAVE SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCW	!COMBINED SHEAR VELOCITY OF GM (M/SEC)
      DOUBLE PRECISION DELTACW  !HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER

      DOUBLE PRECISION W        !WAVE ANGULAR FREQUENCY (RAD/SEC)
      DOUBLE PRECISION Z0       !BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION CR 	!FACTOR DESCRIBING RELATIVE RATIO OF USTC/USTW
      DOUBLE PRECISION DELTA 	!ITERATION CRITERION FOR UST CALCULATION
      DOUBLE PRECISION TEMP 		!TEMPORARY VARIABLE
      DOUBLE PRECISION A,B,AA,BB,CC,DD	!TEMPORARY VARIABLE
      DOUBLE PRECISION DX,X,XNEW,FCWNEW	!TEMPORARY VARIABLE
      DOUBLE PRECISION CRNEW	 	!TEMPORARY VARIABLE
      DOUBLE PRECISION EPSILON 		!TEMPORARY VARIABLE

      DOUBLE PRECISION G        !GRAVITY

!  INITIALIZE ITERATION PARAMETERS

      W=2.*PII/PER
      PHIB=PHI100 
      DELTA=1.0
      CR=1.0
      FCW=0.01
      Z0=RKB/30.

! CALCULATE UST'S BY ITERATION (GM,1986)
 10   IF (DELTA .LT. 0.00001) GO TO 200      
      Z0=RKB/30.
      TEMP=CR*UB/(Z0*W)
! INITIALIZE THE ITERATION CRITERION EPSILON     
      EPSILON=1.0
 50   IF (EPSILON .LT. 0.00001) GO TO 100

! CALCULATE FCW ACCORDING TO GM (1986). NEWTON-RAPHSON SOLUTION IS USED IN      
! ITERATION.
      X=4*SQRT(FCW)
      A=0.24*X-1.65+DLOG10(TEMP*X)-1/X
      B=0.24+1/X**2+1/(X*DLOG(10.0D00))
      DX=A/B
      XNEW=X-DX
      FCWNEW=(XNEW/4)**2
      EPSILON=DABS(FCWNEW/FCW-1)
      FCW=FCWNEW
      GO TO 50
 100  CONTINUE 
!     CALCULATE USTW, USTCW AND DELTACW (BOUNDARY LAYER THICKNESS)     
      USTW=SQRT(0.5*CR*FCW*UB**2)
      USTCW=SQRT(CR)*USTW
      DELTACW=2.*0.4*USTCW/W        !ggu error in article -> must be omega
!---- CALCULATE CURRENT SHEAR VELOCITY USTC ------------------------
!     FOR DELTACW=Z0, NO WAVE EFFECT AND PURE CURRENT ASSUMED
      IF (DELTACW .LE. Z0) THEN
         FCW=6.0E-3
         U100=UZ*LOG(30.0/RKB)/LOG(30.*Z/RKB)
         UA=U100
         PHIB=0.0
         USTC=SQRT(0.5*FCW*U100**2)
         USTCW=USTC
         USTW=0						!ccf INTEL
! GET FINAL BED ROUGHNESS AND BED ROUGHNESS HEIGHT USING LOG LAW
         Z0=EXP(LOG(Z)-UZ*0.4/USTC)
         RKB=30*Z0
         RKBC=RKB
         RETURN
      ENDIF
      AA=DLOG(DELTACW/Z0)/USTCW
      BB=DLOG(Z/DELTACW)
      CC=-0.4*UZ
      DD=SQRT(BB**2-4*AA*CC)
      USTC=0.5*(-BB+DD)/AA
      RKBC=DELTACW*EXP(-AA*USTC)
      CRNEW=SQRT(1.0+2*(USTC/USTW)**2*COS(PHIB)
     $      +(USTC/USTW)**4)
      DELTA=DABS(CRNEW/CR-1)
      CR=CRNEW
      GO TO 10

 200  CONTINUE
! CALCULATE VELOCITY AT THE TOP OF THE WAVE-CURRENT BOUNDARY LAYER     
      UA=(USTC/0.4)*DLOG(DELTACW/RKBC)
! CALCULATE VELOCITY AT 1M ABOVE THE BOTTOM      
      IF (DELTACW .GT. 1.) THEN
        U100=(USTC/USTCW)*(USTC/0.4)*DLOG(1.0D00/Z0)
      ELSE
        U100=(USTC/0.4)*DLOG(1.0D00/RKBC)
      ENDIF

      RETURN
      END subroutine

!**************************************************************************
! SUBROUTINE FRIC3
!**************************************************************************
!  THIS SUBROUTINE CALCULATES THE BOTTOM FRICTION FACTOR FOR THE PURE
!  WAVE CONDITION USING THE METHOD OF JONSSON (1966) AS MODIFIED BY
!  NIELSEN (1979).  
!
!  INPUT VARIABLES:
!         AB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE DISPLACEMENT (M)
!        RKB = BOTTOM ROUGHNESS HEIGHT (M)
!
!  OUTPUT VARIABLES:
!        FCW = BOTTOM FRICTION FACTOR FOR THE PURE WAVE CASE
!---------------------------------------------------------------------------

      SUBROUTINE FRIC3(AB,RKB,FCW)
 
      IMPLICIT NONE

      DOUBLE PRECISION AB		!EXCURSION LENGTH OF BOTTOM WAVE ORBIT (M)
      DOUBLE PRECISION RKB		!BOTTOM ROUGHNESS HEIGHT (M)
      DOUBLE PRECISION FCW		!BOTTOM (SKIN) FRICTION FACTOR

      FCW=DMIN1(EXP(5.213*(RKB/AB)**0.194-5.977),0.28D0)
      RETURN
      END subroutine
   

!**************************************************************************
! SUBROUTINE ROUGH
!**************************************************************************
!  THIS SUBROUTINE USES SKIN-FRICTION SHEAR VELOCITIES TO CALCULATE THE
!  INITIAL RIPPLE GEOMETRY AND VARIOUS ROUGHNESS HEIGHTS BASED ON THE GM86
!  COMBINED-FLOW BBL MODEL AND THE LATEST SIB DATA
!
!  INPUT VARIABLES
!      VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
!     RHINP = INITIAL RIPPLE HEIGHT (M)         
!     RLINP = INITIAL RIPPLE LENGTH (M)
!     USTCS = GM CURRENT SKINFRICTION SHEAR VELOCITY (M/S)
!     USTWS = GM WAVE SKINFRICTION SHEAR VELOCITY (M/S)
!    USTCWS = GM COMBINED SKINFRICTION SHEAR VELOCITY (M/S)
!    USTCRB = CRITICAL SHEAR VELOCITY FOR THE INITIATION OF BEDLOAD TRANSPORT
!     USTUP = CRITICAL SHEAR VELOCITY FOR UPPER PLANE BED
!        GD = SEDIMENT GRAIN SIZE
!      RHOW = FLUID DENSITY; RHOS=SEDIMENT GRAIN DENSITY
!      RHOS = SEDIMENT DENSITY; 
!    
!  OUTPUT VARIABLES
!    RHEIGHT = PREDICTED RIPPLE HEIGHT
!    RLENGTH = PREDICTED RIPPLE LENGTH
!        SKB = PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
!        HTM = BEDLOAD LAYER HEIGHT
!        TKB = PREDICTED TOTAL BOTTOM ROUGHNESS HEIGHT
!    USTCWSE = EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
!    RPLCOEF = UST CONVERSION COEFFICIENT
!      USTBF = CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
!
!  INTERMEDIATE VARIABLES
!          G = GRAVITY ACCELERATION
!        SST = DIMENTIONLESS SEDIMENT GRAIN SIZE PARAMETER
!      PSICR = CRITICAL SHIELDS PARAMETER FOR BEDLOAD TRANSPORT
!      PSIBF = GM82 CRITICAL SHIELDS PARAMETER FOR RIPPLE BREAKOFF
!        GKB = GRAIN ROUGHNESS HEIGHT (=2.5*GD)
!      STEEP = PREDICTED RIPPLE STEEPNESS
!---------------------------------------------------------------------------

      SUBROUTINE ROUGH(VISK,RHINP,RLINP,USTCS,USTWS,USTCWS,USTCRB,
     $USTUP,GD,RHOW,RHOS,RHEIGHT,RLENGTH,SKB,HTM,TKB,USTCWSE,
     $RPLCOEF,USTBF)

      IMPLICIT NONE

      DOUBLE PRECISION VISK	!KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
      DOUBLE PRECISION RHINP	!INITIAL RIPPLE HEIGHT (M)         
      DOUBLE PRECISION RLINP	!INITIAL RIPPLE LENGTH (M)
      DOUBLE PRECISION USTCS	!GM CURRENT SKINFRICTION SHEAR VELOCITY (M/S)
      DOUBLE PRECISION USTWS	!GM WAVE SKINFRICTION SHEAR VELOCITY (M/S)
      DOUBLE PRECISION USTCWS	!GM COMBINED SKINFRICTION SHEAR VELOCITY (M/S)
      DOUBLE PRECISION USTCRB	!CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTUP	!CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION GD	!SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION RHOW	!DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION RHOS	!DENSITY OF SEDIMENT MINERAL(S) (KG/M**3)
      DOUBLE PRECISION RHEIGHT	!PREDICTED RIPPLE HEIGHT
      DOUBLE PRECISION RLENGTH	!PREDICTED RIPPLE LENGTH
      DOUBLE PRECISION SKB	!PREDICTED GRAIN + BEDLOAD ROUGHNESS HEIGHT
      DOUBLE PRECISION TKB	!PREDICTED TOTAL BOTTOM ROUGHNESS HEIGHT
      DOUBLE PRECISION HTM	!BEDLOAD LAYER HEIGHT
      DOUBLE PRECISION USTCWSE	!EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION RPLCOEF	!RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION USTBF	!CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF

      DOUBLE PRECISION RKB	!BOTTOM ROUGHNESS HEIGHT (M)
      DOUBLE PRECISION SST	!DIMENTIONLESS SEDIMENT GRAIN SIZE PARAMETER
      DOUBLE PRECISION PSICR	!CRITICAL SHIELDS PARAMETER FOR BEDLOAD TRANSPORT
      DOUBLE PRECISION PSIBF	!GM82 CRITICAL SHIELDS PARAMETER FOR RIPPLE BREAKOFF
      DOUBLE PRECISION STEEP	!PREDICTED RIPPLE STEEPNESS
      DOUBLE PRECISION S	!RHOS/RHOW

! INITIALIZING PARAMETERS
      S=RHOS/RHOW
      SST=GD*((S-1)*G*GD)**0.5/(4*VISK)
      PSICR=(USTCRB**2)/((S-1)*G*GD)
      PSIBF=1.8*(SST**0.6)*PSICR
      USTBF=(PSIBF*(S-1)*G*GD)**0.5
      STEEP = 0.

!-----------------------------------------------------------------
!  FOR COARSE AND VERY-COARSE SANDS, BEDFORM GEOMETY CANNOT BE PREDICTED
!  AND THE INITIAL RIPPLE HEIGHT AND LENGTH WILL BE USED
      IF(GD .GE. 0.0005) THEN
           RHEIGHT=RHINP
           RLENGTH=RLINP
           IF (RLENGTH .EQ. 0) STEEP = 0.
           IF (RLENGTH .GT. 0) STEEP=RHEIGHT/RLENGTH
           GO TO 33
      ENDIF

!-----------------------------------------------------------------
!  COMPUTE THE INITIAL RIPPLE CONVERSION COEFFICIENT RPLCOEF. IT IS EQUAL 
!  TO 1 FOR FLAT BED, OTHERWISE IS CALCULATED ACCORDING TO NIELSEN (1992)
      IF(RHINP .EQ. 0 .OR. USTCWS .GE. USTUP) THEN
           RPLCOEF=1
      ELSE
           RPLCOEF=1/(1-3.14*RHINP/RLINP)
      ENDIF

!  COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF

!-----------------------------------------------------------------
!  FOR NO TRANSPORT CASE, USE THE COMBINED WAVE-CURRENT RIPPLE HEIGHT AND
!  LENGTH AT THE BEDLOAD THRESHOLD CONDITION BASED ON LI AND AMOS (IN REVIEW)
!      IF(USTCWSE .LT. USTCRB) THEN
!           STEEP=0.12
!           RHEIGHT=GD*(22.15+6.38)
!           RLENGTH=RHEIGHT/STEEP
!           GO TO 33
!      ENDIF

!----------------------------------------------------------------- !ggu check
!  USE THE INITIAL RIPPLE HEIGHT AND LENGTH FOR NO TRANSPORT CASE  !ggu Li96
       IF(USTCWSE .LT. USTCRB) THEN
            RHEIGHT=RHINP
            RLENGTH=RLINP
            IF (RLENGTH .EQ. 0) STEEP = 0
            IF (RLENGTH .GT. 0) STEEP=RHEIGHT/RLENGTH
            GO TO 33
       ENDIF

!-----------------------------------------------------------------
!  COMPUTE RIPPLES FOR ACTIVE TRANSPORT CASE

!  RIPPLES IN THE WEAK-TRANSPORT RANGE
      IF(USTCWSE .GE. USTCRB .AND. USTCWS .LT. USTCRB) THEN
c      IF(USTCWS .LT. USTCRB) THEN	!ccf
           STEEP=0.11
           RHEIGHT=GD*(19.59*(USTCWS/USTCRB)+20.92)
           RLENGTH=RHEIGHT/STEEP
      ENDIF

!  RIPPLES IN THE EQUILIBRIUM RANGE
      IF(USTCWS .GE. USTCRB .AND. USTCWS .LT. USTBF) THEN
!  COMPUTE WAVE-DOMINANT RIPPLES FOR U*WS/U*CS>=1.25
           IF(USTWS/USTCS .GE. 1.25) THEN
                STEEP=0.15
c                RHEIGHT=GD*(9.86*(USTCWS/USTCRB)+37.91)
                RHEIGHT=GD*(27.14*(USTCWS/USTCRB)+16.36)
           ELSE
!  COMPUTE COMBINED WAVE-CURRENT OR CURRENT-DOMINANT RIPPLES
                STEEP=0.12
                RHEIGHT=GD*(22.15*(USTCWS/USTCRB)+6.38)
           ENDIF
           RLENGTH=RHEIGHT/STEEP
      ENDIF

!  COMPUTE RIPPLES IN THE BREAK-OFF RANGE BASED ON THE SIB METHOD
      IF(USTCWS .GE. USTBF .AND. USTCWS .LT. USTUP) THEN
             RLENGTH = 530*GD
             STEEP=(0.15/(1-USTBF/USTUP))*(1-USTCWS/USTUP)
             RHEIGHT = RLENGTH*STEEP
      ENDIF

!  NO RIPPLES UNDER SHEET FLOW CONDITIONS
      IF (USTCWS .GE. USTUP) THEN
           RHEIGHT=0
           RLENGTH=0
           STEEP=0
      ENDIF

 33   CONTINUE     

!  COMPUTE THE TRUE RIPPLE CONVERSION COEFFICIENT RPLCOEF
      IF(RHEIGHT .EQ. 0 .OR. RLENGTH .EQ. 0) THEN
           RPLCOEF=1
      ELSE
           RPLCOEF=1/(1-3.14*RHEIGHT/RLENGTH)
      ENDIF

! ************************************
! THE NEXT LINE IS ONLY RUN FOR SIB82 DATA TO CALCULATE RPLCOEF USING
! INPUT RIPPLE HEIGHT AND LENGTH
!      RPLCOEF=1/(1-3.14*RHINP/RLINP)

!  COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF

!  CALCULATE BEDLOAD LAYER HEIGHT HTM USING THE MODIFIED NIELSEN 
!  (1992) METHOD
      IF(USTCWS .GE. USTCRB) THEN
           HTM=2.9*GD*((USTCWS**2)/((S-1)*G*GD)-PSICR)**0.75
!  LIMIT HTM TO BE <10*GRAIN DIAMETER
           IF (HTM .GT. 10*GD) HTM=10*GD
!  CALCULATE BEDLOAD LAYER HEIGHT HTM USING THE MODIFIED GM82 METHOD
!           HTM=1.22*GD*(S+0.5)*PSICR*(USTCWS/USTCRB-0.7)**2
!      ELSE IF(USTCWSE .GE. USTCRB) THEN
!           HTM=1.22*GD*(S+0.5)*PSICR*((USTCRB/USTCWS)**(0.282)*
!     $         USTCWS/USTCRB-0.7)**2

      ELSE
           HTM=0
      ENDIF

!  COMPUTE VARIOUS ROUGHNESS HEIGHTS: RKB, RIPPLE ROUGHNESS HEIGHT
!  AND TKB, TOTAL BED ROUGHNESS HEIGHT.
      RKB=27.7*RHEIGHT*STEEP
      TKB=RKB+SKB

      RETURN
      END subroutine

!***********************************************************************
!*******************************************************************
! SUBROUTINE TIMIMG
!*******************************************************************
!  TIMING2.FOR USES EFFECTIVE SHEAR STRESSES:
!      CASE 0 - AVERAGE SHEAR STRESS (LINE 92)                   NO
!      CASE A - EFFECTIVE SHEAR STRESSES (LINE 102)             YES
!      CASE B - MAXIMIZED EFFECTIVE SHEAR STRESSES (LINE 114)    NO
!
!  THIS SUBROUTINE USES EFFECTIVE SHEAR STRESSES AT RIPPLE CREST TO
!  CALCULATE THE DURATION OF SEDIMENT TRANSPORT PHASES (NO TRANSPORT, BEDLOAD
!  TRANSPORT, SUSPENDED LOAD TRANSPORT) BY CALCULATING WHEN THE RESPECTIVE
!  CRITICAL SHEAR STRESSES ARE EXCEEDED.
!  ** BEDLOAD USTCWSB IS 'NOT' USED IN DETERMINING TIME FOR SUSPENDED LOAD
!  TRANSPORT
!
! INPUT VARIABLES:
!       PER = WAVE PERIOD (SEC)
!      PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE
!             WAVE BOUNDARY LAYER (RADIANS)
!   RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
!        UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
!        UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)
!    USTCRB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD
!             TRANSPORT (M/SEC)
!    USTCRS = CRITICAL FLUID VELOCITY FOR INITIATION OF SUSPENDED
!             LOAD TRANSPORT (M/SEC)
!   USTCS = CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
!   USTWS = WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
!  USTCWS = COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
!
!  OUTPUT VARIABLES:
!    PERBED = PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE.
!   PERSUSP = PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE.
!       TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
!             LOAD TRANSPORT CEASES (SEC)
!       TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
!             TRANSPORT CEASES (SEC)
!       TS2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED
!             LOAD TRANSPORT RECOMMENCES (SEC)
!       TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD
!             TRANSPORT RECOMMENCES (SEC)
!     USTCS = MAXIMUM CURRENT SKIN-FRICTION SHEAR VELOCITY
!     USTWS = GM WAVE SKIN-FRICTION SHEAR VELOCITY
!    USTCWS = GM WAVE-CURRENT SKIN-FRICTION SHEAR VELOCITY
!   USTCWSE = RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION SHEAR VELOCITY
!
!  INTERMEDIATE VARIABLES:
!    USTCSE = RIPPLE-ENHANCED EFFECTIVE CURRENT SKIN-FRICTION SHEAR VELOCITY
!    USTWSE = RIPPLE-ENHANCED EFFECTIVE WAVE SKIN-FRICTION SHEAR VELOCITY
!       XS1 = B+SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER CREST
!       XB1 = B+SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER CREST
!       XS2 = B-SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER TROUGH
!       XB2 = B-SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER TROUGH
!         B = -B/2A, AS IN EQ'N. FOR ROOTS OF A QUADRATIC EQUATION
!     B24AC = (B**2-4*A*C)/(2*A)**2, AS IN QUADRATIC EQ'N. SOLUTION
!---------------------------------------------------------------------------

      SUBROUTINE TIMING(RHOW,UA,UB,PER,U100,USTCRB,USTCRS,USTCWS,PHIB,
     $USTCS,USTWS,RPLCOEF,TB1,TB2,TS1,PERBED,PERSUSP)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITYAT THE BOTTOM (M/S)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTCRS   !CRITICAL SHEAR VEL FOR INITI OF SUSPENDED LOAD TRANSPORT (M/SEC)
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION TB1      !TIME AT WHICH BEDLOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TB2      !TIME AT WHICH BEDLOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION TS1      !TIME AT WHICH SUSPENDED LOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TS2      !TIME AT WHICH SUSPENDED LOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION PERBED   !PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
      DOUBLE PRECISION PERSUSP  !PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE

      DOUBLE PRECISION USTCSE	!RIPPLE-ENHANCED EFFECTIVE CURRENT SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION USTWSE	!RIPPLE-ENHANCED EFFECTIVE WAVE SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION USTCWSE  !EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION XS1	!B+SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER CREST
      DOUBLE PRECISION XB1	!B+SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER CREST
      DOUBLE PRECISION XS2	!B-SQRT(B24ACS) USED IN SUSPENDED TIME CALCULATION UNDER TROUGH
      DOUBLE PRECISION XB2	!B-SQRT(B24ACB) USED IN BEDLOAD TIME CALCULATION UNDER TROUGH
      DOUBLE PRECISION B	!-B/2A, AS IN EQ`N. FOR ROOTS OF A QUADRATIC EQUATION
      DOUBLE PRECISION B24ACB	!(B**2-4*A*C)/(2*A)**2, AS IN QUADRATIC EQ`N. SOLUTION
      DOUBLE PRECISION B24ACS	!(B**2-4*A*C)/(2*A)**2, AS IN QUADRATIC EQ`N. SOLUTION
      DOUBLE PRECISION TAOWS	!SHEAR STRESS
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION TAOCRS	!SHEAR STRESS
      DOUBLE PRECISION TAOCS	!SHEAR STRESS

!  FIRST, SET DEFAULT VALUES
      TS1=0.0
      TB1=0.0
      TS2=0.0
      TB2=0.0
      PERSUSP=0.0
      PERBED=0.0
      USTWSE=USTWS

!*****************************
! PURE CURRENT CASE
!*****************************
      IF (UB .LT. 0.01) THEN
        IF (USTCS .GE. USTCRS) PERSUSP=100
        IF (USTCS .GE. USTCRB .AND. USTCS .LT. USTCRS) PERBED=100
        RETURN
      ENDIF

!*****************************
! WAVE PRESENT CASE
!*****************************
! --- CASE 0: NO (USE AVERAGE SHEAR STRESS)
! OBTAIN GRANT-MADSEN SKIN-FRICTION CURRENT SHEAR VELOCITY
!      USTCSE=USTCS
! OBTAIN SKIN-FRICTION WAVE SHEAR VELOCITY
!      USTWSE=USTWS
! OBTAIN SKIN-FRICTION COMBINED SHEAR VELOCITY
!      USTCWSE=USTCWS
! ASIGN THE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
!      USTCWSM=USTCWSE

! --- CASE A: YES (USE RIPPLE-ENHANCED AVEARGE SHEAR STRESSES)
! COMPUTE RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION CURRENT SHEAR VELOCITY
      USTCSE=USTCS*RPLCOEF
! COMPUTE EFFECTIVE SKIN-FRICTION WAVE SHEAR VELOCITY
      IF (UB .NE. 0.0) USTWSE=USTWS*RPLCOEF
! COMPUTE THE EFFECTIVE SKIN-FRICTION COMBINED SHEAR VELOCITY
      USTCWSE=USTCWS*RPLCOEF
! ASIGN EFFECTIVE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
c      USTCWSM=USTCWSE

! --- CASE B: NO (USE MAXIMIZED SHEAR STRESSES FOR CURRENT DOMINANT
!                 OR WAVE-CURRENT COMBINED CONDITIONS)
!      IF (USTWS/USTCS .LT. 1.25) THEN
! CHOOSE THE BIGGER FROM THE GM AND STERNBERG CURRENT SHEAR VELOCITIES
!      IF (USTCS .LT. USTCS1) USTCS=USTCS1
!          TAOCS=RHOW*USTCS**2
!          TAOWS=RHOW*USTWS**2
! COMPUTE VECTORIALLY THE MAXIMIZED COMBINED SKIN-FRICTION SHEAR STRESS BASED
! ON THE GM TAOWS AND MAXIMIZED TAOCS
!          TAOCWS=SQRT((TAOCS*SIN(PHIB))**2+
!     $            (TAOCS*COS(PHIB)+TAOWS)**2)
!          USTCWS=SQRT(TAOCWS/RHOW)
!      ENDIF
! COMPUTE RIPPLE-ENHANCED EFFECTIVE SKIN-FRICTION CURRENT SHEAR VELOCITY
!      USTCSE=USTCS*RPLCOEF
! COMPUTE EFFECTIVE SKIN-FRICTION WAVE SHEAR VELOCITY
!      IF (UB .NE. 0.0) USTWSE=USTWS*RPLCOEF
! COMPUTE THE EFFECTIVE SKIN-FRICTION COMBINED SHEAR VELOCITY
!      USTCWSE=USTCWS*RPLCOEF
! ASIGN EFFECTIVE COMBINED SHEAR VELOCITY TO THE MAXIMUM SHEAR VELOCITY
!      USTCWSM=USTCWSE

!-------------------------
! FOR WAVE-ONLY CONDITION
!-------------------------
      IF (UA .EQ. 0.0) THEN
! COVERT RIPPLE-ENHANCED SHEAR VELOCITIES TO SHEAR STRESSES
         TAOWS=RHOW*USTWSE**2
         TAOCRB=RHOW*USTCRB**2
         TAOCRS=RHOW*USTCRS**2
! CHECK IF SAND SUSPENSION OCCURS
         IF (USTCRS .LT. USTWSE) THEN
            TS1=PER/(2.*PII)*ACOS(TAOCRS/TAOWS)
            TS2=PER/2.-TS1
            PERSUSP=400.*TS1/PER
         ENDIF
! CHECK IF ONLY BEDLOAD TRANSPORT OCCURS AT TIMES
         IF (USTCRB .LT. USTCRS .AND. USTCRB .LT. USTWSE) THEN
            TB1=PER/(2.*PII)*ACOS(TAOCRB/TAOWS)
            TB2=PER/2.-TB1
            PERBED=400.*(TB1-TS1)/PER
         ENDIF
         RETURN
      ENDIF

!----------------------------------------
! CONSIDER COMBINED WAVES AND CURRENTS.
!----------------------------------------
! COVERT RIPPLE-ENHANCED SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCS=RHOW*USTCSE**2
      TAOWS=RHOW*USTWSE**2
      TAOCRB=RHOW*USTCRB**2
      TAOCRS=RHOW*USTCRS**2

!*** FIRST CALCULATE TIMES FOR SUSPENDED LOAD ***
      IF (USTCWSE .LT. USTCRS) THEN
         PERSUSP=0
         GO TO 50
      ENDIF
      B24ACS=(TAOCRS**2.0-(TAOCS*SIN(PHIB))**2.0)/(TAOWS**2.0)
      IF (B24ACS .LE. 0.0) THEN
! CRITICAL STRESS FOR SUSPENSION OF SEDIMENT ALWAYS EXCEEDED
         TS1=PER/2.
         PERSUSP=100.0
         PERBED=0.0
         RETURN
      ENDIF
      B=-TAOCS*COS(PHIB)/TAOWS
      XS1=B+SQRT(B24ACS)
      IF (XS1 .GE. 1.0) THEN
! CRITICAL STRESS FOR SUSPENSION OF SEDIMENT NEVER EXCEEDED
         PERSUSP=0.0
         GO TO 50
      ENDIF
      IF (XS1 .LE. -1.0) THEN
! SECOND CASE WHERE CRITICAL STRESS FOR SUSPENSION OF SEDIMENT IS
! ALWAYS EXCEEDED
         TS1=PER/2.
         PERSUSP=100.0
         PERBED=0.0
         RETURN
      ELSE
! CRITICAL STRESS FOR SUSPENSION OF SEDIMENT SOMETIMES EXCEEDED
         TS1=PER/(2.*PII)*ACOS(XS1)
      ENDIF
      XS2=B-SQRT(B24ACS)
      IF (XS2 .LE. -1.0) THEN
! CRITICAL STRESS FOR SUSPENSION OF SEDIMENT NOT EXCEEDED DURING TROUGH
         PERSUSP=200.*TS1/PER
      ELSE
! CRITICAL STRESS FOR SUSPENSION OF SEDIMENT EXCEEDED DURING TROUGH
         TS2=PER/(2.*PII)*ACOS(XS2)
         PERSUSP=(2.*(TS1-TS2)+PER)/PER*100.
      ENDIF

!**** CALCULATE TIMES FOR BEDLOAD ****
! CALCULATE TIMES FOR BEDLOAD ONLY IF USTCRB < USTCRS
  50  IF (USTCRB .GE. USTCRS ) RETURN

! FOR EFFECTIVE SHEAR VELOCITY < USTCRB, NO BEDLOAD TRANSPORT
      IF (USTCWSE .LT. USTCRB) THEN
         PERBED=0
         RETURN
      ENDIF
      B24ACB=(TAOCRB**2-(TAOCS*SIN(PHIB))**2)/(TAOWS**2)
      IF (B24ACB .LE. 0.0) THEN
! CRITICAL STRESS FOR BEDLOAD TRANSPORT ALWAYS EXCEEDED
         TB1=PER/2.
         PERBED=100.-PERSUSP
         RETURN
      ENDIF
      B=-TAOCS*COS(PHIB)/TAOWS
      XB1=B+SQRT(B24ACB)
      IF (XB1 .GE. 1.0) THEN
! CRITICAL STRESS FOR BEDLOAD TRANSPORT NEVER EXCEEDED
         PERBED=0.0
         RETURN
      ENDIF

      IF (XB1 .LE. -1.0) THEN
! SECOND CASE WHERE CRITICAL STRESS FOR BEDLOAD TRANSPORT
!                                       IS ALWAYS EXCEEDED.
          TB1=PER/2.
          PERBED=100.-PERSUSP
          RETURN
      ENDIF

! CRITICAL STRESS FOR BEDLOAD TRANSPORT EXCEEDED DURING CREST
      TB1=PER/(2.*PII)*ACOS(XB1)
      XB2=B-SQRT(B24ACB)
      IF (XB2 .LE. -1.0) THEN
! CRITICAL STRESS FOR BEDLOAD TRANSPORT NOT EXCEEDED DURING TROUGH
         PERBED=200.*TB1/PER-PERSUSP
      ELSE
! CRITICAL STRESS FOR BEDLOAD TRANSPORT EXCEEDED DURING TROUGH
         TB2=PER/(2.*PII)*ACOS(XB2)
         PERBED=(2.*(TB1-TB2)+PER)/PER*100.-PERSUSP
      ENDIF

      RETURN
      END subroutine

! ****************************************************************
! SUBROUTINE PROFL
! ****************************************************************
! THIS SUBROUTINE PREDICTS THE VELOCITY AND SUSPENDED SEDIMENT CONCENTRATION
! (SSC)PROFILES BASED ON THE GM86 COMBINED-FLOW BBL MODEL AND THE MODIFIED
! ROUSE EQUATION ACCORDING TO GLENN AND GRANT (1987).THE CALCULATED SSC AND
! VELOCITY PROFILES ARE THEN NUMERICALLY INTEGRATED TO OBTAIN THE SUSPENDED
! TRANSPORT RATE.
! 
! INPUT VARIABLES:
!
!  D = WATER DEPTH (M)
!  CDIR = CURRENT DIRECTION
!  DELTACW = HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
!  FALL = SAND SETTLING VELOCITY
!  RHOW = FLUID DENSITY
!  RHOS = SEDIMENT DENSITY (KG/M**3)
!  DX = DIMENSIONLESS GRAIN SIZE
!  GD = SEDIMENT GRAIN SIZE (M)
!  UB = WAVE ORBITAL VELOCITY
!  USTC = TOTAL CURRENT SHEAR VELOCITY
!  USTCRB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT (M/S)
!  USTCRS = CRITICAL FLUID VELOCITY FOR INITIATION OF SUSPENDED LOAD TRANSPORT (M/S)
!  USTCS = SKIN-FRICTION CURRENT SHEAR VELOCITY
!  USTCW = TOTAL COMBINED WAVE AND CURRENT SHEAR VELOCITY
!  USTCWS = SKIN-FRICTION COMBINED SHEAR VELOCITY                                    
!  USTCWSB = BEDLOAD COMBINED SHEAR VELOCITY
!  USTUP = CRITICAL FLUID VELOCITY FOR SHEET FLOW TRANSPORT (M/SEC)
!  USTW = TOTAL WAVE SHEAR VELOCITY
!  USTWS = SKIN-FRICTION WAVE SHEAR VELOCITY
!  Z0 = BED ROUGHNESS LENGTH (M)
!  Z0S = REFERENCE HEIGHT FOR C0 (M)
!  Z0C = APPARENT BED ROUGHNESS LENGTH (M)
!
! OUTPUT VARIABLES:
!  C0 = REFERENCE CONCENTRATION AT Z0 (KG/M^3)
!  C0A = DEPTH AVERAGED REFERENCE CONCENTRATION (KG/M^3)
!  QS = SUSPENDED SEDIMENT TRANSPORT RATE (KG/M/S)
!  QSDIR = DIRECTION OF SUSPENDED SEDIMENT TRANSPORT (DEGREE)
!
! INTERMEDIATE VARIABLES:
!
!  CZ = PREDICTED SUSPENDED SEDIMENT CONCENTRATION AT HEIGHT Z (KG/M^3)
!  CZD = PREDICTED SUSPENDED SEDIMENT CONCENTRATION AT DELTACW (KG/M^3)
!  TAOST = NORMALIZED EXCESS SHEAR STRESS
!  UUZ = PREDICTED MEAN VELOCITY AT HEIGHT Z (M/S)
!  ZZ = HEIGHT ABOVE THE SEABED (M)
!  ZKAPPA = VON KARMAN CONSTANT (=0.4)
!  ZZINI = DEFAULT VALUES FOR ZZ
!---------------------------------------------------------------------------

      SUBROUTINE PROFL(D,RHOW,FALL,UB,CDIR,USTCWS,USTCW,USTCRB,USTCRS,
     $USTUP,Z0C,DELTACW,USTCS,USTWS,USTCWSB,USTC,USTW,RPLCOEF,USTBF,
     $Z0,GD,DX,RHOS,QS,QSDIR,C0,C0A)

      IMPLICIT NONE

      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITYAT THE BOTTOM (M/S)
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCW	!COMBINED TOTAL SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTCRS   !CRITICAL SHEAR VEL FOR INITI OF SUSPENDED LOAD TRANSPORT (M/SEC)
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION Z0C      !APPARENT BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION DELTACW  !HEIGHT OF THE WAVE-CURRENT BOUNDARY LAYER
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION USTC     !TOTAL CURRENT SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTW     !TOTAL WAVE SHEAR VELOCITY OF GM
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION Z0       !BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S)   (KG/M**3)
      DOUBLE PRECISION QS	!SUSPENDED SEDIMENT TRANSPORT RATE (KG/M/S)
      DOUBLE PRECISION QSDIR	!DIRECTION OF SUSPENDED SEDIMENT TRANSPORT (DEGREE)
      DOUBLE PRECISION C0       !REFERENCE CONCENTRATION AT Z0 (KG/M^3)
      DOUBLE PRECISION C0A      !DEPTH AVERAGED REFERENCE CONCENTRATION AT Z0 (KG/M^3)

      DOUBLE PRECISION CZD	!PREDICTED SUSPENDED SEDIMENT CONCENTRATION AT DELTACW (KG/M^3)
      DOUBLE PRECISION ZKAPPA	!VON KARMAN CONSTANT (=0.4)
      DOUBLE PRECISION TAOST	!NORMALIZED EXCESS SHEAR STRESS
      DOUBLE PRECISION TAOWS	!SHEAR STRESS
      DOUBLE PRECISION TAOCWS	!SHEAR STRESS
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION TAOCS	!SHEAR STRESS
      DOUBLE PRECISION Z0S	!BED ROUGHNESS LENGTH FOR SUSPENDED (M)
      DOUBLE PRECISION ZZINI(0:18)	!DEFAULT VALUES FOR ZZ
      DOUBLE PRECISION ZZ(0:18),CZ(18),UUZ(18)
      DOUBLE PRECISION GETC0	!compute the reference concentration C0
      DOUBLE PRECISION GETC01	!compute the reference concentration C0
      DOUBLE PRECISION S	!RHOS/RHOW
      DOUBLE PRECISION SOM
      DOUBLE PRECISION CZZ

      INTEGER I,IL

! INITIALIZE ZZ, CZ AND UUZ
      DATA ZZINI /0.0,0.01,0.02,0.03,0.05,0.07,0.1,0.2,0.3,0.5,0.7,
     $1.0,2.0,4.0,6.0,8.0,12.0,15.0,20.0/

      DO 421,I=1,18
         ZZ(I)=ZZINI(I)
         CZ(I)=0.D0
         UUZ(I)=0.D0
421   CONTINUE

! GET THE UPPER LIMIT DEPTH FOR THE CONCENTRATION PROFILE
      IL = 18
      DO 12 I=1,IL
       IF (D.le.ZZ(I))THEN
        ZZ(I) = D
        IL = I
        GOTO 432
       END IF
 12    CONTINUE

432   CONTINUE

! SET DEFAULT VALUES TO ZERO
      C0=0.D0
      CZD=0.D0
      QS=0.D0
      C0A=0.D0
      SOM=0.D0

! ASSIGN VALUES TO CONSTANTS
      ZKAPPA=0.4D0

! CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCS=RHOW*USTCS**2
      TAOWS=RHOW*USTWS**2
      TAOCWS=RHOW*USTCWS**2
      TAOCRB=RHOW*USTCRB**2

! INITIALIZE VARIABLES

      S = RHOS/RHOW

!*****************************
! COMPUTE THE VELOCITY PROFILE
!*****************************
! PURE WAVE CASE:
      IF (USTCS.EQ.0) GOTO 25 
! PURE CURRENT CASE:
      IF (UB.LT.0.01) THEN
        DO 10 I=1,IL,1
        UUZ(I)=(USTC/ZKAPPA)*DLOG(ZZ(I)/Z0)    !al posto di ALOG 
        IF (UUZ(I).LT.0.) UUZ(I)=0.
 10     CONTINUE                             !c'e' DLOG 
      ELSE
! COMBINED WAVES AND CURRENT
        DO 20 I=1,IL,1
        IF (ZZ(I).LE.DELTACW) THEN
          UUZ(I)=(USTC/ZKAPPA)*(USTC/USTCW)*DLOG(ZZ(I)/Z0)
        ELSE
          UUZ(I)=(USTC/ZKAPPA)*DLOG(ZZ(I)/Z0C)
        ENDIF
        IF (UUZ(I).LT.0.) UUZ(I)=0.
 20     CONTINUE
      ENDIF 

!*****************************************************
! COMPUTE THE SUSPENDED SEDIMENT CONCENTRATION PROFILE
!*****************************************************
 25   CONTINUE
! PURE CURRENT CASE:
      IF (UB.LT.0.01) THEN
        IF (USTCS.LE.USTCRS) THEN
          QS=0
          GOTO 300
        ELSE
! CALCULATE REFERENCE CONCENTRATIONS C0 AT Z0S
          TAOST=(TAOCS-TAOCRB)/TAOCRB
          CALL GETC0_VE(RHOW,G,S,GD,TAOST,C0,Z0S)
!          CALL GETC0_SM(TAOCRB,RHOW,G,S,GD,USTCS,USTUP,TAOST,USTBF,
!     +                  C0,Z0S)
! 	   CALL GETC0_VR(GD,DX,Z0,TAOST,C0,Z0S)
!          CALL GETC0_ZF(TAOCS,RHOW,G,S,GD,C0,Z0S)
          DO 40 I=1,IL
            CZ(I)=C0*(ZZ(I)/Z0S)**(-0.74*FALL/(0.4*USTC))
            IF (CZ(I) .LT. 0.) CZ(I) = 0.
 40       CONTINUE
        ENDIF
      ELSE
        IF (USTCS.EQ.0) THEN
! PURE WAVE CASE:
          IF (USTWS.LE.USTCRS) THEN
            QS=0
            GOTO 300                          
          ELSE
! CALCULATE REFERENCE CONCENTRATIONS C0 AT Z0S
            TAOST=(TAOWS-TAOCRB)/TAOCRB
            CALL GETC0_VE(RHOW,G,S,GD,TAOST,C0,Z0S)
!            CALL GETC0_SM(TAOCRB,RHOW,G,S,GD,USTWS,USTUP,TAOST,USTBF,
!     +                    C0,Z0S)
!	     CALL GETC0_VR(GD,DX,Z0,TAOST,C0,Z0S)
!             CALL GETC0_ZF(TAOWS,RHOW,G,S,GD,C0,Z0S)
            DO 60 I=1,IL
              CZ(I)=C0*(ZZ(I)/Z0S)**(-0.74*FALL/(0.4*USTW))
              IF (CZ(I) .LT. 0.) CZ(I) = 0.
 60         CONTINUE
          ENDIF
        ELSE
! COMBINED WAVE-CURRENT CASE: 
! FOR USTCWSB<USTCRS,NO SUSPENSION PROFILE WILL BE PREDICTED
          IF (USTCWSB.LE.USTCRS) THEN
            QS=0
            GOTO 300
          ELSE
! CALCULATE REFERENCE CONCENTRATIONS C0 AT Z0S AND CZD AT DELTACW
            TAOST=(TAOCWS-TAOCRB)/TAOCRB
            CALL GETC0_VE(RHOW,G,S,GD,TAOST,C0,Z0S)
!            CALL GETC0_SM(TAOCRB,RHOW,G,S,GD,USTCWSB,USTUP,TAOST,USTBF,
!     +                    C0,Z0S)
!	     CALL GETC0_VR(GD,DX,Z0,TAOST,C0,Z0S)
!            CALL GETC0_ZF(TAOCWS,RHOW,G,S,GD,C0,Z0S)
            CZD=C0*(DELTACW/Z0S)**(-0.74*FALL/(0.4*USTCW))
! PREDICT SUSPENSION PROFILE BASED ON THE MODIFIED ROUSE EQUATION
            DO 90 I=1,IL
              IF (ZZ(I).LE.DELTACW) THEN
                CZ(I)=C0*(ZZ(I)/Z0S)**(-0.74*FALL/(0.4*USTCW))
              ELSE
                CZ(I)=CZD*(ZZ(I)/DELTACW)**(-0.74*FALL/(0.4*USTC))
              ENDIF
              IF (CZ(I) .LT. 0.) CZ(I) = 0.
 90         CONTINUE
          ENDIF
        ENDIF
      ENDIF

!*********************************************************************
! INTEGRATE VELOCITY AND SSC PROFILE FOR SUSPENDED LOAD TRNSPORT RATE
! AND CALCULATE THE DEPTH AVERAGED EQUILIBRIUM CONCENTRATION (C0A)  !ccf
!*********************************************************************

      ZZ(0)=0
      DO 95 I=1,IL
        QS=QS+(ZZ(I)-ZZ(I-1))*UUZ(I)*CZ(I)
        SOM = SOM + CZ(I)*(ZZ(I)-ZZ(I-1))
 95   CONTINUE
      C0A = SOM/D

! ASSIGN THE CURRENT DIRECTION AS THE DIRECTION OF QS
      QSDIR=CDIR

 300  CONTINUE
      RETURN
      END subroutine      

! ************************************************************
! SUBROUTINE GETC0_SM
! ************************************************************
! This subroutine computes the reference concentration C0 using 
! modified Smith and McLean (1977) at Z0S (see Sedtrans96).

      SUBROUTINE GETC0_SM(TAOCRB,RHOW,G,S,GD,UST,USTUP,TAOST,USTBF,
     +                    C0,Z0S)

      IMPLICIT NONE

      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION G        !GRAVITY
      DOUBLE PRECISION S	!RHOS/RHOW
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION UST	!SHEAR VELOCITY
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION TAOST    !NORMALIZED EXCESS SHEAR STRESS
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION CB	!BULK BOTTOM SEDIMENT CONCENTRATION (KG/M^3)
      DOUBLE PRECISION GAMMA0	!SAND RESUSPENSION COEFFICIENT
      DOUBLE PRECISION C0	!compute the reference concentration C0
      DOUBLE PRECISION Z0S	!BED ROUGHNESS LENGTH FOR SUSPENDED (M)

      CB=1722.5

! PREDICT GAMMA0 BASED ON LI ET AL.(IN 1996)
      IF (UST.LT.USTBF.OR.TAOST.LE.1.) THEN
        GAMMA0=0.0355*(TAOST**1.94)
      ELSEIF (UST.GE.USTBF.AND.UST.LT.USTUP) THEN
        GAMMA0=0.0206*(TAOST**(-1.931))
      ELSE
        GAMMA0=0.00013
      ENDIF

      C0 = CB*GAMMA0*TAOST

      !Z0S = (26.3*TAOCRB*TAOST)/(RHOW*G*(S-1)) + GD/12.
      Z0S = (0.15*GD*TAOST**1.5)/(RHOW*G*(S-1)) + GD/12.	!Cacchione 2008

      END subroutine

! ************************************************************
! SUBROUTINE GETC0_VE
! ************************************************************
! This subroutine computes the reference concentration C0 using 
! original Smith and McLean (1977) at Z0S using gamma parameter derived
! for Venice Lagoon (Amos et al, 2010, The measurement of sand transport 
! in two inlets of Venice lagoon, Italy).

      SUBROUTINE GETC0_VE(RHOW,G,S,GD,TAOST,C0,Z0S)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION G        !GRAVITY
      DOUBLE PRECISION S	!RHOS/RHOW
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION TAOST    !NORMALIZED EXCESS SHEAR STRESS
      DOUBLE PRECISION C0	!compute the reference concentration C0
      DOUBLE PRECISION Z0S	!BED ROUGHNESS LENGTH FOR SUSPENDED (M)
      DOUBLE PRECISION CB	!BULK BOTTOM SEDIMENT CONCENTRATION (KG/M^3)
      DOUBLE PRECISION GAMMA0	!SAND RESUSPENSION COEFFICIENT

      CB=1722.5

      GAMMA0=0.000354

      C0 = CB*GAMMA0*TAOST/(1.+GAMMA0*TAOST)

      Z0S = (0.15*GD*TAOST**1.5)/(RHOW*G*(S-1)) + GD/12.	!Cacchione 2008

      END subroutine

! ************************************************************
! SUBROUTINE GETC0_VR
! ************************************************************
! This subroutine computes the reference concentration C0 using
! Van Rjin (1993) at Z0S.

      SUBROUTINE GETC0_VR(GD,DX,Z0,TAOST,C0,Z0S)

      IMPLICIT NONE

      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE
      DOUBLE PRECISION Z0       !BED ROUGHNESS LENGTH (M)
      DOUBLE PRECISION TAOST    !NORMALIZED EXCESS SHEAR STRESS
      DOUBLE PRECISION C0	!compute the reference concentration C0
      DOUBLE PRECISION Z0S	!BED ROUGHNESS LENGTH FOR SUSPENDED (M)
      DOUBLE PRECISION CB	!BULK BOTTOM SEDIMENT CONCENTRATION (KG/M^3)

      CB = 1722.5

      Z0S = Z0*30 / 2.	
      Z0S = DMAX1(Z0S,0.01d0)

      C0 = CB * (0.015*GD*TAOST**1.5)/(Z0S*DX**0.3)

      END subroutine

! ************************************************************
! SUBROUTINE GETC0_ZF
! ************************************************************
! This subroutine computes the reference concentration C0 using
! Zyserman and Fredsoe (1994) at Z0S.

      SUBROUTINE GETC0_ZF(TAO,RHOW,G,S,GD,C0,Z0S)

      IMPLICIT NONE

      DOUBLE PRECISION TAO	!SHEAR STRESS
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION G        !GRAVITY
      DOUBLE PRECISION S	!RHOS/RHOW
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION C0	!compute the reference concentration C0
      DOUBLE PRECISION Z0S	!BED ROUGHNESS LENGTH FOR SUSPENDED (M)
      DOUBLE PRECISION CB	!BULK BOTTOM SEDIMENT CONCENTRATION (KG/M^3)
      DOUBLE PRECISION TAOS	!AUX
      DOUBLE PRECISION GAMM	!AUX

      CB=1722.5

      TAOS = TAO / (G*RHOW*(S-1)*GD)
      GAMM = (TAOS - 0.045)**1.75

      C0 = CB * (0.331*GAMM) / (1.D0 + 0.720 * GAMM) 
      Z0S = 2.D0 * GD

      END subroutine

! ************************************************************
! SUBROUTINE TRANSPO
! ************************************************************
!  TRANSPO4.FOR USES EFFECTIVE COMBINED SHEAR STRESS FOR TRANSPORT
!  CALCULATION. THE TOTAL TRANSPORT DUE TO TAOCWS IS OBTAINED FIRST. THE
!  ORIGINAL COEFFICIENTS OF 0.005 AND M=0.635 ARE USED IN BAGNLOD AND YALIN
!  RESPECTIVELY. THE OPTIONS ARE:
!      CASE O: USING THE AVERAGE GM SHEAR STRESS             NO
!      CASE A: USING EFFECTIVE SHEAR STRESS TAOCWE           NO
!      CASE B: USING THE AVERAGE (TAOCR+TAOCWE)/2            NO
!      CASE C: USING 1.4*TAOCWS OF WIBERG-NELSON STRESS      NO
!      CASE D: USING NEW EFFECTIVE SHEAR STRESS              YES
!      CASE E: USING 1.65*TCWS OF WIBERG-NELSON              NO
!
!  THIS SUBROUTINE CALCULATES THE TIME-AVERAGED NET SEDIMENT TRANSPORT
!  BY A CHOICE OF METHODS.  FOR THE PURE WAVE CASE THERE IS NO NET
!  TRANSPORT SINCE TRANSPORT DURING THE WAVE CREST IS EQUAL AND OPPOSITE
!  TO THAT DURING THE WAVE TROUGH (DUE TO THE USE OF LINEAR WAVE THEORY).
!  FOR THE PURE CURRENT AND MIXED CONDITIONS, THE USER MAKES A CHOICE
!  BETWEEN TRANSPORT FORMULAE, HOWEVER IF SUSPENDED LOAD TRANSPORT IS
!  SIGNIFICANT IT IS RECOMMENDED THAT A TOTAL LOAD FORMULA BE USED.
!
!  THE OPTIONS AVAILABLE ARE:
!
!            1 - ENGELUND-HANSEN (1967) TOTAL LOAD EQUATION,
!            2 - EINSTEIN-BROWN (1950) BEDLOAD EQUATION,
!            3 - BAGNOLD (1963) TOTAL LOAD EQUATION,
!            4 - YALIN (1963) BEDLOAD EQUATION,
!            5 - VAN RIJN (1993) BEDLOAD EQUATION,
!
!   THE CHOICE IS CONTROLLED BE THE VALUE OF "IOPT1" (1 TO 7) WHICH
!   IS READ IN IN SUBROUTINE READIN (READBCH FOR THE BATCH PROGRAM)
!
! INPUT VARIABLES:
!       D = WATER DEPTH (M)                                          (INDATA96 file)
!     PER = WAVE PERIOD (SEC)                                        (INDATA96 file)
!      GD = SEDIMENT GRAIN SIZE (M)                                  (INDATA96 file)
!    BETA = BED SLOPE (DEGREE)                                       (INDATA96 file)
!    CDIR = CURRENT DIRECTION (AZIMUTH,DEGREES)                      (INDATA96 file)
!    WDIR = WAVE DIRECTION (AZIMUTH,DEGREES)                         (INDATA96 file)
!   IOPT1 = OPTION SELECTED FOR SEDIMENT TRANSPORT FORMULA           (INDATA96 file)
!   RKERO = PROPORT. COEFFICIENT FOR EROSION RATE (DEFAULT = 1.62)*  (INDATA96 file)
!    RHOS = SEDIMENT DENSITY (KG/M**3)                               (READIN subroutine)
!    RHOW = FLUID DENSITY (KG/M**3)                                  (READIN subroutine)
!      UB = MAXIMUM WAVE-INDUCED BOTTOM PARTICLE VELOCITY (M/SEC)    (OSCIL subroutine)
!  USTCRB = CRITICAL SHEAR VELOCITY FOR BEDLOAD TRANSPORT            (THRESH subroutine)
!   USTUP = CRITICAL SHEAR VEL. BED SHEET FLOW (M/SEC) 		     (THRESH subroutine)
!      UA = CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC (M/SEC)   (FRICFAC subroutine)
!    U100 = CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)               (FRICFAC subroutine)
!    PHIB = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS WITHIN THE     (FRICFAC subroutine)
!           WAVE BOUNDARY LAYER (RADIANS)
!  PHI100 = ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M.        (FRICFAC subroutine)
!           ABOVE SEABED (RADIANS)
!     FCW = BOTTOM FRICTION FACTOR                                   (FRICFAC subroutine)
! RPLCOEF = RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION         (FRICFAC subroutine)
!   USTCS = MAXIMUM CURRENT SKIN-FRICTION SHEAR VELOCITY             (FRICFAC subroutine)
!   USTWS = GM WAVE SKIN-FRICTION SHEAR VELOCITY                     (FRICFAC subroutine)
!  USTCWS = GM COMBINED WAVE-CURRENT SKIN-FRICTION SHEAR VELOCITY    (FRICFAC subroutine)
! USTCWSB = TRANSPORT-RELATED COMBINED SHEAR VELOCITY                (FRICFAC subroutine)
!     TB1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD      (TIMING subroutine)
!           TRANSPORT CEASES (SEC)
!     TB2 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH BEDLOAD      (TIMING subroutine)
!           TRANSPORT RECOMMENCES (SEC)
!     TS1 = TIME, AFTER PASSAGE OF WAVE CREST, AT WHICH SUSPENDED    (TIMING subroutine)
!           LOAD TRANSPORT CEASES (SEC)
!  PERBED = PERCENTAGE OF TIME SPENT IN BEDLOAD TRANSPORT PHASE      (TIMING subroutine)
! PERSUSP = PERCENTAGE OF TIME SPENT IN SUSP. LOAD TRANSPORT PHASE   (TIMING subroutine)
!    VISK = KINEMATIC VISCOSITY OF FLUID (M**2/SEC)
!      DX = DIMENSIONLESS GRAIN SIZE
!    FALL = SPHERICAL-GRAIN SETTLING VELOCITY [M/S]
!      HT = WAVE HEIGHT (M)
!
! OUTPUT VARIABLES:
!     SED = TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME TRANSPORTED PER
!           UNIT BED WIDTH PER UNIT TIME (M**3/M/S)
!    SEDM = TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS OF SEDIMENT
!           SOLIDS TRANSPORTED PER UNIT BED WIDTH PER UNIT TIME (KG/M/S)
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!
! INTERMEDIATE VARIABLES:
!      G = GRAVITY ACCELERATION
!   DRHO = RHOS-RHOW
! DGAMMA = G*DRHO
!    PHI = ANGLE OF REPOSE : ESTIMATED TO BE 30
!      S = SPECIFIC GRAVITY OF WATER
!    VCB = CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT
!     DS = DIFFERENCE BETWEEN SPECIFIC GRAVITY OF SEDIMENT AND WATER
!  TAOCS = SKINFRICTION CURRENT SHEAR STRESS
!  TAOWS = SKINFRICTION WAVE SHEAR STRESS
!---------------------------------------------------------------------------

      SUBROUTINE TRANSPO(D,UA,UB,U100,PER,GD,BETA,RHOS,RHOW,USTCRB,
     $USTUP,PHIB,PHI100,USTCS,USTWS,USTCWSB,RPLCOEF,TB1,TB2,TS1,
     $PERBED,PERSUSP,USTBF,FCW,USTCWS,HT,CDIR,WDIR,IOPT1,DX,FALL,
     $SED,SEDM,SEDDIR)

      IMPLICIT NONE

      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION BETA     !BED SLOPE (DEGREE)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S) (KG/M**3)
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION FCW		!BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION HT       !WAVE HEIGHT (M)
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)
      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION TB1      !TIME AT WHICH BEDLOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION TB2      !TIME AT WHICH BEDLOAD TRANSPORT RECOMMENCES (SEC)
      DOUBLE PRECISION TS1      !TIME AT WHICH SUSPENDED LOAD TRANSPORT CEASES (SEC)
      DOUBLE PRECISION PERBED   !PERCENTAGE OF TIME SPENT IN ONLY BEDLOAD TRANSPORT PHASE
      DOUBLE PRECISION PERSUSP  !PERCENTAGE OF TIME SPENT IN SUSPENDED LOAD TRANSPORT PHASE
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDM	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS MASS (KG/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      DOUBLE PRECISION DRHO	!RHOS-RHOW
      DOUBLE PRECISION DGAMMA	!G*DRHO
      DOUBLE PRECISION PHI	!ANGLE OF REPOSE : ESTIMATED TO BE 30
      DOUBLE PRECISION VCB	!CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT
      DOUBLE PRECISION TAO0	!CURRENT SHEAR STRESS
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION TIMEC	!SEDIMENT TRANSPORT TIME UNDER CREST
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      INTEGER NC,NT

!----------------------------------------------
! INITIALIZE AND DEFINE CONSTANTS AND VARIABLES
!----------------------------------------------
      DRHO=RHOS-RHOW
      DGAMMA=G*DRHO
      PHI=30
      IF (PER .NE. 0.0) W=2.*PII/PER
      VCB=0.0                        !ggu INTEL
      SED=0.0
      SEDM=0.0
      SEDDIR=0.0
      RLWRT = 0.0
      TAO0 = 0.0

! CALCULATE BEDLOAD THRESHOLD FLOW VELOCITY VCB
      TAOCRB=RHOW*USTCRB**2
      VCB=SQRT(2.*TAOCRB/(RHOW*FCW))

!******************************
!  WAVES ONLY (NO NET CURRENT)
!******************************
!  FOR THE PURE WAVE CASE, NO NET TRANSPORT OCCURS. EXIT PROGRAM.
!      IF (UA .EQ. 0.0 .AND. IOPT1. NE. 7) RETURN

!******************************
!  CURRENT ONLY (NO WAVES)
!******************************
!  NO INTEGRATION IS REQUIRED FOR THE PURE CURRENT CASE.
!  WHEN TRANSPORT IS ALL IN SUSPENDED LOAD, THEN A WARNING MESSAGE
!  IS PRINTED CONCERNING THE USE OF "BEDLOAD" TRANSPORT PREDICTORS

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 888

! CHECK IF THERE IS ANY TRANSPORT, IF NOT THEN EXIT.
      IF (PERBED .EQ. 0.0 .AND. PERSUSP .EQ. 0.0) RETURN

! OBTAIN CURRENT SHEAR STRESS TAO0, UA=U100 FOR STEADY CURRENT
!      TAO0=RHOW*FCW/2.*UA**2
      TAO0=RHOW*USTCS**2
      
      GOTO 777

 888  CONTINUE

!******************************
!  COMBINED WAVE AND CURRENT 
!******************************
!
!  THE COMBINED WAVE AND CURRENT CASE REQUIRES INTEGRATION OF THE
!  INSTANTANEOUS TRANSPORT OVER THE WAVE PERIOD. THE USE OF LWT ALLOWS
!  INTEGRATION TO BE DONE OVER ONLY HALF A WAVE CYCLE. BAGNOLD'S METHOD
!  DOES NOT REQUIRE INTEGRATION. THE X- AND Y- COMPONENTS OF TRANSPORT 
!  ARE CONSIDERED SEPARATELY, WHERE THE X-COMPONENT IS PARALLEL TO THE 
!  WAVE DIRECTION AND THE Y-COMPONENT IS NORMAL TO THE WAVE DIRECTION.
!
!  INTERMEDIATE VARIABLES:
!      STEPC = INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
!      STEPT = INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
!      TIMEC = SEDIMENT TRANSPORT TIME UNDER CREST
!      TIMET = SEDIMENT TRANSPORT TIME UNDER TROUGH 
!      RLWRT = LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH

!  CHECK IF CRITICAL STRESS IS EVER EXCEEDED, IF NOT THEN EXIT.
      IF (TB1 .EQ. 0.0 .AND. TS1 .EQ. 0.0) RETURN
      IF (PERBED .EQ. 0.0 .AND. PERSUSP .EQ. 0.0) RETURN

! COMPUTE THE TRANSPORT TIMES FOR WAVE CREST AND TROUGH RESPECTIVELY
      IF (TB1 .EQ. PER/2. .OR. TS1 .EQ. PER/2.) THEN
         TIMEC = PER/4.
         TIMET = PER/4.
         RLWRT = PER/4.
      ELSE IF (TB1 .GT. 0. .AND. TB2 .GT. 0.) THEN
         TIMEC = TB1
         TIMET = PER/2. - TB2
         RLWRT = TB2
      ELSE 
         TIMEC=TB1                 
         TIMET=0.
      ENDIF

! COMPUTE THE INTEGRATION TIME STEPS
      IF (TIMEC .GE. 10.) THEN
         NC = 20
      ELSE
         NC = 10
      ENDIF
      IF (TIMET .GE. 10.) THEN
         NT = 20
      ELSE
         NT = 10
      ENDIF
      STEPC = TIMEC/NC
      STEPT = TIMET/NT

 777  CONTINUE

!-----------------------------------
! DECIDE FORMULA ACCORDING TO IOPT1
!-----------------------------------
      
      GOTO (1,2,3,4,5) IOPT1

 1    CALL ENGHAN(RHOW,DGAMMA,GD,U100,USTCS,USTWS,USTCWS,USTCWSB,
     $PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,STEPC,STEPT,
     $RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998

 2    CALL EINBWN(RHOW,DGAMMA,GD,FALL,USTCS,USTWS,USTCWS,USTCWSB,
     $PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,STEPC,STEPT,
     $RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998
 
 3    CALL BAGNLD(RHOW,DGAMMA,U100,CDIR,USTCWS,USTCWSB,
     $USTCRB,USTBF,USTUP,RPLCOEF,SED,SEDDIR,UB,GD,RHOS,VCB)
      GOTO 998

 4    CALL YALIN(RHOW,RHOS,GD,USTCS,USTWS,USTCWS,USTCWSB,
     $PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,STEPC,STEPT,
     $RLWRT,UB,FCW,UA,VCB,TAOCRB,G,DRHO,IOPT1,
     $PER,PHI100,CDIR,WDIR,SED,SEDDIR)
      GOTO 998

 5    CALL VANRIJN(RHOW,RHOS,HT,D,DX,GD,USTCS,USTWS,USTCWS,
     $USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,
     $G,SED,SEDDIR)
      GOTO 998

 998  CONTINUE

! INLUDE BED-SLOPE EFFECT AND THEN CONVERT SED FROM VOLUME FLUX (M^2/M/S)
! TO MASS FLUX SEDM IN KG/SEC/M.

      SED=SED*(1-TAN(BETA/57.3)/TAN(PHI/57.3))
      SEDM = RHOS*SED
      RETURN
      END subroutine

!****************************************************************************
! SUBROUTINE ENGHAN
!***********************************************************************
! THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
! THE ENGELUND AND HANSEN (1967) TOTAL LOAD EQUATION.
!
! OUTPUT VARIABLE:
!     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!---------------------------------------------------------------------------

      SUBROUTINE ENGHAN(RHOW,DGAMMA,GD,U100,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,
     $STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION DGAMMA	!G*DRHO
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      INTEGER NC,NT
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION TAO0	!CURRENT SHEAR STRESS
      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION V,CONST1,CONST2

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

!****************************
!  CURRENT ONLY (NO WAVES)
!****************************

      V=U100
      SED=0.05*V**2*SQRT(TAO0**3*RHOW)/(GD*DGAMMA**2)
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

!*****************************
!  COMBINED WAVE AND CURRENT
!*****************************

      CONST1=0.05*(RHOW*U100/DGAMMA)**2/GD
      CONST2=3.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END subroutine

!*********************************************************************
! SUBROUTINE EINBWN
!***********************************************************************
! THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
! THE EINSTEIN AND BROWN (1950) BEDLOAD EQUATION.
!
! OUTPUT VARIABLE:
!     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!---------------------------------------------------------------------------

      SUBROUTINE EINBWN(RHOW,DGAMMA,GD,FALL,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,
     $STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION DGAMMA	!G*DRHO
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION FALL     !SETTLING VELOCITY FOR NON-COHESIVE SEDIMENT (M/SEC)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      INTEGER NC,NT
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION TAO0	!CURRENT SHEAR STRESS
      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION CONST1,CONST2

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

!****************************
!  CURRENT ONLY (NO WAVES)
!****************************

      SED = 40.0*FALL*GD*(TAO0/(DGAMMA*GD))**3
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

!*****************************
!  COMBINED WAVE AND CURRENT
!*****************************

      CONST1=40*FALL*GD*(RHOW/(GD*DGAMMA))**3
      CONST2=6.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END subroutine

!*********************************************************************
! SUBROUTINE BAGNLD
!*********************************************************************
! THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
! THE BAGNOLD (1963) TOTAL LOAD EQUATION.
!
! OUTPUT VARIABLE:
!     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!---------------------------------------------------------------------------

      SUBROUTINE BAGNLD(RHOW,DGAMMA,U100,CDIR,USTCWS,USTCWSB,
     $USTCRB,USTBF,USTUP,RPLCOEF,SED,SEDDIR,UB,GD,RHOS,VCB)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION DGAMMA	!G*DRHO
      DOUBLE PRECISION U100     !CURRENT SPEED AT 1 M. ABOVE SEABED (M/SEC)
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S) (KG/M**3)
      DOUBLE PRECISION VCB	!CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION USTCWSE  !EFFECTIVE COMBINED SKIN-FRICTION SHEAR VELOCITY
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION TAOCWS	!SHEAR STRESS
      DOUBLE PRECISION TAOCWSE	!SHEAR STRESS
      DOUBLE PRECISION USTCWSGM	!AUX
      DOUBLE PRECISION BETA,ALPHA,EST,RK

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

!****************************
!  CURRENT ONLY (NO WAVES)
!****************************

      BETA=1.73D0
      IF (GD .LE. 0.00031) BETA=7.22D0
      SED=BETA/RHOS*(U100-VCB)**3
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

!*****************************
!  COMBINED WAVE AND CURRENT
!*****************************

      USTCWSGM=USTCWS
      ALPHA=(USTUP-USTCWSB)/(USTUP-USTBF)
! COMPUTE EFFECTIVE SHEAR VELOCITY AT THE RIPPLE CREST
      USTCWSE=USTCWS*RPLCOEF
! CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCRB = RHOW*USTCRB**2
      TAOCWS = RHOW*USTCWS**2
      TAOCWSE = RHOW*USTCWSE**2

! COMPUTE THE EFFECTIVE SHEAR STRESS CAUSING SEDIMENT TRANSPORT
! -----------------------------------------
! CASE O: NO
! USING THE AVERAGE GM SHEAR STRESS
!      TAOCWS=TAOCWS
! -----------------------------------------
! -----------------------------------------
! CASE A: NO
! USING THE RIPPLE-ENHANCED SHEAR STRESS
!      TAOCWS=TAOCWSE
! -----------------------------------------
! -----------------------------------------
! CASE B: NO
! USING THE AVERAGE SHEAR STRESS OF TAOCRB AND TAOCWSE
!      TAOCWS = 0.5*(TAOCRB+TAOCWSE)
! -----------------------------------------
! -------------------------------------------
! CASE D: YES
! USING THE NEW EFFECTIVE SHEAR STRESS

! FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
      ENDIF

! FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
! CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
!         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
! CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
      ENDIF

! FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
      ENDIF

! FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
! -------------------------------------------

! COMPUTE NORMALIZED EXCESS SHEAR STRESS EST
      EST = (TAOCWS - TAOCRB)/TAOCRB

! CALCULATE TOTAL TRANSPORT RATE; RK IS BASED ON STERNBERG (1972)
        IF (EST .LT. 0) THEN
           SED = 0
        ELSE
           RK = 0.005*EXP(0.7*EST)
           SED=RK*TAOCWS*U100/DGAMMA
        ENDIF
        SEDDIR=CDIR
      RETURN
      END subroutine

!*********************************************************************
! SUBROUTINE YALIN
!*********************************************************************
! THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
! THE YALIN (1963) BEDLOAD EQUATION.
!
! OUTPUT VARIABLE:
!     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!---------------------------------------------------------------------------

      SUBROUTINE YALIN(RHOW,RHOS,GD,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,TIMET,
     $STEPC,STEPT,RLWRT,UB,FCW,UA,VCB,TAOCRB,G,DRHO,IOPT1,
     $PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S) (KG/M**3)
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      INTEGER NC,NT
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION FCW              !BOTTOM (SKIN) FRICTION FACTOR
      DOUBLE PRECISION UA       !CURRENT SPEED TO BE USED IN BOTTOM STRESS CALC. (M/SEC)
      DOUBLE PRECISION VCB	!CRITICAL FLUID VELOCITY FOR INITIATION OF BEDLOAD TRANSPORT
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION G        !GRAVITY
      DOUBLE PRECISION DRHO	!RHOS-RHOW
      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION CONST1,CONST2
      DOUBLE PRECISION S,A,USTAR

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

!****************************
!  CURRENT ONLY (NO WAVES)
!****************************

      USTAR=SQRT(FCW/2.)*UA
      S=(UA/VCB)**2-1.0
      A=2.45*(RHOW/RHOS)**0.4*SQRT(TAOCRB/(G*DRHO*GD))
      SED=0.635*GD*USTAR*S*(1.0-DLOG(1.0+A*S)/(A*S))
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

!*****************************
!  COMBINED WAVE AND CURRENT
!*****************************

      CONST1=0.635*GD
      CONST2=2.45*(RHOW/RHOS)**0.4*SQRT(TAOCRB/(G*DRHO*GD))

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END subroutine

!*********************************************************************
! SUBROUTINE VANRIJN
!*********************************************************************
! THIS SUBROUTINE CALCULATES SEDIMENT TRANSPORT RATES ACCORDING TO
! THE VAN RIJN (1993) BEDLOAD EQUATION
!
! OUTPUT VARIABLE:
!     SED = VOLUME RATE OF SEDIMENT TRANSPORT PER UNIT WIDTH OF BED
!  SEDDIR = DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)
!
! INTERMEDIATE VARIABLE:
!     S = INSTANTANEOUS SHEAR STRESS PARAMETER [ADIMENSIONAL]
!     A = CALIBRATION FACTOR
!---------------------------------------------------------------------------

      SUBROUTINE VANRIJN(RHOW,RHOS,HT,D,DX,GD,USTCS,USTWS,USTCWS,
     $USTCWSB,TAOCRB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,UB,TAO0,IOPT1,PER,PHI100,CDIR,WDIR,
     $G,SED,SEDDIR)

      IMPLICIT NONE

      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION RHOS     !DENSITY OF SEDIMENT MINERAL(S) (KG/M**3)
      DOUBLE PRECISION HT       !WAVE HEIGHT (M)
      DOUBLE PRECISION D        !WATER DEPTH (M)
      DOUBLE PRECISION DX       !DIMENSIONLESS GRAIN SIZE
      DOUBLE PRECISION GD       !SEDIMENT GRAIN DIAMETER (M)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      INTEGER NC,NT
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      DOUBLE PRECISION UB       !MAXIMUM WAVE INDUCED ORBITAL VELOCITY AT THE BOTTOM (M/S)
      DOUBLE PRECISION TAO0	!CURRENT SHEAR STRESS
      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION CONST1,CONST2
      DOUBLE PRECISION F,F1,EEXP,S,A

      DOUBLE PRECISION G        !GRAVITY

!  SKIP THIS SECTION IF THERE ARE BOTH WAVES AND CURRENTS
      IF (UB .GT. 0.01) GOTO 123

!****************************
!  CURRENT ONLY (NO WAVES)
!****************************

      F = RHOS/RHOW
      F1 = F - 1.
      S = TAO0/TAOCRB - 1.
      IF (S .LT. 3) THEN
        EEXP = 2.1
      ELSE
        EEXP = 1.5
      END IF

      SED = 0.053*(F1*G)**0.5 * GD**1.5 * DX**(-0.3) * S**EEXP
      SEDDIR=CDIR
      RETURN

 123  CONTINUE

!*****************************
!  COMBINED WAVE AND CURRENT
!*****************************

      A = 1. - (HT/D)**0.5
      CONST1=0.25*A*GD*DX**(-0.3)
      CONST2=1.

      CALL WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      RETURN
      END subroutine

!*********************************************************************
! SUBROUTINE WAVEINT
!*********************************************************************
! THIS SUBROUTINE COMPUTE THE TIME AVERAGED TRANSPORT (AS VOLUME)
! THROUGH THE INTEGRATION OVER A WAVE CYCLE
!
!  INTERMEDIATE VARIABLES:
!         S = INSTANTANEOUS SHEAR STRESS PARAMETER [ADIMENSIONAL]
!       GSXC = VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPC
!       GSXT = VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPT
!      SEDXC = TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER CREST   
!      SEDXT = TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER TROUGH   
!      SEDYC = TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER CREST   
!      SEDYT = TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER TROUGH   
!---------------------------------------------------------------------------

      SUBROUTINE WAVEINT(IOPT1,CONST1,CONST2,RHOW,USTCS,USTWS,USTCWS,
     $USTCWSB,PHIB,USTCRB,USTBF,USTUP,RPLCOEF,W,NC,NT,
     $TIMET,STEPC,STEPT,RLWRT,PER,PHI100,CDIR,WDIR,SED,SEDDIR)

      IMPLICIT NONE

      INTEGER IOPT1             !SEDIMENT TRANSPORT FORMULA OPTION NUMBER
      DOUBLE PRECISION CONST1,CONST2
      DOUBLE PRECISION RHOW     !DENSITY OF FLUID (WATER)  (KG/M**3)
      DOUBLE PRECISION USTCS    !CURRENT SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTWS    !WAVE SKIN-FRICTION SHEAR VELOCITY OF GM 
      DOUBLE PRECISION USTCWS	!COMBINED SKIN-FRICTION SHEAR VELOCITY OF GM
      DOUBLE PRECISION USTCWSB  !TRANSPORT-RELATED COMBINED SHEAR VELOCITY
      DOUBLE PRECISION PHIB     !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS (RADIANS)
      DOUBLE PRECISION USTCRB   !CRITICAL SHEAR VEL FOR INITI OF BEDLOAD TRANS (M/SEC)
      DOUBLE PRECISION USTBF    !CRITICAL SHEAR VELOCITY FOR RIPPLE BREAKOFF
      DOUBLE PRECISION USTUP    !CRITICAL SHEAR VEL FOR INITN OF SHEET FLOW TRANSPORT (M/SEC)
      DOUBLE PRECISION RPLCOEF  !RIPPLE COEFFICIENT FOR SHEAR VELOCITY CONVERSION
      DOUBLE PRECISION W	!WAVE ANGULAR FREQUENCY (RAD/SEC)
      INTEGER NC,NT
      DOUBLE PRECISION TIMET	!SEDIMENT TRANSPORT TIME UNDER TROUGH 
      DOUBLE PRECISION STEPC	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER CREST
      DOUBLE PRECISION STEPT	!INTEGRATION TIME STEP OF SEDIMENT TRANSPORT UNDER TROUGH 
      DOUBLE PRECISION RLWRT	!LOWER LIMIT OF TRANSPORT INTEGRATION UNDER TROUGH
      DOUBLE PRECISION PER      !WAVE PERIOD (S)
      DOUBLE PRECISION PHI100   !ANGLE BETWEEN WAVE AND CURRENT DIRECTIONS AT 1 M. ABOVE SEABED
      DOUBLE PRECISION CDIR     !DIRECTION OF AMBIENT CURRENT (DEGREES TRUE)
      DOUBLE PRECISION WDIR     !WAVE PROPAGATION DIRECTION (DEGREES TRUE)

      DOUBLE PRECISION SED	!TIME-AVERAGED NET SEDIMENT TRANSPORT AS VOLUME (M**3/S/M)
      DOUBLE PRECISION SEDDIR	!DIRECTION OF NET SEDIMENT TRANSPORT (AZIMUTH,DEGREES)

      DOUBLE PRECISION USTCSGM	!
      DOUBLE PRECISION USTWSGM	!
      DOUBLE PRECISION USTCWSGM	!
      DOUBLE PRECISION TAOCRB	!SHEAR STRESS
      DOUBLE PRECISION TAOCS	!SHEAR STRESS
      DOUBLE PRECISION TAOWS	!SHEAR STRESS
      DOUBLE PRECISION S	!INSTANTANEOUS SHEAR STRESS PARAMETER [ADIMENSIONAL]
      DOUBLE PRECISION GSXC	!VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPC
      DOUBLE PRECISION GSXT	!VOLUME TRANSPORT IN WAVE DIRECTION IN TIME STEPT
      DOUBLE PRECISION SEDXC	!TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER CREST   
      DOUBLE PRECISION SEDXT	!TOTAL VOLUME TRANSPORT IN WAVE DIRECTION UNDER TROUGH   
      DOUBLE PRECISION SEDYC	!TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER CREST   
      DOUBLE PRECISION SEDYT	!TOTAL VOLUME TRANSPORT IN Y DIRECTION UNDER TROUGH   
      DOUBLE PRECISION ALPHA,CONST3
      DOUBLE PRECISION TAOCSX,TAOCSY,TAOCWSX
      DOUBLE PRECISION TAOCWSE,TAOCWS
      DOUBLE PRECISION GSYC,GSYT,FACTOR,PHIS
      DOUBLE PRECISION SEDX,SEDY
      DOUBLE PRECISION DIF,CWDIF
      INTEGER I

      GSXC = 0.0
      GSXT = 0.0
      GSYC = 0.0
      GSYT = 0.0
      SEDXC= 0.0
      SEDYC= 0.0
      SEDXT= 0.0
      SEDYT= 0.0
      CONST3 = CONST2

! PRESERVE THE BURST-AVERAGED SHEAR VELOCITIES
      USTCSGM=USTCS
      USTWSGM=USTWS
      USTCWSGM=USTCWS
      ALPHA=(USTUP-USTCWSB)/(USTUP-USTBF)

! CONVERT SHEAR VELOCITIES TO SHEAR STRESSES
      TAOCRB=RHOW*USTCRB**2
      TAOCS=RHOW*USTCS**2
      TAOWS=RHOW*USTWS**2

! COMPUTE STEADY CURRENT SHEAR STRESS IN THE X DIRECTION
      TAOCSX=TAOCS*COS(PHIB)

! COMPUTE STEADY CURRENT SHEAR STRESS IN THE Y DIRECTION
      TAOCSY=TAOCS*SIN(PHIB)

! ===================================================================
! COMPUTE TRANSPORT VOLUME UNDER THE WAVE CREST
      DO 111 I = 0, (NC-1)
         TAOCWSX=TAOCSX+0.5*TAOWS*(COS(W*I*STEPC)+COS(W*(I+1)*STEPC))
         TAOCWS=SQRT(TAOCSY**2+TAOCWSX**2)
         TAOCWSE=(RPLCOEF**2)*TAOCWS

! -----------------------------------------
! CASE O: NO
! USING THE AVERAGE GM SHEAR STRESS
!         USTCWS=SQRT(TAOCWS/RHOW)
! -----------------------------------------
! -------------------------------------------
! CASE A: NO
! USING EFFECTIVE SHEAR STRESS
!         USTCWS=RPLCOEF*SQRT(TAOCWS/RHOW)
! -------------------------------------------
! -------------------------------------------
! CASE B: NO
! USING THE AVERAGED EFFECTIVE SHEAR STRESS OF TAOCRB AND TAOCWSE
!         TAOCWS=0.5*(TAOCRB+TAOCWSE)
!         USTCWS=SQRT(TAOCWS/RHOW)
! -----------------------------------------
! -----------------------------------------
! CASE C: NO OR CASE E: NO
! USING THE EFFECTIVE SHEAR STRESS OF WIBERG-NELSON (1992)
!
! FOR CASE C
!         IF (USTCWSB .LT. USTBF) TAOCWS=1.4*TAOCWS
!         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
!            TAOCWS=(1+0.4*ALPHA)*TAOCWS
!         ENDIF
!         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
!
! FOR CASE E
!         IF (USTCWSB .LT. USTBF) TAOCWS=1.65*TAOCWS
!         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
!            TAOCWS=(1+0.65*ALPHA)*TAOCWS
!         ENDIF
!         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
!
!         USTCWS=SQRT(TAOCWS/RHOW)
! -------------------------------------------
! -------------------------------------------
! CASE D: YES
! USING THE NEW EFFECTIVE SHEAR STRESS

! FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
! CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
!         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
!         USTCWS=SQRT(TAOCWS/RHOW)
! CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) USTCWS=SQRT(TAOCWS/RHOW)
! -------------------------------------------

         S=TAOCWS/TAOCRB - 1.
         IF (S .LT. 0) S = 0.0
         IF(IOPT1.EQ.4)THEN
           FACTOR = DABS(S-LOG(1+CONST3*S)/CONST3)
           CONST2 = 1.
         ELSEIF(IOPT1.EQ.5)THEN
           FACTOR = S**1.5
         ELSE
           FACTOR = 1.
         ENDIF

         SED=CONST1*STEPC*FACTOR*USTCWS**CONST2
         PHIS=ATAN2(TAOCSY,TAOCWSX)
         GSXC=SED*COS(PHIS)
         GSYC=SED*SIN(PHIS)
         SEDXC=SEDXC+GSXC
         SEDYC=SEDYC+GSYC
 111  CONTINUE
      SEDXC=2*SEDXC
      SEDYC=2*SEDYC

! ===================================================================
! COMPUTE TRANSPORT VOLUME UNDER THE WAVE TROUGH

! IF NO TRANSPORT OCCURRED UNDER WAVE TROUGH, EXIT
      IF (TIMET .EQ. 0.) GO TO 333

      DO 222 I = 0, (NT-1)
         TAOCWSX=TAOCSX+0.5*TAOWS*(COS(W*(RLWRT+I*STEPT))
     $           +COS(W*(RLWRT+(I+1)*STEPT)))
         TAOCWS=SQRT(TAOCSY**2+TAOCWSX**2)
         TAOCWSE=(RPLCOEF**2)*TAOCWS

! -------------------------------------------
! CASE O: NO
! USING THE AVERAGE GM SHEAR STRESS
!         USTCWS=SQRT(TAOCWS/RHOW)
! -------------------------------------------
! -------------------------------------------
! CASE A: NO
! USING EFFECTIVE SHEAR STRESS
!         USTCWS=RPLCOEF*SQRT(TAOCWS/RHOW)
! -------------------------------------------
! -----------------------------------------
! CASE B: NO
! USING THE AVERAGED EFFECTIVE SHEAR STRESS 0.5(TAOCRB+TAOCWSE)
!         TAOCWS=0.5*(TAOCRB+TAOCWSE)
!         USTCWS=SQRT(TAOCWS/RHOW)
! -----------------------------------------
! -----------------------------------------
! CASE C: NO OR CASE E: NO
! USING THE EFFECTIVE SHEAR STRESS OF WIBERG-NELSON (1992)
!
! FOR CASE C, TAOCWSE=1.4TAOCWS
!         IF (USTCWSB .LT. USTBF) TAOCWS=1.4*TAOCWS
!         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
!            TAOCWS=(1+0.4*ALPHA)*TAOCWS
!         ENDIF
!         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
!
! FOR CASE E, TAOCWSE=1.65TAOCWS
!         IF (USTCWSB .LT. USTBF) TAOCWS=1.65*TAOCWS
!         IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
!            TAOCWS=(1+0.65*ALPHA)*TAOCWS
!         ENDIF
!         IF (USTCWSB .GE. USTUP) TAOCWS=TAOCWS
!
!         USTCWS=SQRT(TAOCWS/RHOW)
! -------------------------------------------
! -------------------------------------------
! CASE D: YES
! USING THE NEW EFFECTIVE SHEAR STRESS

! FOR WEAK-TRANSPORT RIPPLES
      IF (USTCWSGM .LT. USTCRB) THEN
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR EQUILIBRIUM RIPPLES
      IF (USTCWSB .GE. USTCRB .AND. USTCWSB .LT. USTBF) THEN
! CHOICE A: USE (TAOCRB+TAOCWS+TAOCWSE)/3 FOR USTCWSB<USTBF
!         TAOCWS=0.333*(TAOCRB+TAOCWS+TAOCWSE)
!         USTCWS=SQRT(TAOCWS/RHOW)
! CHOICE B: USE (TAOCRB+TAOCWSE)/2 FOR USTCWSB<USTBF
         TAOCWS=0.5*(TAOCRB+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR BREAK-OFF RIPPLES
      IF (USTCWSB .GE. USTBF .AND. USTCWSB .LT. USTUP) THEN
         TAOCWS=(1/(2+ALPHA))*(ALPHA*TAOCRB+TAOCWS+TAOCWSE)
         USTCWS=SQRT(TAOCWS/RHOW)
      ENDIF

! FOR UPPER-PLANE BED
      IF (USTCWSB .GE. USTUP) USTCWS=SQRT(TAOCWS/RHOW)
! -------------------------------------------

         S=TAOCWS/TAOCRB - 1.
         IF (S .LT. 0) S = 0.0
         IF(IOPT1.EQ.4)THEN
           FACTOR = DABS(S-LOG(1+CONST3*S)/CONST3)
           CONST2 = 1.
         ELSEIF(IOPT1.EQ.5)THEN
           FACTOR = S**1.5
         ELSE
           FACTOR = 1.
         ENDIF

         SED=CONST1*STEPT*FACTOR*USTCWS**CONST2
         PHIS=ATAN2(TAOCSY,TAOCWSX)
         IF (PHIS .LT. 0) THEN
               GSXT=-SED*COS(PHIS)
               GSYT=-SED*SIN(PHIS)
         ELSE
               GSXT=SED*COS(PHIS)
               GSYT=SED*SIN(PHIS)
         ENDIF
         SEDXT=SEDXT+GSXT
         SEDYT=SEDYT+GSYT
 222  CONTINUE
      SEDXT=2*SEDXT
      SEDYT=2*SEDYT
! ===================================================================

 333  CONTINUE

! RETURN THE BURST-AVERAGED SHEAR VELOCITIES
      USTCS=USTCSGM
      USTWS=USTWSGM
      USTCWS=USTCWSGM

! COMPUTE THE DIRECTION OF THE TRANSPORT
      SEDX=(SEDXC+SEDXT)/PER
      SEDY=(SEDYC+SEDYT)/PER
      SED=SQRT(SEDX**2+SEDY**2)
      IF (SEDY .EQ. 0. .AND. SEDX .EQ. 0.) THEN
         SEDDIR = 0.
      ELSE
         PHIS=ATAN2(SEDY,SEDX)
         DIF=SIGN((PHI100-PHIS)*180./PII,CDIR-WDIR)
         CWDIF=ABS(CDIR-WDIR)
         IF(CWDIF .LE. 90.0) SEDDIR=CDIR-DIF
         IF (CWDIF .LE. 180.0 .AND. CWDIF .GT. 90.0) SEDDIR=CDIR+DIF
         IF (CWDIF .LE. 270.0 .AND. CWDIF .GT. 180.0) SEDDIR=CDIR-DIF
         IF (CWDIF .LE. 360.0 .AND. CWDIF .GT. 270.0) SEDDIR=CDIR+DIF
         IF (SEDDIR .LT. 0.0) SEDDIR=SEDDIR+360.0
         IF (SEDDIR .GE. 360.0) SEDDIR=SEDDIR-360.0
      ENDIF

      IF(USTCS .EQ. 0.) SEDDIR = WDIR

      RETURN
      END subroutine


! ======================================================================
! SUBROUTINE COHESIVE
!
! Compute the erosion or deposition of cohesive sediment during a time
! step TIMEDR for a given shear velocity USTCWS.
! Return alos the average transport rate of suspended sediment SEDM.

      SUBROUTINE COHESIVE (NBCONC,CONC,BEDCHA,MAXBED,TIMEDR,USTCWS,
     &U100,D,RHOW,VISK,AULVA,EDRATE,ZS,SEDM,TCONC,TAOS)

      IMPLICIT NONE

! ********************** INPUT VARIABLES *******************
      INTEGER NBCONC            ! number of Ws classes = elements in CONC and in WSI
      DOUBLE PRECISION CONC(NBCONC) !array of suspended sediment concentration (kg/m3)
                               ! for each Ws class,               user input
                               ! Ws of each class is in WSI(i) (common block common WSCLASS)
      INTEGER MAXBED           ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                               ! (1) depth below sediment surface (m)
                               ! (2) critical erosion stress (Pa)
                               ! (3) dry bulk density (kg/m**3)          user input
      DOUBLE PRECISION TIMEDR  ! Duration of call to Sedtrans (s)         user input
      DOUBLE PRECISION USTCWS  ! Combined skin friction stress            FRICFRAC
      DOUBLE PRECISION U100    ! current speed at 1m above seabed (m/sec) FRICFRAC
      DOUBLE PRECISION D       ! water depth (m)                          user input
      DOUBLE PRECISION VISK    ! kinematic viscosity (m**2/s)            from main program
      DOUBLE PRECISION RHOW    ! density of fluid (water)  (kg/m**3)      user input
      DOUBLE PRECISION AULVA   ! fraction of bed area covered by the algae 'Ulva' (range 0-1) user input

! ********************** OUTPUT VARIABLES *******************
      DOUBLE PRECISION EDRATE  ! mean erosion/deposition rate (kg/m^2/s)
                               ! (positive: erosion, negative: deposition
      DOUBLE PRECISION ZS      ! Height change in bed surface (m)
                               ! (positive: erosion, negative: deposition)
      DOUBLE PRECISION SEDM    ! time-averaged net sediment transport as mass (kg/s/m)
      DOUBLE PRECISION TCONC   ! final total suspended sediment concentration (kg/m**3)
      DOUBLE PRECISION TAOS    ! solid transmitted stress due to Ulva (Pa)

! ********************** INTERMEDIATE VARIABLES *******************
      INTEGER NBED             ! number of rows in BEDCHA that are used
      DOUBLE PRECISION TAU0    ! effective skin friction shear stress (Pa)
      DOUBLE PRECISION TCONC1  ! total SSC (sum of CONC) before deposition (kg/m**3)
      DOUBLE PRECISION DMASS   ! deposited mass (kg)
      DOUBLE PRECISION EMASS   ! eroded mass (kg)
      DOUBLE PRECISION TAUCE   ! critical erosion stress at depth ZS (Pa)
      DOUBLE PRECISION MAXEMASS! Maximum erodable sediment mass to avoid problem with drag reduction

      INTEGER I              ! counter for loops

! **********************************************************************
! Initialisation of some variables
      ZS=0d0
      EMASS=0d0		          ! Eroded mass
      DMASS=0d0                ! Deposited mass
      TCONC1=0.D0

! **********************************************************************
! Find the number of rows that are used (NBED). This is indicated by z=0 in
! the row after the last used row.

      DO 10, I=2,MAXBED
         IF (BEDCHA(I,1).EQ.0.0) GOTO 20
10    CONTINUE
20    NBED=I-1

! **********************************************************************
! Compute the total SSC before deposition and erosion

      DO 210, I=1,NBCONC
         TCONC1=TCONC1+CONC(I)
210   CONTINUE

! **********************************************************************
! Compute TAU0, the effective skin friction stress acting on the bed
! drag reduction and solid transmitted stress due to ulva

      TAU0=RHOW*USTCWS**2

      CALL DRAGRED(TAU0,TCONC1,D,MAXEMASS)

      CALL SOLULVA(TAU0,AULVA,TAOS)

! **********************************************************************
! EROSION FIRST PART
! Test if effective bed shear stress (TAU0) is larger or equal to
! surface critical erosion stress (BEDCHA(1,2)

      IF ( TAU0.GT.BEDCHA(1,2) .OR. ( TAU0.EQ.BEDCHA(1,2).AND.
     &   ( TAU0.GE.BEDCHA(2,2) .OR. NBED.EQ.1) ) ) THEN

         CALL EROS1(NBED,MAXBED,BEDCHA,TAU0,MAXEMASS,EMASS,ZS,
     $TIMEDR,TAUCE)

         CALL BEDERO(NBED,MAXBED,BEDCHA,ZS)

      ENDIF    ! end of erosion, 1st part

! **********************************************************************
! DEPOSITION
! Compute deposition only if there are suspended sediments

      IF (TCONC1.EQ.0d0) THEN
         TCONC=0.D0
      ELSE
         CALL DEPOS(D,TIMEDR,NBCONC,CONC,TAU0,RHOW,TCONC1,TCONC,
     $VISK,DMASS)

         CALL BEDDEP(NBED,MAXBED,BEDCHA,TAU0,DMASS,ZS)
      ENDIF

! **********************************************************************
! EROSION SECOND PART
! Put eroded sediment in suspension

      IF (EMASS.GT.0.) THEN
         CALL EROS2(TAUCE,D,RHOW,TAU0,NBCONC,CONC,EMASS,TCONC)
      ENDIF

! **********************************************************************
! FINAL EROSION/DEPOSITION OPERATION
! Compute the deposition rate

      EDRATE=(EMASS-DMASS)/TIMEDR

! **********************************************************************
! Sediment transport is computed from the mean SSC

      SEDM=0.5d0*(TCONC1+TCONC) * D * U100

! **********************************************************************
! Apply bed-compaction for the duration TIMEDR

      IF (DOCOMPACT.NE.0) CALL COMPACT(BEDCHA,MAXBED,TIMEDR,ZS)

      END subroutine  ! end of subroutine COHESIVE

! ======================================================================
! ======================================================================
! SUBROUTINE EROS1
! EROSION FIRST PART : test if erosion
!                      compute erosion rate and eroded depth, and
!                      check that TAUCE(ZS)<=TAU0
!                      compute eroded mass EMASS, which is used later

      SUBROUTINE EROS1(NBED,MAXBED,BEDCHA,TAU0,MAXEMASS,EMASS,ZS,
     $TIMEDR,TAUCE)

      IMPLICIT NONE

! **** INPUT VARIABLES ****
      INTEGER NBED             ! number of rows in BEDCHA that are used
      INTEGER MAXBED           ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                               ! (1) depth below sediment surface (m)
                               ! (2) critical erosion stress (Pa)
                               ! (3) dry bulk density (kg/m**3)          user input
      DOUBLE PRECISION TAU0    ! effective skin friction shear stress (Pa)
      DOUBLE PRECISION MAXEMASS! Maximum erodable sediment mass to avoid problem with drag reduction
      DOUBLE PRECISION TIMEDR  ! Duration of call to Sedtrans (s)         user input
! **** OUTPUT VARIABLES ****
      DOUBLE PRECISION EMASS   ! eroded mass (kg)
      DOUBLE PRECISION RHOS    ! dry bulk density at depth ZS (kg/m**3)
      DOUBLE PRECISION TAUCE   ! critical erosion stress at depth ZS (Pa)
      DOUBLE PRECISION ZS      ! Height change in bed surface (m)
                               !(positive: erosion, negative: deposition)
! **** INTERMEDIATE VARIABLES ****
      DOUBLE PRECISION DZ      ! Height of deposited mud layer (m) / eroded depth in layer IB
      DOUBLE PRECISION T1      ! remaining time during erosion step
      DOUBLE PRECISION M1      ! eroded mass in this layer
      DOUBLE PRECISION RE      ! erosion rate (kg/m**2/s)
      DOUBLE PRECISION RHOA,RHOB,HLAYER,MLAYER,TAUCES ! for erosion steps
      INTEGER I                ! counter for loops
      INTEGER IB               ! counter for loops in BEDCHA

! Statement function which interpolate linearly the value Y of (X,Y),
! which located on the line between (X1,Y1) and (X2,Y2)
      DOUBLE PRECISION LIN,X1,X2,Y1,Y2,X
      LIN(X1,X2,Y1,Y2,X)=Y1 + (X-X1)/(X2-X1)*(Y2-Y1)

      RHOS = 0. 
      M1 = 0.

! Find the erosion depth ZS and also TAUCE and RHOS at this depth
! Each layer is processed successively until the duration TIMEDR is finished
      T1=TIMEDR ! time remaining for erosion
      DO 110, IB=1,NBED
! Density variation in this layer RHO = RHOA * dz + RHOB
         RHOB=BEDCHA(IB,3)
         IF(IB.EQ.NBED) THEN
            RHOA=0d0
            HLAYER=1000d0
         ELSE
            HLAYER=(BEDCHA(IB+1,1)-BEDCHA(IB,1))
            RHOA=(BEDCHA(IB+1,3)-BEDCHA(IB,3)) / HLAYER
         ENDIF
         MLAYER=(RHOB+RHOA*HLAYER/2d0)*HLAYER   ! total mass of layer
! Do 2 iterations do define the eroded depth, for the first RE is calculated
! with TAUCES = Tce(top), for the second TAUCES = (Tce(top)+Tce(dz))/2
         TAUCES=BEDCHA(IB,2) ! for first iteration
         DO 100, I=1,2
            RE=E0*EXP(RKERO*(TAU0-TAUCES)**0.5d0) ! erosion rate
            M1=RE*T1    ! eroded mass
            M1=DMIN1(M1,MAXEMASS-EMASS)
            IF(M1.GE.MLAYER) THEN
               DZ=HLAYER    ! erosion depth in this layer
               TAUCE=BEDCHA(MIN(IB+1,NBED),2)
            ELSE
               IF(RHOA.EQ.0) THEN
                  DZ=M1/RHOB
               ELSE
                  DZ=(-RHOB+SQRT(RHOB**2+2d0*RHOA*M1))/RHOA
               ENDIF
               IF(IB.EQ.NBED) THEN
                  TAUCE=BEDCHA(IB,2)
               ELSE
                 TAUCE=LIN(0d0,HLAYER,BEDCHA(IB,2),BEDCHA(IB+1,2),DZ)
               ENDIF
            ENDIF
            IF(TAU0.LT.TAUCE)THEN ! check if erosion went to deep (into too strong sediments
               ZS=LIN(BEDCHA(IB,2),BEDCHA(IB+1,2),
     &               BEDCHA(IB,1),BEDCHA(IB+1,1),TAU0)
               TAUCE=TAU0
               RHOS=LIN(BEDCHA(IB,1),BEDCHA(IB+1,1),
     &              BEDCHA(IB,3),BEDCHA(IB+1,3),ZS)
               GOTO 120  ! out of loop DO 100
            ENDIF
            IF(I.EQ.1) THEN
               TAUCES=(TAUCES+TAUCE)/2d0 ! for second iteration
            ELSE
               IF(M1.LE.MLAYER)THEN ! the layer is not eroded completely
                  RHOS=DZ*RHOA+RHOB
                  ZS=DZ+BEDCHA(IB,1)
                  GOTO 120  ! out of loop DO 100
               ENDIF
               T1=T1-(MLAYER/RE) ! substract from T1 the time taken to erode this layer
            ENDIF
100      CONTINUE
         EMASS=EMASS+M1  ! first approximation for drag-reduction limitation
110   CONTINUE  ! this loop will always be exited by a GOTO 120

120   IF(IB.LT.NBED) IB=IB+1
! At this stage, IB can have following values
!    if ZS > BEDCHA(NBED,1)               IB=NBED   (also if NBED=1)
!    if ZS = BEDCHA(n,1)                  IB=n
!    if BEDCHA(n-1,1) < ZS < BEDCHA(n,1)  IB=n

! Compute the eroded mass, EMASS
      EMASS=0d0 ! EMASS was used above for temporary storage
      IF(ZS.GT.BEDCHA(NBED,1) .OR. ZS.EQ.BEDCHA(IB,1)) THEN
         DO 140, I=1,IB-1
            EMASS=EMASS + (BEDCHA(I+1,1)-BEDCHA(I,1))*
     &         (BEDCHA(I,3)+BEDCHA(I+1,3))*0.5d0   ! dZ * (RHOS1+RHOS2)/2
140      CONTINUE
         EMASS=EMASS + (ZS-BEDCHA(IB,1))*BEDCHA(IB,3) ! dZ * RHOS
      ELSE
         DO 150, I=1,IB-2
            EMASS=EMASS + (BEDCHA(I+1,1)-BEDCHA(I,1))*
     &         (BEDCHA(I,3)+BEDCHA(I+1,3))*0.5d0    ! dZ * (RHOS1+RHOS2)/2
150      CONTINUE
         EMASS=EMASS+(ZS-BEDCHA(IB-1,1))*(BEDCHA(IB-1,3)+RHOS)*0.5d0
      ENDIF

      END subroutine

! ======================================================================
! SUBROUTINE BEDERO(MAXBED,BEDCHA,ZS)
! Compute the bed characteristics after erosion (truncate the bed at depth ZS)

      SUBROUTINE BEDERO(NBED,MAXBED,BEDCHA,ZS)

      IMPLICIT NONE

      INTEGER NBED             ! number of rows in BEDCHA that are used
      INTEGER MAXBED           ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                               ! (1) depth below sediment surface (m)
                               ! (2) critical erosion stress (Pa)
                               ! (3) dry bulk density (kg/m**3)
      DOUBLE PRECISION ZS      ! Height change in bed surface (m)
      DOUBLE PRECISION TAUCE   ! critical erosion stress at depth ZS (Pa)
      DOUBLE PRECISION RHOS    ! dry bulk density at depth ZS (kg/m**3)
      INTEGER MOVEZ            ! nb of row that are reshuffled after erosion
      INTEGER I                ! counter for loops
      INTEGER IB               ! first row in BEDCHA strictly below ZS
! Statement function interpolating linearly the value Y of (X,Y),
! which is located on the line between (X1,Y1) and (X2,Y2)
      DOUBLE PRECISION LIN,X1,X2,Y1,Y2,X
      LIN(X1,X2,Y1,Y2,X)=Y1 + (X-X1)/(X2-X1)*(Y2-Y1)

      IF (NBED.GT.1) THEN      ! nothing has to be done if NBED=1
         IF (ZS.GE.BEDCHA(NBED,1)) THEN
! The bed is eroded below the deepest point of BEDCHA
            TAUCE=BEDCHA(NBED,2)
            RHOS=BEDCHA(NBED,3)
            MOVEZ=NBED-1
            IB=NBED+1
         ELSE
! Find next point below erosion depth, and interpolate TAUCE and RHOS
            DO 20,IB=2,NBED
               IF (ZS.LT.BEDCHA(IB,1)) GOTO 30
20          CONTINUE
30          TAUCE=LIN(BEDCHA(IB-1,1), BEDCHA(IB,1),
     &                BEDCHA(IB-1,2), BEDCHA(IB,2), ZS)
            RHOS= LIN(BEDCHA(IB-1,1), BEDCHA(IB,1),
     &                BEDCHA(IB-1,3), BEDCHA(IB,3), ZS)
            MOVEZ=IB-2
         ENDIF
! Modify BEDCHA
         BEDCHA(1,2)=TAUCE
         BEDCHA(1,3)=RHOS
         DO 170, I=IB,NBED
            BEDCHA(I-MOVEZ,1)=BEDCHA(I,1)-ZS
            BEDCHA(I-MOVEZ,2)=BEDCHA(I,2)
            BEDCHA(I-MOVEZ,3)=BEDCHA(I,3)
170      CONTINUE
!  set rows after last used row to zero
         DO 180, I=NBED-MOVEZ+1,NBED
            BEDCHA(I,1)=0d0
            BEDCHA(I,2)=0d0
            BEDCHA(I,3)=0d0
180      CONTINUE
         NBED=NBED-MOVEZ
      ENDIF

      END subroutine

! ======================================================================
! SUBROUTINE DEPOS
! DEPOSITION: - deposit only if SSC>0
!             - loop trough each Ws class
!             - compute TAUCD (include floccultion effects)

      SUBROUTINE DEPOS(D,TIMEDR,NBCONC,CONC,TAU0,RHOW,TCONC1,TCONC2,
     $VISK,DMASS)

      IMPLICIT NONE

! **** INPUT VARIABLES ****
      DOUBLE PRECISION D       ! water depth (m)                          user input
      DOUBLE PRECISION TIMEDR  ! Duration of call to Sedtrans (s)         user input
      DOUBLE PRECISION TAU0    ! effective skin friction shear stress (Pa)
      DOUBLE PRECISION RHOW    ! density of fluid (water)  (kg/m**3)      user input
      DOUBLE PRECISION TCONC1  ! total SSC (sum of CONC) before deposition (kg/m**3)
      DOUBLE PRECISION VISK    ! kinematic viscosity (m**2/s)            from main program
      INTEGER NBCONC           ! number of Ws classes = elements in CONC and in WSI
! **** INPUT and OUTPUT VARIABLES ****
      DOUBLE PRECISION CONC(NBCONC) !array of suspended sediment concentration (kg/m3)
                               ! for each Ws class,               user input
                               ! Ws of each class is in WSI(i) (common block common WSCLASS)
! **** OUTPUT VARIABLES ****
      DOUBLE PRECISION TCONC2  ! total SSC (sum of CONC) after deposition (kg/m**3)
      DOUBLE PRECISION DMASS   ! deposited mass (kg)
! **** INTERMEDIATE VARIABLES ****
      DOUBLE PRECISION TAUCD   ! critical deposition stress for the considered floc class (Pa)
      DOUBLE PRECISION WS      ! settling velocity of the considered floc class (m/s)
! for flocculation calculation
      DOUBLE PRECISION WSFLOC  ! Ws computed for flocculation
      DOUBLE PRECISION DE,CF   ! intermediate value for flocculation equation
      DOUBLE PRECISION RHOFLOC ! density of flocs
      DOUBLE PRECISION CVOL    ! volume concentration
      DOUBLE PRECISION MEANWS  ! ~mean log(Ws)
      INTEGER I                ! counter for loops

      WSFLOC = 0.
      MEANWS = 0. 

! Computed median Ws of flocculated particles (WSFLOC) according to Whitehouse et al. 1999
! An alternative would be to use Andy Manning's formula which include also turbulence
      IF(TCONC1.GT.CLIM1) THEN
         IF(TCONC1.LE.CLIM2) THEN
            WSFLOC=KFLOC*(TCONC1**MFLOC)
         ELSEIF(TCONC1.LE.50) THEN
            RHOFLOC=RHOW+0.03d0*(RHOCLAY-RHOW)
            CVOL=TCONC1/RHOCLAY
            DE=SQRT(19.8d0*RHOW*VISK*RHOCLAY**MFLOC*KFLOC/
     &         (9.81d0*(RHOFLOC-RHOW))) * CVOL**(MFLOC/2d0)
            CF=(RHOCLAY-RHOW)*CVOL/(RHOFLOC-RHOW)
            WSFLOC=VISK/DE*((10.36d0**2+1.049d0*(1-CF)**4.7d0*
     &         DE**3 * 9.81d0*(RHOFLOC-RHOW)/(RHOW*VISK**2) )**0.5d0
     &         -10.36d0)
         ELSEIF(TCONC1.LE.82) THEN
            WSFLOC=0.00462d0*(1d0-0.01d0*TCONC1)**3.54d0
         ELSE
            WSFLOC=1d-5
         ENDIF
! compute the mean log(Ws)
         MEANWS=0
         DO 212,I=1,NBCONC
            MEANWS=MEANWS+LOG10(WSI(I))*CONC(I)
212      CONTINUE
         MEANWS=10**(MEANWS/TCONC1)

      ENDIF

! LOOP FOR EACH SUSPENDED SEDIMENT CLASS
      DO 220 I=NBCONC,1,-1
         IF (CONC(I).GT.0.D0) THEN ! SKIP CLASS IF EMPTY
            WS=WSI(I) ! compute free settling velocity for this class
            IF(TCONC1.GT.CLIM1) THEN ! correct for flocculation
               WS=MAX(WS,WSFLOC* SQRT(WS/MEANWS) )
            ENDIF
            TAUCD=CTAUDEP*RHOW*0.64D0*WS**2 ! compute the critical stress for deposition
            TAUCD=CTAUDEP*2800D0*WS**1.03 ! compute the critical stress for deposition
					  ! as in sedtrans05 paper

! Check if effective skin friction stress below TAUCD
            IF (TAU0.GE.TAUCD) THEN
! if this class do not settle, the finer class will also not  --> exit Ws loop 220
               GOTO 230
            ELSEIF (CONC(I).GT.0) THEN
! COMPUTE THE CONCENTRATION REMAINING IN SUSPENSION AFTER DEPOSITION DURING TIME TIMEDR
               CONC(I)=CONC(I) *
     &                 EXP(-WS*(1D0-TAU0/TAUCD)*(1d0-PRS)*TIMEDR/D)
! IF CONCENTRATION IN ONE CLASS FALL BELOW 0.00001 KG/M**3, DEPOSIT EVERYTHING
               IF (CONC(I) .LE. 0.00001D0) THEN
                  CONC(I)=0.D0
               ENDIF
            ENDIF
         ENDIF
220   CONTINUE

! COMPUTE THE TOTAL SSC AFTER THE DEPOSITION (but before erosion)
230   TCONC2=0.d0
      DO 240 I=1,NBCONC
        TCONC2=TCONC2+CONC(I)
240   CONTINUE
! Compute the total deposited mass (positive value)
      DMASS=(TCONC1-TCONC2)*D

      END subroutine

! ======================================================================
! SUBROUTINE BEDDEP
! Compute the bed characteristics after deposition

      SUBROUTINE BEDDEP(NBED,MAXBED,BEDCHA,TAU0,DMASS,ZS)

      IMPLICIT NONE

! **** INPUT VARIABLES ****
      INTEGER NBED             ! number of rows in BEDCHA that are used
      INTEGER MAXBED           ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION TAU0    ! effective skin friction shear stress (Pa)
      DOUBLE PRECISION DMASS   ! deposited mass (kg)
! **** INPUT and OUTPUT VARIABLES ****
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                               ! (1) depth below sediment surface (m)
                               ! (2) critical erosion stress (Pa)
                               ! (3) dry bulk density (kg/m**3)          user input
! **** OUTPUT VARIABLES ****
      DOUBLE PRECISION ZS      ! Height change in bed surface (m)
! **** INTERMEDIATE VARIABLES ****
      DOUBLE PRECISION DZ      ! Height of deposited mud layer (m) / eroded depth in layer IB
      INTEGER IB               ! counter for loops in BEDCHA

      IF(DMASS.GT.0) THEN
! ADD THE DEPOSITED SEDIMENT TO THE BED
         IF(BEDCHA(2,1).LT.0.01. AND. ABS(BEDCHA(1,3)-RHOMUD).LT.1.
     &   .AND. ABS(BEDCHA(2,3)-RHOMUD).LT.2. .AND. NBED.GT.1) THEN
! If the surface is already a fluid mud layer (thickness<1cm, density close to RHOMUD)
! add sediment to the top layer
!   (DZ+z2)*(RHOMUD+r2)/2 = DMASS + z2*(r1+r2)/2
            DZ=(2*DMASS + BEDCHA(2,1)*(BEDCHA(1,3)+BEDCHA(2,3))) /
     &            (RHOMUD+BEDCHA(2,3)) - BEDCHA(2,1)
            DO 250, IB=2,NBED
               BEDCHA(IB,1)=BEDCHA(IB,1)+DZ
250         CONTINUE
         ELSE
! otherwise add a new layer to the bed
!   DZ*(RHOMUD+r1)/2 = DMASS      DZ has a POSITIVE value for deposition
            DZ=DMASS*2/(RHOMUD+BEDCHA(1,3))

            IF(NBED.EQ.MAXBED) THEN
               CALL REMOVEBED(BEDCHA,MAXBED,-1d0)
               NBED=NBED-1
            ENDIF
! Shift all element of BEDCHA one row down
            DO 260, IB=NBED,1,-1
               BEDCHA(IB+1,1)=BEDCHA(IB,1)+DZ
               BEDCHA(IB+1,2)=BEDCHA(IB,2)
               BEDCHA(IB+1,3)=BEDCHA(IB,3)
260         CONTINUE
            NBED=NBED+1
            IF (NBED.LT.MAXBED) BEDCHA(NBED+1,1)=0
         ENDIF

! Compute a erosion threshold slighly higher than current TAU0, but not
! higher than the previous bed surface.
         BEDCHA(1,2)=DMIN1(TAU0*1.1d0,BEDCHA(1,2))
! erosion threshold not lower than a minimal value, using the same formula
! than in the subroutine COMPACT
         BEDCHA(1,2)=MAX(BEDCHA(1,2),TEROA*(RHOMUD**TEROB))

         BEDCHA(1,3)=RHOMUD
         ZS=ZS-DZ          !  ZS has a NEGATIVE value for deposition

      ENDIF

      END subroutine

! ======================================================================
! SUBROUTINE WSINKFLOC

      SUBROUTINE WSINKFLOC(NBCONC,CONC,RHOW,VISK,WS)

      IMPLICIT NONE

! **** INPUT VARIABLES ****
      DOUBLE PRECISION RHOW    ! density of fluid (water)  (kg/m**3)      user input
      DOUBLE PRECISION VISK    ! kinematic viscosity (m**2/s)            from main program
      INTEGER NBCONC           ! number of Ws classes = elements in CONC and in WSI
! **** INPUT and OUTPUT VARIABLES ****
      DOUBLE PRECISION CONC(NBCONC) !array of suspended sediment concentration (kg/m3)
                               ! for each Ws class,               user input
                               ! Ws of each class is in WSI(i) (common block common WSCLASS)
! **** OUTPUT VARIABLES ****
      DOUBLE PRECISION WS(NBCONC)      ! settling velocity of the considered floc class (m/s)
! for flocculation calculation
      DOUBLE PRECISION TCONC1  ! total SSC (sum of CONC) before deposition (kg/m**3)
      DOUBLE PRECISION WSFLOC  ! Ws computed for flocculation
      DOUBLE PRECISION DE,CF   ! intermediate value for flocculation equation
      DOUBLE PRECISION RHOFLOC ! density of flocs
      DOUBLE PRECISION CVOL    ! volume concentration
      DOUBLE PRECISION MEANWS  ! ~mean log(Ws)
      INTEGER I                ! counter for loops

      WSFLOC = 0.
      MEANWS = 0. 

      TCONC1 = 0.
      DO 210, I=1,NBCONC
         TCONC1=TCONC1+CONC(I)
210   CONTINUE

! Computed median Ws of flocculated particles (WSFLOC) according to Whitehouse et al. 1999
! An alternative would be to use Andy Manning's formula which include also turbulence
      IF(TCONC1.GT.CLIM1) THEN
         IF(TCONC1.LE.CLIM2) THEN
            WSFLOC=KFLOC*(TCONC1**MFLOC)
         ELSEIF(TCONC1.LE.50) THEN
            RHOFLOC=RHOW+0.03d0*(RHOCLAY-RHOW)
            CVOL=TCONC1/RHOCLAY
            DE=SQRT(19.8d0*RHOW*VISK*RHOCLAY**MFLOC*KFLOC/
     &         (9.81d0*(RHOFLOC-RHOW))) * CVOL**(MFLOC/2d0)
            CF=(RHOCLAY-RHOW)*CVOL/(RHOFLOC-RHOW)
            WSFLOC=VISK/DE*((10.36d0**2+1.049d0*(1-CF)**4.7d0*
     &         DE**3 * 9.81d0*(RHOFLOC-RHOW)/(RHOW*VISK**2) )**0.5d0
     &         -10.36d0)
         ELSEIF(TCONC1.LE.82) THEN
            WSFLOC=0.00462d0*(1d0-0.01d0*TCONC1)**3.54d0
         ELSE
            WSFLOC=1d-5
         ENDIF
! compute the mean log(Ws)
         MEANWS=0
         DO 212,I=1,NBCONC
            MEANWS=MEANWS+LOG10(WSI(I))*CONC(I)
212      CONTINUE
         MEANWS=10**(MEANWS/TCONC1)

      ENDIF

! LOOP FOR EACH SUSPENDED SEDIMENT CLASS
      DO 220 I=NBCONC,1,-1
         IF (CONC(I).GT.0.D0) THEN ! SKIP CLASS IF EMPTY
            WS(I)=WSI(I) ! compute free settling velocity for this class
            IF(TCONC1.GT.CLIM1) THEN ! correct for flocculation
               WS(I)=MAX(WS(I),WSFLOC* SQRT(WS(I)/MEANWS) )
            ENDIF
	 ELSE
            WS(I)=WSI(I) ! compute free settling velocity for this class
         ENDIF
220   CONTINUE

      END subroutine

! ======================================================================
! SUBROUTINE EROS2
! EROSION SECOND PART : put eroded sediment in suspension

      SUBROUTINE EROS2(TAUCE,D,RHOW,TAU0,NBCONC,CONC,EMASS,TCONC2)

      IMPLICIT NONE

! **** INPUT VARIABLES ****
      DOUBLE PRECISION TAUCE   ! critical erosion stress at depth ZS (Pa)
      DOUBLE PRECISION D       !water depth (m)                          user input
      DOUBLE PRECISION RHOW    ! density of fluid (water)  (kg/m**3)      user input
      DOUBLE PRECISION TAU0    ! effective skin friction shear stress (Pa)
      DOUBLE PRECISION EMASS   ! eroded mass (kg)
      INTEGER NBCONC           ! number of Ws classes = elements in CONC and in WSI
! **** OUTPUT VARIABLES ****
      DOUBLE PRECISION CONC(NBCONC) !array of suspended sediment concentration (kg/m3)
                               ! for each Ws class,               user input
                               ! Ws of each class is in WSI(i) (common block common WSCLASS)
      DOUBLE PRECISION TCONC2  ! total SSC (sum of CONC) after deposition (kg/m**3)
! **** INTERMEDIATE VARIABLES ****
      DOUBLE PRECISION WS      ! settling velocity of the considered floc class (m/s)
      INTEGER WSERODED         ! largest Ws class put in suspension + 1
      INTEGER I,J              ! counter for loops

! Determine Ws of suspended sediment, WSERODED is median+1 Ws index
      DO 310, WSERODED=WSCLAY+1,NBCONC
! Find the first Ws class that cannot be lifted
         WS=WSI(MIN(WSERODED+NDISTR-MEDDISTR,NBCONC)) ! Ws of coarsest of distribution
         IF (RHOW*0.64D0*WS**2.GT.TAU0) GOTO 320
! Find the first Ws class that is disrupted
         WS=WSI(WSERODED)  ! Ws of median of distribution
         IF (WS .GE. TAUCE/(TAU0**0.5d0) * CDISRUPT) GOTO 320
310   CONTINUE
      WSERODED=NBCONC+1

320   CONTINUE
!CC      WSERODED=WSERODED-3
      DO 330,I=1,NDISTR
         J=WSERODED-MEDDISTR+I-1
         J=MAX(MIN(J,NBCONC),1)  ! put J in the range 1:NBCONC
         CONC(J)=CONC(J)+EMASS/D*DISTR(I)
330   CONTINUE

! compute the total SSC after the erosion (to be used for the sediment transport)
      TCONC2=0
      DO 390 I=1,NBCONC
         TCONC2=TCONC2+CONC(I)
390   CONTINUE

      END subroutine

! ======================================================================
! SUBROUTINE DRAGRED
! Calculate the drag reduction as function of SSC. Calculate the
! maximum erodable mass so that TAU0 is halfed because of drag reduction

      SUBROUTINE DRAGRED(TAU0,TCONC1,D,MAXEMASS)

      IMPLICIT NONE

      DOUBLE PRECISION TCONC1	! total SSC (sum of CONC) before deposition (kg/m**3)
      DOUBLE PRECISION MAXEMASS	! Maximum erodable sediment mass to avoid problem with drag reduction
      DOUBLE PRECISION TAU0	! effective skin friction shear stress (Pa)
      DOUBLE PRECISION D	! water depth (m)                          user input

      IF(CDRAGRED.LT.0)THEN
         TAU0=TAU0*EXP(CDRAGRED*TCONC1)
         MAXEMASS=D*(-0.693147)/CDRAGRED   ! -0.693147 = LOG(0.5)
      ELSEIF(CDRAGRED.EQ.0)THEN
         MAXEMASS=1d10		! no limitations if no drag reduction
      ENDIF

      END subroutine
 
! ======================================================================
! SUBROUTINE SOLULVA
! CALCULATE SOLID TRANSMITTED STRESS DUE TO ULVA: TAOS
! Uses P. Cozette's formulae (MSc thesis, 2000, SOC)

      SUBROUTINE SOLULVA(TAU0,AULVA,TAOS)

      IMPLICIT NONE

      DOUBLE PRECISION TAU0	! effective skin friction shear stress (Pa)
      DOUBLE PRECISION AULVA	! fraction of bed area covered by the algae 'Ulva' (range 0-1)
      DOUBLE PRECISION TAOS	! solid transmitted stress due to Ulva (Pa)

      IF (AULVA.LE.0.0) THEN	! Compute nothing if there is no Ulva
         TAOS=0.0d0
      ELSE
         IF(TAU0.LE.TMULVA .OR. TAU0.GE.TRULVA) THEN
            TAOS=0.0d0        ! either no motion of Ulva, or full resuspension
         ELSEIF(TAU0.LE.0.8d0*TRULVA) THEN
            TAOS=AULVA*CSULVA*(TAU0-TMULVA) ! normal case
         ELSE
            TAOS=AULVA*CSULVA*(TRULVA-TAU0)/(0.2d0*TRULVA)*
     &       (0.8d0*TRULVA-TMULVA) ! partial resusponsion
         ENDIF
! CALCULATE NEW TOTAL SHEAR STRESS
         TAU0=TAU0+TAOS
      ENDIF
! Save TAU0 to TAU0EFF, which is return in common block
      TAU0EFF=TAU0

      END subroutine

! ======================================================================
! SUBROUTINE COMPACT
!
! Compute the compaction and the consolidation of the bed
! for a give time duration (TIMEDR).
! If necessary or advantageous, simplify the bed structure.
! BEDCHA is input and output argument, containing the bed characteristics.

      SUBROUTINE COMPACT(BEDCHA,MAXBED,TIMEDR,ZS)

      IMPLICIT NONE
! input/output variables
      INTEGER MAXBED          ! number of rows in variable BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                              ! (1) DEPTH BELOW SEDIMENT SURFACE (m)
                              ! (2) CRITICAL EROSION STRESS (Pa)
                              ! (3) DENSITY (kg/m**3)
      DOUBLE PRECISION TIMEDR ! Duration of call to Sedtrans)
      DOUBLE PRECISION ZS     ! change in height of bed surface (m)
                              ! positive when bed compacted

! intermediate variables
      INTEGER HIMAXBED
      PARAMETER (HIMAXBED=50) ! Highest possible value for MAXBED
      DOUBLE PRECISION OLDRHO(HIMAXBED)! old densities (column 3 of BEDCHA)
      DOUBLE PRECISION DZOLD(HIMAXBED) ! new height a bed layer
      DOUBLE PRECISION ABOVE(HIMAXBED) ! mass of sediment above (kg/m**2)
      DOUBLE PRECISION DZNEW  ! new height of a bed layer
      DOUBLE PRECISION FINAL  ! final density for a given over-weight
      DOUBLE PRECISION TAUCE  ! new computed erosion threshold (Pa)
      INTEGER NBED            ! number of rows in BEDCHA that are used
      INTEGER IB              ! counter for loops in BEDCHA

! Find the number of rows that are used (NBED). This is indicated by z=0 in
! row after the last used row.
      ABOVE(1)=0 ! mass above the surface is 0
      DO 10, IB=2,MAXBED
         IF (BEDCHA(IB,1).EQ.0.0) GOTO 20
         ! compute the mass above depth Z : ABOVE(IB-1) + dZ * mean RHOS
         ABOVE(IB)=ABOVE(IB-1) + (BEDCHA(IB,1)-BEDCHA(IB-1,1))*
     &               0.5d0*(BEDCHA(IB,3)+BEDCHA(IB-1,3))
         DZOLD(IB)=BEDCHA(IB,1)-BEDCHA(IB-1,1)
10    CONTINUE
20    NBED=IB-1

! Compute the new bed densities = ACTUAL COMPACTION
      DO 110, IB=1,NBED
         OLDRHO(IB)=BEDCHA(IB,3)
! Compute the final density for this overlaying mass
!     FINAL(I) = A -  B * EXP(-C*ABOVE(I)) - D * EXP(-E*ABOVE(I))
!     A : final deep density
!     A-B-D : final surface density
!     C and E : define the shape (in conjunction with B and C)
         FINAL=DPROFA-DPROFB*EXP(-DPROFC*ABOVE(IB))
     &            -DPROFD*EXP(-DPROFE*ABOVE(IB))
         IF (FINAL.GT.BEDCHA(IB,3)) THEN
                       ! do nothing if already more consolidated than final

! exonential decrease of difference between density and final density
! (RHO2-RHOfinal) = (RHO1-RHOfinal) * exp(-a*dt)
            BEDCHA(IB,3)=FINAL +
     &         (BEDCHA(IB,3)-FINAL) * exp(-CONSOA*TIMEDR)
         ENDIF
! Compute the new Erosion threshold TAUCE
! The same formula is used for the freshly deposited sediment in BEDDEP
! If the computed TAUCE is smaller than the previous one, keep the previous one

         TAUCE=TEROA*(BEDCHA(IB,3)**TEROB)*
     &         (1+TEROC*(1-EXP(TEROD*ABOVE(IB))))
         BEDCHA(IB,2)=DMAX1(BEDCHA(IB,2),TAUCE)

110   CONTINUE

! Adapt the depth Z to the new densities by conserving the sediment mass
      DO 200, IB=2,NBED
         DZNEW=DZOLD(IB)*
     &      ((OLDRHO(IB)+OLDRHO(IB-1)) / (BEDCHA(IB,3)+BEDCHA(IB-1,3)))
         ZS=ZS+(DZOLD(IB)-DZNEW) ! ZS positif for consolidation/compaction
         BEDCHA(IB,1)=BEDCHA(IB-1,1)+DZNEW
200   CONTINUE

! ??????????????????????????????????????????????????????????????????????
! Reorganize BEDCHA if too much rows are used or the difference between
! the rows are too small


      END subroutine  ! end of subroutine COMPACT



! ======================================================================
! ======================================================================
! FUNCTION CHECKBEDCHA
!
! Check if the array BEDCHA follows the rules and contains plausible values.
! If found correct, returns .TRUE., otherwise returns .FALSE.

      LOGICAL FUNCTION CHECKBEDCHA(BEDCHA,MAXBED)
      IMPLICIT NONE
      INTEGER MAXBED          ! upper bound of first dimension of BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                              ! (1) depth below sediment surface (m)
                              ! (2) critical erosion stress (Pa)
                              ! (3) dry bulk density (kg/m**3)          user input
      INTEGER HIMAXBED
      PARAMETER (HIMAXBED=50) ! Highest possible value for MAXBED
      INTEGER NBED, I

      IF (MAXBED.LT.3 .OR. MAXBED.GT.HIMAXBED) GOTO 90
         ! MAXBED must be in the range 3-50

      IF (BEDCHA(1,1).NE.0.0) GOTO 90  ! First entry must be the sediment surface

! Find the number of rows that are used (NBED)
      DO 10, I=2,MAXBED
         IF (BEDCHA(I,1).EQ.0.0) GOTO 20 ! A zero depth indicate the last entry
         IF (BEDCHA(I,1).LT.BEDCHA(I-1,1)) GOTO 90 ! Z must increase monotonously
10    CONTINUE
20    NBED=I-1

! Check that the values of TAUCE and RHOS are plausible
!   0 < TAUCE   and    0 < RHOS < 4000 kg/m**3
      DO 30, I=1,NBED
         IF (BEDCHA(I,2).LE.0 .OR. BEDCHA(I,3).LE.0 .OR.
     &      BEDCHA(I,3).GE.4000) GOTO 90
30    CONTINUE

! Check that all z after NBED are set to 0
      DO 40, I=NBED+1,MAXBED
         IF (BEDCHA(I,1).NE.0) GOTO 90
40    CONTINUE

      CHECKBEDCHA=.TRUE.    ! Found no error, return true
      RETURN

90    CHECKBEDCHA=.FALSE.   ! Found a error, return false
      END function  ! end of function CHECKBEDCHA



! ======================================================================
! ======================================================================
! SUBROUTINE BEDINI
!
! Compute the initial bed characterics BEDCHA for a given surface density
! or critical erosion stress RHOSI
! Depending of the input range of RHOSI, RHOSI has different meaning:
!    0-50 : critical erosion stress (Pa)
!    >50  : dry bulk density of the surface (kg/m**3)

      SUBROUTINE BEDINI(BEDCHA,MAXBED,RHOSI)

      IMPLICIT NONE

! input variables
      INTEGER MAXBED         ! number of rows in variable BEDCHA
      DOUBLE PRECISION RHOSI ! at input, wet/dry density or TAUCE
! output variable
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
                              ! (1) DEPTH BELOW SEDIMENT SURFACE (m)
                              ! (2) CRITICAL EROSION STRESS (Pa)
                              ! (3) DENSITY (kg/m**3)

! intermediate variables
      DOUBLE PRECISION RHOSS  ! dry bulk density of surface (computed from RHOSI)
      INTEGER NBED            ! number of rows in BEDCHA that are used
      INTEGER IB              ! counter for loops in BEDCHA
      DOUBLE PRECISION A,B,C,D,E ! constant for density profile
      DOUBLE PRECISION RHOST  ! ideal surface density according constant DPROFA-E
      DOUBLE PRECISION M1,M2,MMID,ABOVE,DZ,DZEST ! for numerical iteration of densities
      INTEGER N

      IF (RHOSI.LE.50) THEN
! convert critical erosion stress to dry bulk density
! TAUCE=A*(RHOdry**B)
         RHOSS=(RHOSI/TEROA)**(1d0/TEROB)
      ELSE
         RHOSS=RHOSI
      ENDIF

! Define the constant for the density profile
      RHOST=DPROFA-DPROFB-DPROFD
      IF(RHOSS.LE.RHOST) THEN
         A=DPROFA
         B=DPROFB+(RHOST-RHOSS)*DPROFB/(DPROFB+DPROFD)
         C=DPROFC*B/DPROFB
         IF(DPROFD.NE.0)THEN
            D=DPROFD+(RHOST-RHOSS)*DPROFD/(DPROFB+DPROFD)
            E=DPROFE*D/DPROFD
         ELSE
            D=0
            E=0
         ENDIF
      ELSE
         IF(RHOSS.LT.1800) THEN
            A=(RHOSS-RHOST)*(1800d0-DPROFA)/(1800d0-RHOST)+DPROFA
            IF(A.GT.1800) A=1800d0
            B=(A-RHOSS)*DPROFB/(DPROFB+DPROFD)
            C=DPROFC*B/DPROFB
            IF(DPROFD.NE.0)THEN
               D=(A-RHOSS)*DPROFD/(DPROFB+DPROFD)
               E=DPROFE*D/DPROFD
            ELSE
               D=0
               E=0
            ENDIF
         ELSE
            A=RHOSS
            B=0
            C=0
            D=0
            E=0
         ENDIF
      ENDIF

! set the depth depending of the size of BEDCHA
      BEDCHA(1,1)=0
      IF (MAXBED.EQ.3) THEN
         NBED=2
         BEDCHA(2,1)=3d-3
      ELSEIF (MAXBED.GE.4 .AND. MAXBED.LE.5) THEN
         NBED=3
         BEDCHA(2,1)=2d-3
         BEDCHA(3,1)=10d-3
      ELSEIF (MAXBED.GE.6 .AND. MAXBED.LE.7) THEN
         NBED=4
         BEDCHA(2,1)=1d-3
         BEDCHA(3,1)=5d-3
         BEDCHA(4,1)=20d-3
      ELSEIF (MAXBED.GE.8) THEN
         NBED=6
         BEDCHA(2,1)=2d-2
         BEDCHA(3,1)=6d-2
         BEDCHA(4,1)=10d-2
         BEDCHA(5,1)=14d-2
         BEDCHA(6,1)=18d-2
      ELSE
         NBED=1  ! MAXBED must be larger than 2, therefore this should never be executed
      ENDIF

! Compute the density for each depth by numerical iterative aproximation
      ABOVE=0d0
      BEDCHA(1,3)=A-B-D
      BEDCHA(1,2)=TEROA*(BEDCHA(1,3)**TEROB)
      DO 20, IB=2,NBED
         N=0
         DZ=BEDCHA(IB,1)-BEDCHA(IB-1,1)
         M1=ABOVE+DZ*BEDCHA(IB-1,3)*0.95d0
         M2=ABOVE+DZ*BEDCHA(IB-1,3)*2d0
10       MMID=(M1+M2)/2d0
         IF(D.EQ.0) THEN
            BEDCHA(IB,3)=A-B*EXP(-C*MMID)
         ELSE
            BEDCHA(IB,3)=A-B*EXP(-C*MMID)-D*EXP(-E*MMID)
         ENDIF
         DZEST=(MMID-ABOVE)*2d0/(BEDCHA(IB,3)+BEDCHA(IB-1,3))
         n=n+1
         IF(ABS(DZ-DZEST).GT. 0.05d-3) THEN
            IF(DZEST.GT.DZ) THEN
               M2=MMID
            ELSE
               M1=MMID
            ENDIF
            GOTO 10
         ENDIF
         ABOVE=ABOVE+DZ*(BEDCHA(IB,3)+BEDCHA(IB-1,3))/2.
         BEDCHA(IB,2)=TEROA*(BEDCHA(IB,3)**TEROB)*
     &         (1+TEROC*(1-EXP(TEROD*ABOVE)))
20    CONTINUE

      DO 60, IB=NBED+1,MAXBED  ! set the rest of the table to 0
         BEDCHA(IB,1)=0d0
         BEDCHA(IB,2)=0d0
         BEDCHA(IB,3)=0d0
60    CONTINUE

      END subroutine  ! end of subroutine BEDINI

! ======================================================================
! ======================================================================
! SUBROUTINE REMOVEBED
!
! Merge one or several bed layers together be removing one or several
! rows in BEDCHA. The thickness of the bed layers is conserved. The density
! RHOdry is adjusted so to conserve the mass of sediment.
! If NB is an negative integer, this amount of rows are removed in BEDCHA.
! If NB is a positive real smaller than 0.5, then the layer thinner than
! this value (in metres) are merged.

      SUBROUTINE REMOVEBED(BEDCHA,MAXBED,NB)
      IMPLICIT NONE
! input variables
      INTEGER MAXBED         ! number of rows in variable BEDCHA
      DOUBLE PRECISION BEDCHA(MAXBED,3) ! bed characteristics in 3 column table
      DOUBLE PRECISION NB    ! parameter controlling the action of the subroutine
! intemediate variables
      INTEGER HIMAXBED
      PARAMETER (HIMAXBED=50) ! Highest possible value for MAXBED
      DOUBLE PRECISION DIFF(HIMAXBED) ! difference between interpolated
                              ! and old values for RHO
      INTEGER NBED            ! number of rows in BEDCHA that are used
      INTEGER N               ! number of rows in BEDCHA to be removed
                              ! if selection is on layer thickness, = 0

      DOUBLE PRECISION DZ     ! thickness of a layer
      DOUBLE PRECISION MINDZ  ! thickness of the thinnest layer / smallest difference
      INTEGER P               ! position of the row to be removed
      INTEGER I

      P = 0

! Find the number of rows that are used (NBED)
      DO 10, I=2,MAXBED
         IF (BEDCHA(I,1).EQ.0.0) GOTO 20 ! A zero depth indicate the last entry
10    CONTINUE
20    NBED=I-1
      IF(NBED.LT.3) RETURN    ! do nothing if there less than two layers

! Interpret the input argument NB
      IF(NB.LT.0) THEN
         N=MIN(-INT(NB-0.5d0),NBED-2)
            ! at least two entries must remains in BEDCHA
      ELSEIF(NB.EQ.0 .OR. NB.GT.0.5) THEN
         RETURN ! do nothing if NB is incorrect
      ELSE
         N=0
      ENDIF

! start of main loop (removing several rows in BEDCHA)
30    CONTINUE

! Compute how much RHOdry would change if interpolated
      DO 40, I=2,NBED-1
! for position z2, (dX-d1)/(z2-z1) = (d3-d1)/(z3-z2)
         DIFF(I)=(BEDCHA(I,1)-BEDCHA(I-1,1)) /
     &               (BEDCHA(I+1,1)-BEDCHA(I-1,1)) *
     &               (BEDCHA(I+1,3)-BEDCHA(I-1,3)) + BEDCHA(I-1,3)
     &               - BEDCHA(I,3)
40    CONTINUE

      IF(N.EQ.0) THEN
! CASE 1 : removal of the thinnest layer
! find the thinnest layer
         MINDZ=1000
         DO 50,I=NBED-1,1,-1
            DZ=BEDCHA(I+1,1)-BEDCHA(I,1)
            IF(DZ.LT.MINDZ) THEN
               P=I
               MINDZ=DZ
            ENDIF
50       CONTINUE
         IF(MINDZ.GE.NB) RETURN  ! quit if there is no (more) layer thinner than NB
! decide whether upper or limit of layer has to be removed
         IF(P.EQ.1 .OR.
     &   ( DIFF(P).GE.DIFF(P+1) .AND. P+1.LT.NBED ) ) THEN
            P=P+1
         ENDIF

      ELSE
! CASE 2 : removal of the best interpolated point
! find the best interpolated point
         P=NBED-1
         DO 60,I=NBED-2,2,-1
            IF(DIFF(I).LT.DIFF(P)) THEN
               P=I
            ENDIF
60       CONTINUE
         N=N-1
      ENDIF

! REMOVE THE ROW POSMIN
      IF(P.EQ.2) THEN
! particular case P=2, mass conservation for removing row 2
! (r1+r2)*(z2-0) + (r2+r3)*(z3-z2) = (X+r3)*(z3-0)  which can be simplified to
! X = r2 + (r1-r3)*z2/z3
         BEDCHA(1,3)=BEDCHA(2,3) + (BEDCHA(1,3)-BEDCHA(3,3))*
     &         BEDCHA(2,1)/BEDCHA(3,1)
      ELSE
! general case : mass conservation for removing row 3
! (r1+r2)*(z2-z1) + (r2+r3)*(z3-z2) + (r3+r4)*(z4-z3) =
!           (r1+X)*(z2-z1) + (X+r4)*(z4-z2)     which can be simplified to
! X = ( r2*(z3-z1) + r3*(z4-z2) + r4*(z2-z3) ) / (z4-z1)
         BEDCHA(P-1,3)=( BEDCHA(P-1,3)*(BEDCHA(P,1)-BEDCHA(P-2,1)) +
     &         BEDCHA(P,3)*(BEDCHA(P+1,1)-BEDCHA(P-1,1)) +
     &         BEDCHA(P+1,3)*(BEDCHA(P-1,1)-BEDCHA(P,1)) ) /
     &         (BEDCHA(P+1,1)-BEDCHA(P-2,1))
      ENDIF
! reshuffle the rows
      DO 70, I=P,NBED-1
         BEDCHA(I,1)=BEDCHA(I+1,1)
         BEDCHA(I,2)=BEDCHA(I+1,2)
         BEDCHA(I,3)=BEDCHA(I+1,3)
70    CONTINUE
      BEDCHA(NBED,1)=0d0
      NBED=NBED-1

      IF (NB.GT.0 .OR. N.GT.0) GOTO 30
! End of main loop (removing several rows in BEDCHA)

      END subroutine   ! end of subroutine REMOVEBED



!=======================================================================
!=======================================================================
! Subroutine SETCCONST
! Modify the default parameters of the common block MCONST by reading
! the values new values in the file FILENAME.
! INICONST must be called before any call to SETCCONST.
! A parameter remains unchanged if the new value is -999.
! No errror checks are done on the new values !
! If FILEMAME=' ', then write the example file INDATA.CST
! If successful, then the modified parameters are written in the text
! file opened as unit OUTUNIT
!
      SUBROUTINE SETCCONST(FILENAME,OUTUNIT)

      IMPLICIT NONE

      CHARACTER FILENAME*(*)
      INTEGER OUTUNIT

      LOGICAL EX
      CHARACTER NAME*30,CH*300
      INTEGER UN              ! file unit used for reading/writing
      INTEGER I
      DOUBLE PRECISION
     &XCSULVA,XTMULVA,XTRULVA,XRKERO,XE0,XCDISRUPT,XCLIM1,XCLIM2,
     &XKFLOC,XMFLOC,XRHOCLAY,XCTAUDEP,XPRS,XRHOMUD,XDPROFA,XDPROFB,
     &XDPROFC,XDPROFD,XDPROFE,XCONSOA,XTEROA,XTEROB,XTEROC,
     &XTEROD,XCDRAGRED,XZ0COH,XFCWCOH
      INTEGER XWSCLAY,XDOCOMPACT

      DO 10,UN=20,63
         INQUIRE(UNIT=UN,OPENED=EX)
         IF(.NOT.EX)GOTO 20
10    CONTINUE
      STOP
20    CONTINUE

      IF(FILENAME.EQ.' ')THEN
         NAME='INDATA.CST'
         INQUIRE(FILE=NAME, EXIST=EX)
         IF (EX) THEN
            WRITE(*,100) NAME(1:LASTCH(NAME))
            READ(*,*) CH
            IF (CH.NE.'Y' .AND. CH.NE.'y') THEN
               WRITE(*,101) NAME(1:LASTCH(NAME))
               STOP
            ENDIF
         ENDIF
         OPEN(UN,FILE=NAME,ERR=200)
         WRITE(UN,120,ERR=200)
         WRITE(CH,121)
         CALL LINETAB(CH)
         WRITE(UN,'(A)',ERR=200)CH
         WRITE(CH,*)(-999.,I=1,27),-999,-999
         CALL LINETAB(CH)
         WRITE(UN,'(A)',ERR=200)CH
         CLOSE(UN)
      ELSE
         OPEN(UN,FILE=FILENAME,ERR=210)
30       READ(UN,*,ERR=930,END=210)
     &   XCSULVA,XTMULVA,XTRULVA,XRKERO,XE0,XCDISRUPT,XCLIM1,XCLIM2,
     &   XKFLOC,XMFLOC,XRHOCLAY,XCTAUDEP,XPRS,XRHOMUD,XDPROFA,XDPROFB,
     &   XDPROFC,XDPROFD,XDPROFE,XCONSOA,XTEROA,XTEROB,XTEROC,
     &   XTEROD,XCDRAGRED,XZ0COH,XFCWCOH,XWSCLAY,XDOCOMPACT

         WRITE(OUTUNIT,140)
         IF(XCSULVA.NE.-999.)THEN
            CSULVA=XCSULVA
            WRITE(OUTUNIT,141)'CSULVA',XCSULVA
         ENDIF
         IF(XTMULVA.NE.-999.)THEN
            TMULVA=XTMULVA
            WRITE(OUTUNIT,141)'TMULVA',XTMULVA
         ENDIF
         IF(XTRULVA.NE.-999.)THEN
            TRULVA=XTRULVA
            WRITE(OUTUNIT,141)'TRULVA',XTRULVA
         ENDIF
         IF(XRKERO.NE.-999.)THEN
            RKERO=XRKERO
            WRITE(OUTUNIT,141)'RKERO',XRKERO
         ENDIF
         IF(XE0.NE.-999.)THEN
            E0=XE0
            WRITE(OUTUNIT,141)'E0',XE0
         ENDIF
         IF(XCDISRUPT.NE.-999.)THEN
            CDISRUPT=XCDISRUPT
            WRITE(OUTUNIT,141)'CDISRUPT',XCDISRUPT
         ENDIF
         IF(XCLIM1.NE.-999.)THEN
            CLIM1=XCLIM1
            WRITE(OUTUNIT,141)'CLIM1',XCLIM1
         ENDIF
         IF(XCLIM2.NE.-999.)THEN
            CLIM2=XCLIM2
            WRITE(OUTUNIT,141)'CLIM2',XCLIM2
         ENDIF
         IF(XKFLOC.NE.-999.)THEN
            KFLOC=XKFLOC
            WRITE(OUTUNIT,141)'KFLOC',XKFLOC
         ENDIF
         IF(XMFLOC.NE.-999.)THEN
            MFLOC=XMFLOC
            WRITE(OUTUNIT,141)'MFLOC',XMFLOC
         ENDIF
         IF(XRHOCLAY.NE.-999.)THEN
            RHOCLAY=XRHOCLAY
            WRITE(OUTUNIT,141)'RHOCLAY',XRHOCLAY
         ENDIF
         IF(XCTAUDEP.NE.-999.)THEN
            CTAUDEP=XCTAUDEP
            WRITE(OUTUNIT,141)'CTAUDEP',XCTAUDEP
         ENDIF
         IF(XPRS.NE.-999.)THEN
            PRS=XPRS
            WRITE(OUTUNIT,141)'PRS',XPRS
         ENDIF
         IF(XRHOMUD.NE.-999.)THEN
            RHOMUD=XRHOMUD
            WRITE(OUTUNIT,141)'RHOMUD',XRHOMUD
         ENDIF
         IF(XDPROFA.NE.-999.)THEN
            DPROFA=XDPROFA
            WRITE(OUTUNIT,141)'DPROFA',XDPROFA
         ENDIF
         IF(XDPROFB.NE.-999.)THEN
            DPROFB=XDPROFB
            WRITE(OUTUNIT,141)'DPROFB',XDPROFB
         ENDIF
         IF(XDPROFC.NE.-999.)THEN
            DPROFC=XDPROFC
            WRITE(OUTUNIT,141)'DPROFC',XDPROFC
         ENDIF
         IF(XDPROFD.NE.-999.)THEN
            DPROFD=XDPROFD
            WRITE(OUTUNIT,141)'DPROFD',XDPROFD
         ENDIF
         IF(XDPROFE.NE.-999.)THEN
            DPROFE=XDPROFE
            WRITE(OUTUNIT,141)'DPROFE',XDPROFE
         ENDIF
         IF(XCONSOA.NE.-999.)THEN
            CONSOA=XCONSOA
            WRITE(OUTUNIT,141)'CONSOA',XCONSOA
         ENDIF
         IF(XTEROA.NE.-999.)THEN
            TEROA=XTEROA
            WRITE(OUTUNIT,141)'TEROA',XTEROA
         ENDIF
         IF(XTEROB.NE.-999.)THEN
            TEROB=XTEROB
            WRITE(OUTUNIT,141)'TEROB',XTEROB
         ENDIF
         IF(XTEROC.NE.-999.)THEN
            TEROC=XTEROC
            WRITE(OUTUNIT,141)'TEROC',XTEROC
         ENDIF
         IF(XTEROD.NE.-999.)THEN
            TEROD=XTEROD
            WRITE(OUTUNIT,141)'TEROD',XTEROD
         ENDIF
         IF(XCDRAGRED.NE.-999.)THEN
            CDRAGRED=XCDRAGRED
            WRITE(OUTUNIT,141)'CDRAGRED',XCDRAGRED
         ENDIF
         IF(XZ0COH.NE.-999.)THEN
            Z0COH=XZ0COH
            WRITE(OUTUNIT,141)'Z0COH',XZ0COH
         ENDIF
         IF(XFCWCOH.NE.-999.)THEN
            FCWCOH=XFCWCOH
            WRITE(OUTUNIT,141)'FCWCOH',XFCWCOH
         ENDIF
         IF(XWSCLAY.NE.-999)THEN
            WSCLAY=XWSCLAY
            WRITE(OUTUNIT,142)'WSCLAY',XWSCLAY
         ENDIF
         IF(XDOCOMPACT.NE.-999)THEN
            DOCOMPACT=XDOCOMPACT
            WRITE(OUTUNIT,142)'DOCOMPACT',XDOCOMPACT
         ENDIF

         WRITE(OUTUNIT,'(/)')
      ENDIF
      RETURN

200   WRITE(*,102) NAME(1:LASTCH(NAME))
      CLOSE (UN)
      STOP
210   WRITE(*,102) FILENAME(1:LASTCH(FILENAME))
      CLOSE (UN)
      STOP

100   FORMAT (/'The existing file ',A,' will be overwritten, '/,
     &'do you want to continue ? (Y/N)')
101   FORMAT (/'Generation of file ',A,' aborted.')
102   FORMAT (/'Error during opening/writing file ',A,/)

120   FORMAT ('Example of input file for Sedtrans05 to modify the ',
     &'default values of the cohesive parameters.'/,
     &'Parameters with "-999" will keep their standard value.' )
121   FORMAT('CSULVA TMULVA TRULVA RKERO E0 CDISRUPT CLIM1 CLIM2 ',
     &'KFLOC MFLOC ',
     &'RHOCLAY CTAUDEP PRS RHOMUD DPROFA DPROFB DPROFC DPROFD DPROFE ',
     &'CONSOA TEROA TEROB TEROC TEROD CDRAGRED Z0COH FCWCOH ',
     &'WSCLAY DOCOMPACT')
140   FORMAT('Following default parameters for the cohesive algorithm',
     &' were modified')
141   FORMAT(T10,A,' was set to',G12.5)
142   FORMAT(T10,A,' was set to',I4)
930   stop 'error stop reading...'

      END subroutine   ! end of subroutine SETCCONST



! ======================================================================
! ======================================================================
! FUNCTION LASTCH
!
! Return the position of the last non space character of a string.
! Return 1 for an empty string
!
      INTEGER FUNCTION LASTCH(CH)
      CHARACTER CH*(*)
      INTEGER I
      DO 10, I=LEN(CH),2,-1
         IF (CH(I:I).NE.' ') GOTO 20
10    CONTINUE
20    LASTCH=I
      END function

! ======================================================================
! ======================================================================
!  SUBROUTINE LINETAB
!
! Replace in a string all successive spaces with a tab
!
      SUBROUTINE LINETAB(CH)
      IMPLICIT NONE
      CHARACTER CH*(*),TAB
      INTEGER LENGTH,A,B
      LENGTH=LASTCH(CH)
      TAB=CHAR(9)
      A=0
      B=0

10    CONTINUE
         A=A+1
         B=B+1
         IF(CH(B:B).EQ.' ') THEN
            IF(B.NE.1) THEN
               CH(A:A)=TAB
            ELSE
               A=0
            ENDIF

20          CONTINUE
            IF(B.LT.LENGTH) THEN
               IF(CH(B+1:B+1).EQ.' ') THEN
                  B=B+1
                  GOTO 20
               ENDIF
            ENDIF

         ELSE
            CH(A:A)=CH(B:B)
         ENDIF
      IF(B.LT.LENGTH) GOTO 10

      IF(A.LT.LENGTH) CH(A+1:LENGTH)=' '
      END subroutine     ! end of subroutine LINETAB



! ======================================================================
! ======================================================================
! SUBROUTINE DENSVISC
!
! Compute the density and the dynamic visocity of water from the temperature
! and the salinity
      SUBROUTINE DENSVISC(TEMP,SALIN,RHOW,VISC)
      DOUBLE PRECISION TEMP   ! in degrees Celsius
      DOUBLE PRECISION SALIN  ! practical salinity scale (seawater=35)
      DOUBLE PRECISION RHOW   ! density of the (sea)water  (KG/M**3)
      DOUBLE PRECISION VISC   ! dynamic viscosity of the (sea)water (KG/M*SEC (OR N.S/M**2))

! compute the dynamic/molecular viscosity
      VISC=1.802863d-3 - 6.1086d-5*TEMP + 1.31419d-06*TEMP**2 -
     &1.35576d-08*TEMP**3 + 2.15123d-06*SALIN + 3.59406d-11*SALIN**2

! compute the water density according to Brydon et al. 1999, J. Geoph. Res.
! 104/C1, 1537-1540, equation 2 with Coefficient of Table 4, without pressure
! component. Ranges TEMP -2 - 40C, S 0-42, surface water.
!      RHOW=9.20601d-2 + 5.10768d-2*TEMP + 8.05999d-1*SALIN
!     &     -7.40849d-3*TEMP**2 - 3.01036d-3*SALIN*TEMP +
!     %     3.32267d-5*TEMP**3 + 3.21931d-5*SALIN*TEMP**2
!      RHOW=RHOW+1000d0

! compute the water density according to EOS80, Fofonoff 198599,
! J. Geoph. Res. 90/C2, 3332-3342, without pressure component.
      RHOW=999.842594d0 +6.793952d-2*TEMP -9.095290d-3*TEMP**2
     &   +1.00168d-4*TEMP**3 -1.120083d-6*TEMP**4 +6.536332d-9*TEMP**5
     & +(8.24493d-1 -4.0899d-3*TEMP +7.6438d-5*TEMP**2
     &   -8.2467d-7*TEMP**3 +5.3875d-9*TEMP**4) * SALIN
     & +(-5.72466d-3 +1.0227d-4*TEMP -1.6546d-6*TEMP**2) * SALIN**1.5d0
     & +4.8314d-4*SALIN**2

      END subroutine      ! end of subroutine DENSVISC

! ======================================================================
! ======================================================================
! SUBROUTINE INICONST
!
! Must be called before the execution of the COHESIVE subroutine, except
! if ALL constants are assigned separately.
!
      SUBROUTINE INICONST(NBCONC)

      IMPLICIT NONE

      INTEGER NBCONC           ! number of Ws classes = elements in CONC and in WSI
! intermediate variable to construct WSI and DISTR
      INTEGER I
      DOUBLE PRECISION WSTART    ! slowest Ws
      DOUBLE PRECISION DWSLOG    ! LOG10 of ratio of two successive Ws
      DOUBLE PRECISION STDEV     ! standard deviation of log10 normal distribution
      DOUBLE PRECISION A,MEAN,SUMNORM

      ALLOCATE(WSI(NBCONC))	
      ALLOCATE(DISTR(NBCONC))	

! Ws of each Ws class, depending of number of Ws classes (NBCONC) : WSI
      WSTART=1d-5
      IF(NBCONC.GT.33 .OR. NBCONC.LT.5)THEN
         WRITE(*,*)'Incorrect value of NBCONC',NBCONC
         STOP
      ELSEIF(NBCONC.GE.25) THEN
         DWSLOG=0.125d0
      ELSEIF(NBCONC.GE.22) THEN
         DWSLOG=1d0/6d0
      ELSEIF(NBCONC.GE.15) THEN
         DWSLOG=0.2d0
         IF (NBCONC.EQ.15) WSTART=10d0**(-4.8d0)
      ELSEIF(NBCONC.GE.11) THEN
         DWSLOG=0.25d0
         IF (NBCONC.LE.12) WSTART=10d0**(-4.75d0)
      ELSEIF(NBCONC.GE.8) THEN
         DWSLOG=LOG10(2d0)
         IF (NBCONC.GT.8) THEN
            WSTART=0.00002d0
         ELSE
            WSTART=0.00004d0
         ENDIF
      ELSEIF(NBCONC.EQ.7) THEN
         DWSLOG=LOG10(2.5d0)
         WSTART=0.00001d0	!ccf
         WSTART=0.00002d0
!         WSTART=0.00003d0	!ccf
      ELSE
         DWSLOG=LOG10(3d0)
         IF (NBCONC.GT.5) THEN
            WSTART=0.00002d0
         ELSE
            WSTART=0.00005d0
         ENDIF
      ENDIF
      DO 100,I=1,NBCONC
100      WSI(I)=WSTART * 10d0**(DBLE(I-1)*DWSLOG)

! Shape of Ws distribution put in suspension : DISTR
! compute normal distribution and scale it so that sum(DISTR)=1
!   normal = 1/(STDEV*sqrt(2*PI) * exp( -0.5*((x-MEAN)/STDEV)**2)
      STDEV=0.3d0  ! standard deviation of LOG10(Ws)
      NDISTR=MIN(2*INT(0.7/DWSLOG+0.3)+1,15) ! number of used elements in DISTR
      MEDDISTR=(NDISTR+1)/2
      MEAN=MEDDISTR*DWSLOG
      A=1/(STDEV*SQRT(2d0*2d0*ACOS(0d0)))
      SUMNORM=0d0
      DO 210,I=1,NDISTR
         DISTR(I)=A*EXP(-0.5d0*((I*DWSLOG-MEAN)/STDEV)**2) * DWSLOG
         SUMNORM=SUMNORM+DISTR(I)
210   CONTINUE
      DO 220,I=1,NDISTR
         DISTR(I)=DISTR(I)/SUMNORM
220   CONTINUE

! ----------------------------------------------------------------
      END subroutine   ! end of subroutine INICONST

!************************************************************

      END MODULE mod_sedtrans05

